using System;
using SCG = System.Collections.Generic;

using Nemerle.Assertions;
using Nemerle.Utility;
using Nemerle.Collections;
using Nemerle.Compiler;

using PT = Nemerle.Compiler.Parsetree;
using PEG = Nemerle.Peg;
using CS = CSharpParser;

namespace CSharpToNemerle
{
  public partial class AstConverter
  {
    public this(manager : ManagerClass)
    {
      this.manager = manager;
      this.isLastStatement = true;
      this.isIntoExpression = false;
      this.labelScope = LabelScope();
    }

    private this( manager           : ManagerClass,
                  currentEnv        : GlobalEnv,
                  typeDecls         : SCG.List[PT.TopDeclaration], 
                  isLastStatement   : bool,
                  isIntoExpression  : bool,
                  labelScope        : LabelScope)
    {
      this.manager = manager;
      this.currentEnv = currentEnv;
      this.typeDecls = typeDecls;
      this.isLastStatement = isLastStatement;
      this.isIntoExpression = isIntoExpression;
      this.labelScope = labelScope;
    }

    private WithLastStatement(value : bool) : AstConverter
    {
      if(isLastStatement == value)
        this
      else
        AstConverter(
          manager = this.manager,
          currentEnv = this.currentEnv,
          typeDecls = this.typeDecls,
          isLastStatement = value,
          isIntoExpression = this.isIntoExpression,
          labelScope = this.labelScope)
    }

    private WithIntoExpression(value : bool) : AstConverter
    {
      if(isIntoExpression == value)
        this
      else
        AstConverter(
          manager = this.manager,
          currentEnv = this.currentEnv,
          typeDecls = this.typeDecls,
          isLastStatement = this.isLastStatement,
          isIntoExpression = value,
          labelScope = this.labelScope)
    }

    private WithNewLabelScope() : AstConverter
    {
      AstConverter(
        manager = this.manager,
        currentEnv = this.currentEnv,
        typeDecls = this.typeDecls,
        isLastStatement = this.isLastStatement,
        isIntoExpression = this.isIntoExpression,
        labelScope = LabelScope())
    }

    manager : ManagerClass;

    [RecordIgnore] mutable currentEnv : GlobalEnv;

    [RecordIgnore] mutable typeDecls : SCG.List[PT.TopDeclaration];

    isLastStatement : bool;

    isIntoExpression : bool;

    labelScope : LabelScope;

    public Convert(compilationUnit : CS.CompilationUnit) : list[PT.TopDeclaration]
    {
      manager.MacroColors.PushUseSiteColor();
      try {
        typeDecls = SCG.List();
        currentEnv = manager.CoreEnv;
        ConvertExternAliases(compilationUnit.ExternAliases);
        ConvertUsingDirectives(compilationUnit.UsingDirectives);
        ConvertGlobalAttributes(compilationUnit.GlobalAttributes);
        ConvertNamespaceNodes(compilationUnit.Members);
        NList.ToList(typeDecls)
      } finally {
        manager.MacroColors.PopColor();
      }
    }
  }
}