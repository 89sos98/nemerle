using System;
using SCG = System.Collections.Generic;

using Nemerle.Assertions;
using Nemerle.Collections;
using Nemerle.Utility;
using Nemerle.Peg;
using LRPEGCC;

namespace CSharpParser
{
  public partial class Parser
  {
    #region Function arguments

    //argumentName    : Identifier                = identifier ":" !":" s; // avoid conflict with alias
    argumentName( identifier : VToken[Identifier],
                  _colon : NToken, _NOT_colon : NToken, _ : NToken) : Identifier
    {
      identifier.Value
    }

    //argumentModfier : FunctionArgumentModifier  = ("ref" / "out")S;
    argumentModfier(keyword : NToken, _ : NToken) : FunctionArgumentModifier
    {
      match(keyword.GetText()) {
        | "ref" => FunctionArgumentModifier.Ref
        | "out" => FunctionArgumentModifier.Out
        | _     => throw ArgumentOutOfRangeException("keyword")
      }
    }

    //argument        : FunctionArgument          = argumentName? argumentModfier? expression;
    argument( argumentName    : option[VToken[Identifier]],
              argumentModfier : option[VToken[FunctionArgumentModifier]],
              expr            : VToken[Expr]) : FunctionArgument
    {
      FunctionArgument(
        GetLocation(_, expr),
        argumentName.WithDefault(null),
        argumentModfier.WithDefault(FunctionArgumentModifier.Normal),
        expr.Value)
    }

    //argumentList    : FunctionArgumentList      = argument (","s argument)*;
    argumentList( head : VToken[FunctionArgument],
                  tail  : SCG.List[NToken * NToken * VToken[FunctionArgument]]) : list[FunctionArgument]
    {
      head.Cons(tail)
    }

    #endregion

    #region Common expressions

    //expression          : Expr  = lambdaExpression
    //                            / queryExpression
    //                            / assignmentExpression;
    expression(opt : VToken[Expr]) : Expr
    {
      opt.Value
    }

    //expressionList      : ExprList = expression (","s expression)*;
    expressionList( head : VToken[Expr],
                    tail  : SCG.List[NToken * NToken * VToken[Expr]]) : list[Expr]
    {
      head.Cons(tail)
    }

    //constantExpression  : Expr = expression;
    constantExpression(opt : VToken[Expr]) : Expr
    {
      opt.Value
    }

    //booleanExpression  : Expr = expression;
    booleanExpression(opt : VToken[Expr]) : Expr
    {
      opt.Value
    }

    //simpleName        : Expr = identifier ("::"s identifier)? typeArgumentList?;
    simpleName( identifierOrAlias   : VToken[Identifier],
                identifier          : option[NToken * NToken * VToken[Identifier]],
                typeArgumentList    : option[VToken[list[TypeRef]]]) : Expr
    {
      def make_part(id, args) {
        def part = Expr.Ref(id.Value);
        match(args) {
          | Some(args)  => Expr.TypeArguments(part, args.Value)
          | _           => part
        }
      }

      match(identifier) {
        | Some((_, _, identifier)) => Expr.Member(Expr.Alias(identifierOrAlias.Value), make_part(identifier, typeArgumentList))
        | _ => make_part(identifierOrAlias, typeArgumentList)
      }
    }

    //literalExpression : Expr = literal;
    literalExpression(literal : VToken[Literal]) : Expr
    {
      Expr.Literal(literal.Value)
    }

    //parenthesizedExpression : Expr = "("s expression ")"s;
    parenthesizedExpression(_lparen : NToken, _ : NToken,
                            expr : VToken[Expr],
                            _rparen : NToken, _ : NToken,) : Expr
    {
      expr.Value
    }

    //thisAccess        : Expr = "this"S;
    thisAccess(_this : NToken, _ : NToken) : Expr
    {
      Expr.This()
    }

    //baseAccess        : Expr = "base"S;
    baseAccess(_base : NToken, _ : NToken) : Expr
    {
      Expr.Base()
    }

    //predefinedAccess  : Expr = predefinedType;
    predefinedAccess(predefinedType : VToken[TypeRef]) : Expr
    {
      Expr.Predefined(predefinedType.Value)
    }

    //typeofExpression        : Expr = "typeof"S "("s anyType ")"s;
    typeofExpression( _typeof : NToken, _ : NToken,
                      _lparen : NToken, _ : NToken,
                      typeRef : VToken[TypeRef],
                      _rparen : NToken, _ : NToken) : Expr
    {
      Expr.TypeOf(typeRef.Value)
    }

    //checkedExpression       : Expr = "checked"S "("s expression ")"s;
    checkedExpression(_checked  : NToken, _ : NToken,
                      _lparen   : NToken, _ : NToken,
                      expr      : VToken[Expr],
                      _rparen   : NToken, _ : NToken) : Expr
    {
      Expr.Checked(expr.Value, true)
    }

    //uncheckedExpression       : Expr = "unchecked"S "("s expression ")"s;
    uncheckedExpression(_unchecked  : NToken, _ : NToken,
                        _lparen     : NToken, _ : NToken,
                        expr        : VToken[Expr],
                        _rparen     : NToken, _ : NToken) : Expr
    {
      Expr.Checked(expr.Value, false)
    }

    //defaultValueExpression  : Expr = "default"S "("s anyType ")"s;
    defaultValueExpression( _default  : NToken, _ : NToken,
                            _lparen   : NToken, _ : NToken,
                            typeRef   : VToken[TypeRef],
                            _rparen   : NToken, _ : NToken) : Expr
    {
      Expr.DefaultValue(typeRef.Value)
    }

    //sizeofExpression        : Expr = "sizeof"S "("s anyType ")"s;
    sizeofExpression( _sizeof : NToken, _ : NToken,
                      _lparen : NToken, _ : NToken,
                      typeRef : VToken[TypeRef],
                      _rparen : NToken, _ : NToken) : Expr
    {
      Expr.SizeOf(typeRef.Value)
    }

    //otherExpression : Expr  = simpleName
    //                        / literalExpression
    //                        / parenthesizedExpression
    //                        / thisAccess
    //                        / baseAccess
    //                        / predefinedAccess
    //                        / newExpression
    //                        / typeofExpression
    //                        / checkedExpression
    //                        / uncheckedExpression
    //                        / defaultValueExpression
    //                        / anonymousMethodExpression
    //                        / sizeofExpression;
    otherExpression(opt : VToken[Expr]) : Expr
    {
      opt.Value
    }

    #endregion 

    #region New object expression

    //newObject_1   : Expr = anyNonArrayType "("s argumentList? ")"s complexInitializer?;
    newObject_1(objType   : VToken[TypeRef],
                _lparen   : NToken, _ : NToken,
                ctorArgs  : option[VToken[list[FunctionArgument]]],
                _rparen   : NToken, _ : NToken,
                init      : option[VToken[VariableInitializer]]) : Expr
    {
      Expr.NewObject(
        objType.Value,
        ctorArgs.WithDefault([]),
        init.WithDefault(null))
    }

    //newObject_2   : Expr = anyNonArrayType complexInitializer;
    newObject_2(objType   : VToken[TypeRef],
                init      : VToken[VariableInitializer]) : Expr
    {
      Expr.NewObject(
        objType.Value,
        [],
        init.Value)
    }

    //newArray_1    : Expr = anyNonArrayType "["s expressionList "]"s rankSpecifier* complexInitializer?;
    newArray_1( elementType : VToken[TypeRef],
                _lparen     : NToken, _ : NToken,
                args        : VToken[list[Expr]],
                _rparen     : NToken, _ : NToken,
                ranks       : SCG.List[VToken[TypeSpecifier]],
                init        : option[VToken[VariableInitializer]]) : Expr
    {
      mutable finalElementType = elementType.Value;
      foreach(rank in ranks) {
        match(rank.Value) {
          | TypeSpecifier.Array(dim) =>
            finalElementType = TypeRef.Array(finalElementType, dim);
          | _ => throw ArgumentOutOfRangeException("ranks")
        }
      }
      Expr.NewArray(
        finalElementType,
        args.Value,
        args.Value.Length,
        init.WithDefault(null))
    }

    //newArray_2    : Expr = anyNonArrayType rankSpecifier+ complexInitializer;
    newArray_2( elementType   : VToken[TypeRef],
                ranks         : SCG.List[VToken[TypeSpecifier]],
                init          : VToken[VariableInitializer]) : Expr
    {
      mutable finalElementType = elementType.Value;
      mutable finalDim = 0;
      foreach(rank in ranks) {
        match(rank.Value) {
          | TypeSpecifier.Array(dim) =>
            finalElementType = TypeRef.Array(finalElementType, dim);
            finalDim = dim;
          | _ => throw ArgumentOutOfRangeException("ranks")
        }
      }
      Expr.NewArray(
        finalElementType,
        [],
        finalDim,
        init.Value)
    }

    //newArray_3    : Expr = rankSpecifier complexInitializer;
    newArray_3(rank : VToken[TypeSpecifier],
               init : VToken[VariableInitializer]) : Expr
    {
      match(rank.Value) {
        | TypeSpecifier.Array(dim) =>
          Expr.NewArray(
            null,
            [],
            dim,
            init.Value)
        | _ => throw ArgumentOutOfRangeException("rank")
      }
    }

    //newAnonymous  : Expr = complexInitializer;
    newAnonymous(init : VToken[VariableInitializer]) : Expr
    {
      Expr.NewAnonymous(init.Value)
    }

    //newExpression : Expr = "new"S (newObject_1 / newArray_1 / newArray_2 / newObject_2 / newArray_3 / newAnonymous);
    newExpression(_new  : NToken, _ : NToken,
                  opt   : VToken[Expr]) : Expr
    {
      opt.Value
    }

    #endregion

    #region Delegate expression

    //explicitAnonymousFunctionParameterModifier  : FormalParameterModifier = ("ref" / "out")S;
    explicitAnonymousFunctionParameterModifier(keyword : NToken, _ : NToken) : FormalParameterModifier
    {
      match(keyword.GetText()) {
        | "ref" => FormalParameterModifier.Ref
        | "out" => FormalParameterModifier.Out
        | _     => throw ArgumentOutOfRangeException("keyword")
      }
    }

    //explicitAnonymousFunctionParameter          : FormalParameter         = explicitAnonymousFunctionParameterModifier? anyType identifier;
    explicitAnonymousFunctionParameter( modifier      : option[VToken[FormalParameterModifier]],
                                        parameterType : VToken[TypeRef],
                                        identifier    : VToken[Identifier]) : FormalParameter
    {
      FormalParameter(
        GetLocation(parameterType, _),
        identifier.Value, 
        modifier.WithDefault(FormalParameterModifier.Normal),
        parameterType.Value,
        null)
    }

    //explicitAnonymousFunctionParameterList      : FormalParameterList     = explicitAnonymousFunctionParameter (","s explicitAnonymousFunctionParameter)*;
    explicitAnonymousFunctionParameterList( head : VToken[FormalParameter],
                                            tail  : SCG.List[NToken * NToken * VToken[FormalParameter]]) : list[FormalParameter]
    {
      head.Cons(tail)
    }

    //explicitAnonymousFunctionSignature          : FormalParameterList     = "("s explicitAnonymousFunctionParameterList? ")"s;
    explicitAnonymousFunctionSignature( _lparen : NToken, _ : NToken,
                                        parms   : option[VToken[list[FormalParameter]]],
                                        _rparen : NToken, _ : NToken) : list[FormalParameter]
    {
      parms.WithDefault([])
    }

    //anonymousMethodExpression : Expr = "delegate"S explicitAnonymousFunctionSignature? block;
    anonymousMethodExpression(_delegate : NToken, _ : NToken,
                              signature : option[VToken[list[FormalParameter]]],
                              body      : VToken[Statement]) : Expr
    {
      match(signature) {
        | Some(parms) => Expr.Function(parms.Value, false, false, body.Value)
        | _           => Expr.Function([],          true,  false, body.Value)
      }
    }

    #endregion

    #region Call or indexer

    //indexerArguments        : CallOrIndexerArgumentList = "["s argumentList? "]"s;
    indexerArguments( _lparen : NToken, _ : NToken,
                      argumentList : option[VToken[list[FunctionArgument]]],
                      _rparen : NToken, _ : NToken) : list[FunctionArgument] * bool
    {
      (argumentList.WithDefault([]), false)
    }

    //callArguments           : CallOrIndexerArgumentList = "("s argumentList? ")"s;
    callArguments(_lparen : NToken, _ : NToken,
                  argumentList : option[VToken[list[FunctionArgument]]],
                  _rparen : NToken, _ : NToken) : list[FunctionArgument] * bool
    {
      (argumentList.WithDefault([]), true)
    }

    //callOrIndexerExpression : Expr                      = otherExpression (indexerArguments / callArguments)*;
    callOrIndexerExpression(otherExpression : VToken[Expr], applies : SCG.List[VToken[list[FunctionArgument] * bool]]) : Expr
    {
      mutable chain = otherExpression.Value;
      foreach(apply in applies) {
        chain = match(apply.Value) {
          | (args, true)  => Expr.Call(chain, args)
          | (args, false) => Expr.Indexer(chain, args)
        }
      }
      chain
    }

    #endregion

    #region Member / Pointer access

    //memberOperator    : bool = ("." / "->")s;
    memberOperator(op : NToken, _ : NToken) : bool
    {
      match(op.GetText()) {
        | "."   => true
        | "->"  => false
        | _     => throw ArgumentOutOfRangeException("op")
      }
    }

    //memberExpression  : Expr = callOrIndexerExpression (memberOperator callOrIndexerExpression)*;
    memberExpression(expr : VToken[Expr], applies : SCG.List[VToken[bool] * VToken[Expr]]) : Expr
    {
      mutable chain = expr.Value;
      foreach((memberAccess, valueExpr) in applies) {
        chain =
          if(memberAccess.Value)
            Expr.Member(chain, valueExpr.Value)
          else
            Expr.PointerMember(chain, valueExpr.Value);
      }
      chain
    }

    #endregion

    #region Unary and Cast operators

    //postfixOperator   : string = ("++" / "--")s;
    postfixOperator(op : NToken, _ : NToken) : string
    {
      op.GetText();
    }

    //postfixExpression : Expr = memberExpression postfixOperator*;
    postfixExpression(expr : VToken[Expr], ops : SCG.List[VToken[string]]) : Expr
    {
      mutable chain = expr.Value;
      foreach(op in ops)
        chain = Expr.UnaryOperator(chain, op.GetText(), false);
      chain
    }

    //prefixOperator    : object = ("++" / "--" / "+" / "-" / "~" / "!" / "&" / "*")s;
    prefixOperator(op : NToken, _ : NToken) : object
    {
      op.GetText()
    }

    //castOperator      : object = "("s anyType ")"s;
    castOperator( _lparen : NToken, _ : NToken,
                  toType  : VToken[TypeRef],
                  _rparen : NToken, _ : NToken,) : object
    {
      toType.Value
    }

    //prefixExpression  : Expr   = (prefixOperator / castOperator)* postfixExpression;
    prefixExpression(ops : SCG.List[VToken[object]], expr : VToken[Expr]) : Expr
    {
      mutable chain = expr.Value;
      foreach(op in ops) {
        chain = match(op.Value) {
          | op is string      => Expr.UnaryOperator(chain, op, true)
          | toType is TypeRef => Expr.CastOperator(chain, toType)
          | _ => throw ArgumentOutOfRangeException("ops")
        }
      }
      chain
    }

    #endregion

    #region Binary operators

    //multiplicativeOperator    : string  = ("*" / "/" / "%")s;
    multiplicativeOperator(op : NToken, _ : NToken) : string
    {
      op.GetText()
    }

    //multiplicativeExpression  : Expr    = prefixExpression (multiplicativeOperator prefixExpression)*;
    multiplicativeExpression(head : VToken[Expr], tail : SCG.List[VToken[string] * VToken[Expr]]) : Expr
    {
      head.Cons(tail)
    }

    //additiveOperator = ("+" / "-")s;
    additiveOperator(op : NToken, _ : NToken) : string
    {
      op.GetText()
    }

    //additiveExpression = multiplicativeExpression (additiveOperator multiplicativeExpression)*;
    additiveExpression(head : VToken[Expr], tail : SCG.List[VToken[string] * VToken[Expr]]) : Expr
    {
      head.Cons(tail)
    }

    //shiftOperator = ("<<" / ">>")s;
    shiftOperator(op : NToken, _ : NToken) : string
    {
      op.GetText()
    }

    //shiftExpression = additiveExpression (shiftOperator additiveExpression)*;
    shiftExpression(head : VToken[Expr], tail : SCG.List[VToken[string] * VToken[Expr]]) : Expr
    {
      head.Cons(tail)
    }

    //releationalOperator = ("<=" / ">=" / "<" / ">")s;
    releationalOperator(op : NToken, _ : NToken) : string
    {
      op.GetText()
    }

    //typeTestingOperator = ("is" / "as")S;
    typeTestingOperator(op : NToken, _ : NToken) : string
    {
      op.GetText()
    }

    //relationalOrTypeTestingExpression = shiftExpression ((releationalOperator / typeTestingOperator) shiftExpression)*;
    relationalOrTypeTestingExpression(head : VToken[Expr], tail : SCG.List[VToken[string] * VToken[Expr]]) : Expr
    {
      head.Cons(tail)
    }

    //equalityOperator = ("==" / "!=")s;
    equalityOperator(op : NToken, _ : NToken) : string
    {
      op.GetText()
    }

    //equalityExpression = relationalOrTypeTestingExpression (equalityOperator relationalOrTypeTestingExpression)*;
    equalityExpression(head : VToken[Expr], tail : SCG.List[VToken[string] * VToken[Expr]]) : Expr
    {
      head.Cons(tail)
    }

    //logicalAndOperator = "&"s;
    logicalAndOperator(op : NToken, _ : NToken) : string
    {
      op.GetText()
    }

    //logicalAndExpression = equalityExpression (logicalAndOperator equalityExpression)*;
    logicalAndExpression(head : VToken[Expr], tail : SCG.List[VToken[string] * VToken[Expr]]) : Expr
    {
      head.Cons(tail)
    }

    //logicalXorOperator = "^"s;
    logicalXorOperator(op : NToken, _ : NToken) : string
    {
      op.GetText()
    }

    //logicalXorExpression = logicalAndExpression (logicalXorOperator logicalAndExpression)*;
    logicalXorExpression(head : VToken[Expr], tail : SCG.List[VToken[string] * VToken[Expr]]) : Expr
    {
      head.Cons(tail)
    }

    //logicalOrOperator = "|"s;
    logicalOrOperator(op : NToken, _ : NToken) : string
    {
      op.GetText()
    }

    //logicalOrExpression = logicalXorExpression (logicalOrOperator logicalXorExpression)*;
    logicalOrExpression(head : VToken[Expr], tail : SCG.List[VToken[string] * VToken[Expr]]) : Expr
    {
      head.Cons(tail)
    }

    //conditionalAndOperator = "&&"s;
    conditionalAndOperator(op : NToken, _ : NToken) : string
    {
      op.GetText()
    }

    //conditionalAndExpression = logicalOrExpression (conditionalAndOperator logicalOrExpression)*;
    conditionalAndExpression(head : VToken[Expr], tail : SCG.List[VToken[string] * VToken[Expr]]) : Expr
    {
      head.Cons(tail)
    }

    //conditionalOrOperator = "||"s;
    conditionalOrOperator(op : NToken, _ : NToken) : string
    {
      op.GetText()
    }

    //conditionalOrExpression = conditionalAndExpression (conditionalOrOperator conditionalAndExpression)*;
    conditionalOrExpression(head : VToken[Expr], tail : SCG.List[VToken[string] * VToken[Expr]]) : Expr
    {
      head.Cons(tail)
    }

    #endregion

    #region Assignment

    //assignmentOperator   : string = ("=" / "+=" / "-=" / "*=" / "/=" / "%=" / "&=" / "|=" / "^=" / "<<=" / ">>=")s;
    assignmentOperator(op : NToken, _ : NToken) : string
    {
      op.GetText()
    }

    //assignmentExpression : Expr = conditionalExpression (assignmentOperator expression)?;
    assignmentExpression(expr : VToken[Expr], assign : option[VToken[string] * VToken[Expr]]) : Expr
    {
      match(assign) {
        | Some( (op, value) ) => Expr.Assign(expr.Value, value.Value, op.Value)
        | _ => expr.Value
      }
    }

    #endregion

    #region Conditional expression

    //conditionalExpression = conditionalOrExpression ("?"s expression ":"s expression)?;
    conditionalExpression(expr  : VToken[Expr],
                          op    : option[NToken * NToken * VToken[Expr] * NToken * NToken * VToken[Expr]]) : Expr
    {
      match(op) {
        | Some( (_, _, trueExpr, _, _, falseExpr) ) => Expr.ConditionalOperator(expr.Value, trueExpr.Value, falseExpr.Value)
        | _ => expr.Value
      }
    }

    #endregion

    #region Lambda expression

    //implicitAnonymousFunctionParameter     : FormalParameter      = identifier;
    implicitAnonymousFunctionParameter(identifier : VToken[Identifier]) : FormalParameter
    {
      FormalParameter(
        GetLocation(identifier),
        identifier.Value,
        FormalParameterModifier.Normal,
        null,
        null)
    }

    //implicitAnonymousFunctionParameterList : FormalParameterList  = implicitAnonymousFunctionParameter (","s implicitAnonymousFunctionParameter)*;
    implicitAnonymousFunctionParameterList( head : VToken[FormalParameter],
                                            tail  : SCG.List[NToken * NToken * VToken[FormalParameter]]) : list[FormalParameter]
    {
      head.Cons(tail)
    }

    //implicitAnonymousFunctionSignature_1  : FormalParameterList = implicitAnonymousFunctionParameter;
    implicitAnonymousFunctionSignature_1(single : VToken[FormalParameter]) : list[FormalParameter]
    {
      [single.Value]
    }

    //implicitAnonymousFunctionSignature_2  : FormalParameterList = "("s implicitAnonymousFunctionParameterList? ")"s;
    implicitAnonymousFunctionSignature_2( _lparen : NToken, _ : NToken,
                                          parms   : option[VToken[list[FormalParameter]]],
                                          _rparen : NToken, _ : NToken) : list[FormalParameter]
    {
      parms.WithDefault([])
    }

    //implicitAnonymousFunctionSignature    : FormalParameterList = implicitAnonymousFunctionSignature_1
    //                                                            / implicitAnonymousFunctionSignature_2;
    implicitAnonymousFunctionSignature(opt : VToken[list[FormalParameter]]) : FormalParameterList
    {
      opt.Value
    }

    //anonymousFunctionSignature  : FormalParameterList = explicitAnonymousFunctionSignature
    //                                                  / implicitAnonymousFunctionSignature;
    anonymousFunctionSignature(opt : VToken[list[FormalParameter]]) : list[FormalParameter]
    {
      opt.Value
    }

    //anonymousFunctionBody_expression  : Statement = expression;
    anonymousFunctionBody_expression(expr : VToken[Expr]) : Statement
    {
      Statement.Block([Statement.Return(expr.Value)])
    }

    //anonymousFunctionBody_block       : Statement = block;
    anonymousFunctionBody_block(block : VToken[Statement]) : Statement
    {
      block.Value
    }

    //anonymousFunctionBody             : Statement = anonymousFunctionBody_expression
    //                                              / anonymousFunctionBody_block;
    anonymousFunctionBody(opt : VToken[Statement]) : Statement
    {
      opt.Value
    }

    //lambdaExpression : Expr = anonymousFunctionSignature "=>"s anonymousFunctionBody;
    lambdaExpression( signature : VToken[list[FormalParameter]],
                      _arrow    : NToken, _ : NToken,
                      body      : VToken[Statement]) : Expr
    {
      Expr.Function(signature.Value, false, true, body.Value)
    }

    #endregion
  }
}