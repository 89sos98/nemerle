/*
 * Copyright (c) 2004 The University of Wroclaw.
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions
 * are met:
 *    1. Redistributions of source code must retain the above copyright
 *       notice, this list of conditions and the following disclaimer.
 *    2. Redistributions in binary form must reproduce the above copyright
 *       notice, this list of conditions and the following disclaimer in the
 *       documentation and/or other materials provided with the distribution.
 *    3. The name of the University may not be used to endorse or promote
 *       products derived from this software without specific prior
 *       written permission.
 *
 * THIS SOFTWARE IS PROVIDED BY THE UNIVERSITY ``AS IS'' AND ANY EXPRESS OR
 * IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES
 * OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN
 * NO EVENT SHALL THE UNIVERSITY BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
 * SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED
 * TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR
 * PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF
 * LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING
 * NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS
 * SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 */

namespace Nemerle.Compiler
{
  public type TypingContext = MacroColorizer;

  /** A class responsible for assigning new colors to macro contexts. */
  public module MacroColorizer
  {
    // FIXME: drop the Macro prefix:
    public mutable MacroColor : int;
    public mutable MacroUseColor : int;
    public mutable MacroUseContext : GlobalEnv;

    mutable max_macro_color : int;
    mutable colors_stack : list [int * int * GlobalEnv]; // FIXME: use Stack


    this () 
    { 
      MacroColor = 1;
      MacroUseColor = 1;
      MacroUseContext = null;
      max_macro_color = 1;
    }
    
                           
    public NewColor () : int
    {
      max_macro_color = max_macro_color + 1;
      max_macro_color
    }
    
    
    /** Creates new color for execution of macro expansion, stores
        this color as top of the stack, together with [macro_use]
        as context color of currently executed macro.
     */   
    public PushNewColor (use_color : int, use_context : GlobalEnv) : void
    {
      colors_stack = (MacroColor, MacroUseColor, MacroUseContext) :: colors_stack;
      max_macro_color = max_macro_color + 1;
      MacroColor = max_macro_color;
      MacroUseColor = use_color;
      MacroUseContext = use_context;
    }


    public PushUseSiteColor () : void
    {
      colors_stack = (MacroColor, MacroUseColor, MacroUseContext) :: colors_stack;
      MacroColor = MacroUseColor;
    }
    

    /** Bring color context of previous execution of macro
        (occurs when macro expansion execution finishes).

        Assumes that [PushNewColor] was called before
     */    
    public PopColor () : void
    {
      match (colors_stack) {
        | (col, use_col, use_ctx) :: rest =>
          colors_stack = rest;
          MacroColor = col;
          MacroUseColor = use_col;
          MacroUseContext = use_ctx;
        | [] =>
          Util.ice ("pop from empty stack of macro colors...")
      }
    }
  }

}
