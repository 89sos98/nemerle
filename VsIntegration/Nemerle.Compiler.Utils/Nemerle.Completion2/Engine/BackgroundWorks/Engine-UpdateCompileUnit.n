using System;
using System.Diagnostics;
using System.Collections.Generic;
using System.Linq;

using Nemerle.Assertions;
using Nemerle.Collections;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Imperative;
using Nemerle.Utility;
using Nemerle.Compiler.Utils;
using Nemerle.Compiler.Utils.Async;
using Nemerle.Surround;

[assembly: DefineSurround("currentAsyncRequest", false, 
                          _currentAsyncRequest = request, 
                          {
                            _currentAsyncRequest = null;
                            request.MarkAsCompleted();
                          })]


namespace Nemerle.Completion2
{
	public partial class Engine
	{
    public BeginUpdateCompileUnit(source : ISource, newEndIndex : int, newEndLine : int, oldEndIndex : int, oldEndLine : int, startIndex : int, startLine : int) : AsyncRequest
    {
      def request = AsyncRequest(AsyncRequestType.UpdateCompileUnit, this, source, UpdateCompileUnit(_, 
        newEndIndex, newEndLine, oldEndIndex, oldEndLine, startIndex, startLine));
      AsyncWorker.AddWork(request);
      request
    }

    /// Run by AsyncWorker thread!
    /// Parse CompileUnit. Set CompileUnit to source. Update information about outline regions. 
    private UpdateCompileUnit(request : AsyncRequest, 
      newEndIndex : int, newEndLine : int, oldEndIndex : int, oldEndLine : int, startIndex : int, startLine : int) : void
    {
      surroundwith (currentAsyncRequest)
      try
      {
        //def timer = Stopwatch.StartNew();
        AsyncWorker.CheckCurrentThreadIsTheAsyncWorker();
        _currentAsyncRequest = request;
        _callback.SetStatusText("Update compile unit...");
        //Debug.WriteLine($"UpdateCompileUnit 1 $(timer.Elapsed)"); timer.Reset(); timer.Start();

        when (request.Stop)
          return;
        
        def source   = request.Source;

        //TODO: VladD2: Попробовать выполнять релокешон в отдельном потоке (из пула потоков). 
        //      Это должно быть особенно эффективно на машинах имеющих более двух процссоров! 
        def resetedMember = 
          if (IsProjectAvailable && !IsDefaultEngine && newEndIndex != 0)
            Project.AddRelocation(source.FileIndex, newEndIndex, newEndLine, 
              oldEndIndex, oldEndLine, startIndex, startLine) :> Factories.IntelliSenseModeMethodBuilder;
          else null;

			  def compUnit = ParseCompileUnit(request.GetTextCurrentVersionAndFileIndex());

        source.CompileUnit = compUnit;
        
        //Debug.WriteLine($"UpdateCompileUnit 2 $(timer.Elapsed)"); timer.Reset(); timer.Start();

        when (compUnit.ParseCompilerMessages.Any(cm => cm.Msg.Contains("unexpected end of file")))
        {
          // The user does type non closed bracket. The AST is be in incorrect state.
          // We should report errors and stop processing the CompileUnit.

          //TODO: Добавить выдачу сообщений об ошибках парсинга CompileUnit-а!
          _callback.SetStatusText("Update compile unit aborted (file in inconsistent state).");
          return;
        }

        def collectTopDeclarations(source, compUnit) : void
        {
			    def topDecls       = compUnit.TopDeclarations;
			    def decls          = AstUtils.GetAllDeclarations(topDecls);

			    def declsAry = decls
				    .Where(d => d.name is Splicable.Name && d.name.GetName().context != null)
				    .OrderBy(d => d.Name)
				    .ToArray();

			    //source.Declarations = declsAry;
          //AsyncWorker.AddResponse(() => source.SetTopDeclarations(declsAry));
          source.SetTopDeclarations(declsAry);
        }

        def isNeedRebuildTypesTree(engine, source) : bool
        {
			    // Сравненивает старый и новый CompileUnit, и если их структура не совпадает (т.е. были 
			    // добавлены, удалены, изменены методы), запустить парсинг дерева типов. 
			    // Парсинг дерева типов так же запускается если оно не существует к этому моменту.
          def isNeedBuildTypesTree = if (engine.IsProjectAvailable)
            //TODO: VladD2: Возможно в IsStructureOfCompileUnitChanged нужно передавать еще 
            // старое значение CompileUnit-а (то что было в source.CompileUnit до его обновления).
            // Иначе сравнение будет всегда происходить с CompileUnit-ом полученым при построении
            // дерева типов и это может привести черт знает к чему. В общем нужно продумать стратегию
            // проверки изменения структуры CompileUnit-а!!!
                                       engine.Project.IsStructureOfCompileUnitChanged(source)
                                     else
                                       true;
          isNeedBuildTypesTree
        }

        collectTopDeclarations(source, compUnit);

        //Debug.WriteLine($"UpdateCompileUnit 3 $(timer.Elapsed)"); timer.Reset(); timer.Start();

        when (request.Stop)
          return;

        def version = compUnit.SourceVersion;

        when (version == source.CurrentVersion)
        {
			    def regions = RegionsHelper.Collect(compUnit);
        
          //Debug.WriteLine($"UpdateCompileUnit 4 $(timer.Elapsed)"); timer.Reset(); timer.Start();

          when (version == source.CurrentVersion)
            AsyncWorker.AddResponse(() => source.SetRegions(regions, version));
        }

        //Debug.WriteLine($"UpdateCompileUnit 5 $(timer.Elapsed)"); timer.Reset(); timer.Start();

        when (!request.Stop && !IsDefaultEngine)
        {
          if (isNeedRebuildTypesTree(this, source))
					  ignore(BeginBuildTypesTree());
          else when (resetedMember != null)
            AddMethodAtFirstCheckQueue(resetedMember); // Add request for typing of relocated method body
        }

        //Debug.WriteLine($"UpdateCompileUnit 6 $(timer.Elapsed)"); timer.Reset(); timer.Start();
      }
      catch
      { | e is CompilationAbortedException =>
          def msg = $"The update CompileUnit operation aborted at: $(e.Message)";
          throw CompilationAbortedException(msg, e);
      }
      finally { _callback.SetStatusText("Update compile unit is complete."); }
    }

    private ParseCompileUnit(code : string, sourceVersion : int, fileIndex : int) : CompileUnit
    {
      RestoreManagerClassInstance();
      _isCompileUnit = true;
      def currentMessages = _currentMessages;
      try
      {
        _namespaces.Clear();

        _currentMessages = List();
        _decls           = [];
        def lexer        = LexerString(this, code, Location(fileIndex, 1, 1));
        def topDecls     = try { ParsingPipeline(lexer) } catch { | _ => [] }; //!!! теряем сообщение!
        def endLoc       = lexer.Location;
        def loc          = Location(fileIndex, 1, 1, endLoc.EndLine, endLoc.EndColumn);
        def openLoc      = Location(fileIndex, 1, 1, 1, 1);
        def closeLoc     = Location(fileIndex, endLoc.EndLine, endLoc.EndColumn, endLoc.EndLine, endLoc.EndColumn);
        def insideEnv    = match (_decls.Find(_ is Decl.Using))
                           { | Some(Decl.Using as u) => u.AfterEnv | _ => CoreEnv };
        def topNs        = Decl.Namespace(loc, _decls.Rev(), [], [], CoreEnv, insideEnv, openLoc, openLoc, closeLoc, topDecls);
        
        assert2(_namespaces.Count == 0);

        // Данная функция подключает полученный список TopDeclaration-ов в пространсва имен к которым
        // они пренадлежат.
        // Go through namespaces for each file.
        
        def addTypes(ns, topDeclarations) : void
        {
          def getNsDeclList(ns, acc = List())
          {
            acc.Add(ns);

            foreach (subNs is Decl.Namespace in ns.Decls)
              _ = getNsDeclList(subNs, acc);

            acc
          }
          
          def typesGroupedByNs = topDeclarations.GroupBy(
                d => match (d.TryGetEnv()) { | Some(env) => env | _ => null });
          def nsDeclList = getNsDeclList(ns).NToList();
          def join = nsDeclList.Join(typesGroupedByNs, 
                nsDecl => nsDecl.InsideEnv,
                group  => group.Key,
                x      => x);
          foreach ((nsDecl, group) in join)
            foreach (topDeclaration in group)
              nsDecl.Decls ::= Decl.Type(topDeclaration.Location, topDeclaration);
        }
        
        addTypes(topNs, topDecls);
        //assert2(lexer.IsEof);
        
        def cu = CompileUnit(topNs, topDecls.Rev(), lexer.Regions.Rev(), _currentMessages.UpCastToArray(),
                    lexer.LinesLengths, sourceVersion, fileIndex);

        foreach (msg in cu.ParseCompilerMessages)
          msg.CompileUnit = cu;

        _callback.SetCompilerMessageForCompileUnit(cu);

        cu
      }
      finally
      {
        _isCompileUnit = false;

        //when (currentMessages != null && _currentMessages.Count > 0)
        //  currentMessages.AddRange(_currentMessages);

        _currentMessages = currentMessages;
        _decls           = [];
      }
    }
  } // end class Engine
} // end of Nemerle.Completion2 namespace
