using System;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;

namespace LRPEGCC
{
  [Nemerle.MacroUsage(Nemerle.MacroPhase.BeforeTypedMembers, Nemerle.MacroTargets.Class)]
  macro PegGrammar (tb : TypeBuilder, startRule, rules)
  {
    GrammarImpl.MakeParser(tb, startRule, rules);
  }

  internal module GrammarImpl
  {
    public ParseRules(startRule : string, rules : PExpr) : Grammar
    {
      def rules = match (rules)
      {
      | Sequence(rules) => rules;
      | rule =>
        Message.Error($"1 $(rule.GetType()) $(rule)");
        throw ArgumentException();
      }
      def toInt(expr : PExpr) : int
      {
      | Literal(Integer(val, neg, _)) =>
        def val = val :> int;
        if (neg) -val else val;
      | _ => throw ArgumentException();
      }
      def toChar(expr : PExpr) : char
      {
      | Literal(Char(val)) => val;
      | _ => throw ArgumentException();
      }
      def transformRule(_ : PExpr) : Rule
      {
      | <[ And($rule) ]>                => Rule.And(transformRule(rule))
      | <[ Not($rule) ]>                => Rule.Not(transformRule(rule))
      | Literal(String(str))            => Rule.Chars(str.Map(ch =>RangeSet().AddRange(ch, ch)))
      | Literal(Char(ch))               => Rule.Chars([RangeSet().AddRange(ch, ch)])
      | <[ Range($from, $to) ]>         => Rule.Chars([RangeSet().AddRange(toChar(from), toChar(to))])
      | <[ OneOrMany($rule) ]>          => Rule.RepeatMin(1, transformRule(rule))
      | <[ ZeroOrMany($rule) ]>         => Rule.RepeatMin(0, transformRule(rule))
      | <[ AtLeast($from, $rule) ]>     => Rule.RepeatMin(toInt(from), transformRule(rule))
      | <[ ZeroOrOne($rule) ]>          => Rule.RepeatMinMax(0, 1, transformRule(rule))
      | <[ FromTo($from, $to, $rule) ]> => Rule.RepeatMinMax(toInt(from), toInt(to), transformRule(rule))
      | <[ Seq(..$rules) ]>             => Rule.Sequence(rules.Map(transformRule))
      | <[ Or(..$rules) ]>              => Rule.Choice(rules.Map(transformRule))
      | <[ $name(..$rules) ]>           => Rule.Capture(name.ToString(), Rule.Sequence(rules.Map(transformRule)))
      | Ref(name)                       => Rule.Call(name.ToString())
      | rule =>
        Message.Error($"2 $(rule.GetType()) $(rule)");
        throw ArgumentException();
      }
      rules.Fold(Grammar(startRule), fun(rule, grammar)
      {
        match (rule)
        {
        | <[ def $name = $rule; ]> =>
          grammar.Add(name.ToString(), transformRule(rule));
        | rule =>
          Message.Error($"3 $(rule.GetType()) $(rule)");
          throw ArgumentException();
        }
      });
    }

    public MakeParser(tb : TypeBuilder, startRule : PExpr, rules : PExpr) : void
    {
      def compiler = RuleCompiler();
      compiler.GetCharName = "GetChar";
      compiler.CheckTextLengthName = "CheckTextLength";

      def grammar = ParseRules(startRule.ToString(), rules);
      def grammar = Optimizer.OptimizeGrammar(grammar);

      mutable str = "";
      foreach (name in grammar.Names)
      {
        def rule = grammar.GetRule(name);
        def body = compiler.CompileRule(rule, grammar);
        def method = <[decl:
          private $(compiler.DecorateRuleName(name) : dyn)(pos : int) : int
          {
            $(body);
          }
        ]>;
        tb.Define(method);
        str = str + method.ToString() + "\n" + body.ToString() + "\n\n";
      }
      tb.Define(<[decl:
          private $(compiler.CacheName : dyn) : System.Collections.Generic.Dictionary[int, int] = System.Collections.Generic.Dictionary();
      ]>);
      tb.Define(<[decl:
        public Start() : int
        {
          $(compiler.DecorateRuleName(grammar.StartRuleName) : dyn)(0);
        }
      ]>);
      tb.Define(<[decl:
        public override ToString() : string
        {
          $(str : string);
        }
      ]>);
    }
  }
}
