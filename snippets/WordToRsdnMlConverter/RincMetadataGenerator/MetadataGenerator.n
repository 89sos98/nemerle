using Nemerle.Collections;
using Nemerle.Imperative;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Text.RegularExpressions;
using System.IO;
using System.Collections.Generic;
using System.Linq;
using System.Windows.Forms;
using System.Xml.Linq;

namespace RincMetadataGenerator
{
  [Record]
  class MetadataGenerator
  {
    public Path      : string { get; private set; }
    public Year      : int    { get; private set; }
    public Num       : int    { get; private set; }
    public PageCount : int    { get; private set; }
    public Creator   : string { get; private set; }
    
    [RecordIgnore]         _pagesRegex        : Regex = Regex(@"(\d\d)-(\d\d)-.*", RegexOptions.Compiled);
    [RecordIgnore] mutable _errorsCache       : array[Error];
    [RecordIgnore]         _errors            : List[Error] = List();

    [RecordIgnore] static  _authorAttrMap : Hashtable[string, string * string] = Hashtable(array[
              ("e-mail",              (null,                 null                  )),
              ("rsdn-profile",        ("Профиля на rsdn.ru", "Profile on rsdn.ru")),
              ("phone",               ("Телефон",            "Phone"               )), 
              ("address",             ("Адрес",              "Adress"              )),
              ("place-of-employment", ("Место работы",       "Place of employment" )),
              ("job",                 ("Должность",          "Job"                 )),
              ("place-of-study",      ("Место учебы",        "Place of study"      )),
              ("scientific-degree",   ("Научная степень",    "Scientific degree"   )),
              ("scientific-rank",     ("Научное звание",     "Scientific rank"     )),
              ]);
              
    GetAuthorAttrs(author : XElement, forEnu : bool) : string
    {
      def strs = _authorAttrMap.Keys.Map(GetAuthorAttr(author, _, forEnu)).Filter(s => !string.IsNullOrEmpty(s));
      $<#..$(strs; ". ")#>
    }
    
    GetAuthorAttr(author : XElement, attributeName : string, forEnu : bool) : string
    {
      def attr = author.Attribute(attributeName);
      
      when (attr == null)
        return "";
        
      def (rus, enu) = _authorAttrMap[attributeName];
      def prefix = if (forEnu) enu else rus;
      
      if (prefix == null || string.IsNullOrEmpty(attr.Value))
        ""
      else
        prefix + ": " + if (attributeName == "rsdn-profile") $"http://rsdn.ru/Users/$(attr.Value).aspx"
                        else attr.Value
    }
    
    public Errors    : array[Error]
    {
      get
      {
        when (_errorsCache == null)
          _errorsCache = _errors.NToArray();
          
        _errorsCache
      }
    }
    
    AddError(path : string, message : string) : void
    {
      _errors.Add(Error(message, path));
    }
    
    public Generate() : string
    {
      _errors.Clear();
      _errorsCache = null;
      def files = GetArticleXmlPaths();
      //_ = MessageBox.Show($<#Count: $(files.Length)$("\n")..$(files; "\n")#>);
      
      def articleMetadatas = files.Map(ReadArticleMetadata).Filter(e => e.HasElements);
      def issueMetadata    = MekeIssueMetadata(articleMetadatas);
      def metadataFilePath = Path.Combine(Path, $"02346621_$(Year)_-_$(Num)_unicode.xml");
      issueMetadata.Save(metadataFilePath);
      metadataFilePath
    }
    
    MekeIssueMetadata(articleMetadatas : list[XElement]) : XDocument
    {
      def xml = $<#<?xml version="1.0" standalone="no" ?>
<journals>
  <opercard>
    <operator>ООО "К-Пресс"|$Creator</operator>
    <date>$(DateTime.Now)</date>
    <cntnode>0</cntnode>
    <cs>1297715</cs>
  </opercard>
  <journal>
    <journalInfo lang="RUS">
      <jrntitle>RSDN Magazine (РСДН Журнал)</jrntitle>
      <jrntitlevar>RSDN Magazine</jrntitlevar>
      <publ>ООО "К-Пресс"</publ>
      <placepubl>Россия, г. Москва</placepubl>
      <loc>129343, г. Москва, ул. Амундсена, 15-1-7.</loc>
    </journalInfo>
    <issue>
      <issn>0234-6621</issn>
      <jrncode jcountry="ru">02346621</jrncode>
      <jdateUni>$Year$Num</jdateUni>
      <pages>3-$PageCount</pages>

    </issue>
  </journal>
</journals>
#>;
      def doc = XDocument.Parse(xml);
      def issue = doc.Descendants("issue").NFirst();
      issue.Add(articleMetadatas.NToArray());
      def cntnode = doc.Descendants("cntnode").NFirst();
      cntnode.Value = doc.Descendants().Count().ToString();
      doc
    }
    
    /// Получаем спсок XML-ей для статей. Для этого просматриваем текущий каталог и каталоги 
    /// вложенные в него. 
    GetArticleXmlPaths() : array[string]
    {
      def resulr = List();
      
      def getFilesForDir(path) : void
      {
        def files = Directory.GetFiles(path, "*.xml");
        def isNotMetadata(file)
        {
          !Path.GetFileName(file).StartsWith("Metadata", StringComparison.InvariantCultureIgnoreCase)
        }
        
        foreach (file when isNotMetadata(file) in files)
          resulr.Add(file);
      }
      
      getFilesForDir(Path);
      
      foreach (dir in Directory.GetDirectories(Path))
        getFilesForDir(dir);
        
      resulr.OrderBy(x => Path.GetFileName(x)).NToArray()
    }
        
    ReadArticleMetadata(path : string) : XElement
    {
      def error(message : string) : void { AddError(path, message); }
      //[RecordIgnore] mutable _currntArticlePath : string;
      //SetCurrntArticlePath(articlePath : string) : void { _currntArticlePath = articlePath; }
    
      def doc = XDocument.Load(path);
      
      when (doc.Root.Name != "ARTICLE")
        return XElement("none");
        
      def fileName = Path.GetFileName(path);
      def pages    = _pagesRegex.Match(fileName);
      
      when (pages.Groups.Count != 3)
      {
        error($"Имя файла ($fileName) должно начинаться с номера страниц. Например: 03-10-SomeProduct.xml.");
        return XElement("none");
      }
      
      def root        = doc.Root;
      def dummy       = XElement("dummy"); 
      def dummyAttr   = XAttribute("dummy", ""); 
      def rus         = XAttribute("lang", "RUS");
      def eng         = XAttribute("lang", "ENG");
      def isEmpty(str : string) : bool { string.IsNullOrEmpty(str) }
      def get(tagName : string) : XElement
      {
        def tag = root.Element(tagName);
        
        if (tag == null)
        {
          error($"Тег $tagName не задан в документе '$path'");
          dummy
        }
        else
          tag
      }
      def getVal(tagName) : string
      {
        get(tagName).Value
      }
      def getAttr(tag : XElement, attrName : string) : string
      {
        def val = (tag.Attribute(attrName) ?? dummyAttr).Value.Trim();
        val
      }
      
      def arttitles(root : XElement) : XElement
      {
        def title       = root.Element("TITLE").Value;
        def arttitleRu  = XElement("arttitle",  rus, title);
        def arttitles   = XElement("arttitles", arttitleRu);
        
        arttitles
      }
      def keywords() : XElement
      {
        def keywords    = getVal("KEYWORDS");
        
        when (keywords.Contains(","))
          error($<#В файл "$fileName" ключевые разделены недопустимым символом (запятой ","). Ключевые слова должны быть разделены символом точа с запятой ";".#>);
        
        def keywords    = keywords.Split(array[';'], StringSplitOptions.RemoveEmptyEntries);
        if (keywords.Length == 0)
          XElement("nokeywords", "")
        else
        {
          def elems = List.[object](keywords.MapToArray(k => XElement("keyword", k.Trim())));
          elems.Add(rus);
          XElement("keywords", XElement("kwdGroup", elems.NToArray()))
        }
      }
      def biblist() : XElement
      {
        def refs = (root.Element("LITERATURE") ?? dummy).Elements("REF").NToArray();

        if (refs.Length == 0)
          XElement("nobiblist")
        else
          XElement("biblist", refs.Map(r => XElement("blistpart", r.Value)))
      }
      def abstracts() : XElement
      {
        def text    = getVal("ABSTRACT").Trim();
        
        if (isEmpty(text))
        {
          error($<#В файл "$fileName" не заполена "Аннотация".#>);
          XElement("noabstracts", "")
        }
        else
          XElement("abstracts", XElement("abstract", array[rus : object, text]))
      }
      def authors() : XElement
      {
        def authors = root.Elements("AUTHOR").NToArray();
        
        when (authors.Length == 0)
          error($<#В файл "$fileName" не указано ни одного автора.#>);
        // <AUTHOR e-mail="dvyukov@gmail.com" rsdn-profile="38267" place-of-study="МГТУ им. Н. Э. Баумана" 
        // Scientific-rank="маг. техн." href="http://www.rsdn.ru/Users/38267.aspx">Вьюков Дмитрий Сергеевич</AUTHOR>
        mutable i = 0;
        
        def author(a : XElement) : XElement
        {
          i++;
          def fio = a.Value.Trim();
          if (isEmpty(fio) || !fio.Contains(" "))
          {
            error($<#В файл "$fileName" некорректно заполено поле "ФИО". В нем должны содержаться: фамилия, имя и отчество авторов в именительном подеже.#>);
            dummy
          }
          else
          {
            def firstSpaceIndex = fio.IndexOfAny(array[' ', '\t']);
            def surname         = XElement("surname",     fio.Substring(0, firstSpaceIndex));
            def fname           = XElement("fname",       fio.Substring(firstSpaceIndex));
            def auwork          = getAttr(a, "place-of-employment");
            def auwork          = if (isEmpty(auwork)) getAttr(a, "place-of-study") else auwork;
            when (isEmpty(auwork))
              error($<#В файл "$fileName" в информации об авторе "$fio" не заполнены обязательные поля "Место работы" или "Место учебы".#>);
            def auwork          = XElement("auwork", auwork);
            def auemail         = getAttr(a, "e-mail");
            when (isEmpty(auemail))
              error($<#В файл "$fileName" в информации об авторе "$fio" не заполнены обязательные полe "E-Mail".#>);
            def auinf           = XElement("auinf", GetAuthorAttrs(a, forEnu = false));
            def auemail         = XElement("auemail", auemail);
            def individInfo     = XElement("individInfo", array[rus, surname, fname, auinf, auwork, auemail]);
            def author          = XElement("author", array[XAttribute("authornum", i), individInfo]);
            author
          }
        }
        
        XElement("authors", authors.MapToArray(author))
      }
      def keywords    = keywords();
      def biblist     = biblist();
      def abstracts   = abstracts();
      def authors     = authors();
      def arttitles   = arttitles(root);
      def startPage   = XElement("fpageart", pages.Groups[1]);
      def endPage     = XElement("lpageart", pages.Groups[2]);
      def fpdf        = XElement("fpdf", Path.ChangeExtension(fileName, ".pdf"));
      def article     = XElement("article", 
        array[startPage, endPage, authors, arttitles, abstracts, keywords, biblist, fpdf]);

      article
    }
  }
}
