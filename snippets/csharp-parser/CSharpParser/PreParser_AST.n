using System;
using SCG = System.Collections.Generic;

using Nemerle.Assertions;
using Nemerle.Utility;
using Nemerle.Peg;

namespace CSharpParser
{
  [Record]
  public class PreparedCompilationUnit : Located
  {
    [Accessor] elements : SCG.List[PreParseElement];
  }

  public variant PreParseElement : Located
  {
    | Comment
    | Directive { directive : CSharpParser.Directive; }
  }

  public variant Directive : Located
  {
    | If                  { condition   : DirectiveExpression; }
    | Elif                { condition   : DirectiveExpression; }
    | Else
    | EndIf
    | Define              { symbol      : DirectiveExpression.Symbol; }
    | Undefine            { symbol      : DirectiveExpression.Symbol; }
    | LineDefault
    | LineNumber          { number      : int; fileName : string; }
    | Region              { title       : string; }
    | EndRegion           { title       : string; }
    | CompilationWarning  { message     : string; }
    | CompilationError    { message     : string; }
    | Error               { message     : string; }

    public override ToString() : string
    {
      match(this)
      {
        | If => "#if"
        | Elif => "#elif"
        | Else => "#else"
        | EndIf => "#endif"
        | Define => "#define"
        | Undefine => "#undefine"
        | LineDefault => "#line default"
        | LineNumber => "#line"
        | Region(title) => $"#region $title"
        | EndRegion(title) => $"#endregion $title"
        | CompilationWarning(message) => $"#warning $message"
        | CompilationError(message) => $"#error $message"
        | Error(message) => $"error: $message"
      }
    }
  }

  public variant DirectiveExpression
  {
    | Symbol              { id    : string; }
    | Boolean             { value : bool;   }
    | AndOperator         { left  : DirectiveExpression;
                            right : DirectiveExpression; }
    | OrOperator          { left  : DirectiveExpression;
                            right : DirectiveExpression; }
    | EqualsOperator      { left  : DirectiveExpression;
                            right : DirectiveExpression; }
    | NotEqualsOperator   { left  : DirectiveExpression;
                            right : DirectiveExpression; }
    | NotOperator         { e     : DirectiveExpression; }
  }
}