using System;
using System.Collections.Generic;
using System.IO;

using ICSharpCode.Core;
using ICSharpCode.SharpDevelop.Internal.Templates;
using ICSharpCode.SharpDevelop.Project;
using ICSharpCode.SharpDevelop.Dom;

using MSBuild = Microsoft.Build.BuildEngine;

using Nemerle.SharpDevelop.CodeCompletion;
using Nemerle.Utility;

namespace Nemerle.SharpDevelop {

    public class NemerleProject : CompilableProject {

        static this() {
            ProjectService.SolutionLoaded += OnSolutionLoaded;
        }

        public this(provider : IMSBuildEngineProvider, fileName : string, projectName : string) {
            base(provider);
            this.Name = projectName;
            LoadProject(fileName);
        }

        public this(info : ProjectCreateInformation) {
            base(info.Solution);
            Create(info);
        }

        mutable engine_context : EngineContext = null;

        private InitEngineContext() : void {
            engine_context = EngineService.CreateEngineContext(this);
        }

        public override Language : string {
            get { NemerleLanguageProperties.LanguageName }
        }

        public override LanguageProperties : LanguageProperties {
            get { NemerleLanguageProperties.Instance }
        }

        protected override Create(info : ProjectCreateInformation) : void {
            base.Create(info);
            SetProperty("Debug", null, "DefineConstants", "DEBUG;TRACE",
                        PropertyStorageLocations.ConfigurationSpecific, false);
            SetProperty("Release", null, "DefineConstants", "TRACE",
                        PropertyStorageLocations.ConfigurationSpecific, false);

            //def group = MSBuildProject.AddNewPropertyGroup(false);
            //_ = group.AddNewProperty("NoStdLib", "true", true);
            //group.AddNewProperty("Nemerle", "$(ProgramFiles)\\Nemerle", true).Condition = " '$(Nemerle)' == '' ";

            try {
                AddImport("$(Nemerle)\\Nemerle.MSBuild.targets", null);
            } catch {
                | ex is MSBuild.InvalidProjectFileException =>
                    throw ProjectLoadException("Please ensure that the Nemerle compiler is installed on your computer.\n\n" + ex.Message, ex);
            }
        }

        public override GetDefaultItemType(fileName : string) : ItemType {
            if (string.Equals(".n", Path.GetExtension(fileName), StringComparison.OrdinalIgnoreCase))
                ItemType.Compile
            else
                base.GetDefaultItemType(fileName)
        }

        public override CreateProjectItem(item : MSBuild.BuildItem) : ProjectItem {
            match(item.Name) {
                | "MacroReference" => MacroReferenceProjectItem(this, item)
                | "MacroProjectReference" => MacroProjectReferenceProjectItem(this, item)
                | _ => base.CreateProjectItem(item)
            }
        }

        public override GetBuildDependencies(build_options : ProjectBuildOptions) : ICollection[IBuildable] {
            def result = base.GetBuildDependencies(build_options);
            foreach (item is MacroProjectReferenceProjectItem in GetItemsOfType(ItemTypeExt.MacroProjectReference)) {
                when (null != item.ReferencedProject)
                    result.Add(item.ReferencedProject);
            }
            result
        }

        private static OnSolutionLoaded(_ : object, e : SolutionEventArgs) : void {
            foreach(project is NemerleProject in e.Solution.Projects) {
                project.InitEngineContext();
            }
        }

    }

}
