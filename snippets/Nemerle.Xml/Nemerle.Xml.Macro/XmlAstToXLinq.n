using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using TT = Nemerle.Compiler.Typedtree;
using TExpr = Nemerle.Compiler.Typedtree.TExpr;

using System.Collections.Generic;
using System.Xml.Linq;
using L = System.Xml.Linq;
using X = Nemerle.Xml;
using NColl = Nemerle.Collections.NCollectionsExtensions;

namespace Nemerle.Xml
{
  class XmlAstToXLinq
  {
    _typer    : Typer;
    _location : Location;
    _text     : string;
    _lineOffsetMap : array[int];
    
    public this(typer : Typer, location : Location, text : string)
    {
      _typer          = typer;
      _location       = location;
      _text           = text;
      
      def fillLineOffsetMap()
      {
        def map = List(text.Length / 10);

        map.Add(0);
        
        for (mutable i = 0; i < text.Length; i++)
          if (text[i] == '\n')
            map.Add(i + 1);
          else when (text[i] == '\r')
          {
            def next = i + 1;
            
            when (next < text.Length && text[next] != '\n')
              map.Add(i + 1);
          }
          
        map.ToArray()
      }
      
      _lineOffsetMap  = fillLineOffsetMap();
    }
          
    public ToLocation(node : PegLocated) : Location
    {
      def getTextPoint(pos : int) : TextPoint
      {
        def result = _lineOffsetMap.BinarySearch(e => e - pos);
        def index  = if (result < 0) (~result) - 1 else result;
        def offset = _lineOffsetMap[index];
        def ch     = pos - offset;
        
        TextPoint(index + 1, ch + 1)
      }
      
      def result = Location(_location.FileIndex,
        _location.Begin.Offcet(getTextPoint(node.StartPos)),
        _location.Begin.Offcet(getTextPoint(node.EndPos)));
      result
    }
    
    Parse(loc : Location, code : string) : PExpr
    {
      _typer.Manager.MacroColors.PushUseSiteColor();
      def pExpr = try     { MainParser.ParseExpr(_typer.Env, code, loc, false); }
                  finally { _typer.Manager.MacroColors.PopColor(); };
      pExpr
    }

    SplicablePExprName(value : X.Splicable) : PExpr
    {
      | Splicable.PrefiedName(ns, name) => DefineXmlnsImpl.MakeXNameFieldRef(_typer, ToLocation(value), ns, name)
      | Splicable.Value(value)          => <[ L.XName.Get($(value : string)) ]>
      | Splicable.Expr(expr)            => Parse(ToLocation(value), expr)
      | Splicable.ListExpr              => Message.FatalError(<#The "..$" not supportend in name context.#>);
    }
    
    SplicablePExprStr(value : X.Splicable) : PExpr
    {
      | Splicable.PrefiedName     => Message.FatalError(<#A value can't be a prefixed name.#>);
      | Splicable.Value(value)    => <[ $(value : string) ]>
      | Splicable.Expr(expr)      => Parse(ToLocation(value), expr)          
      | Splicable.ListExpr(_expr) => Message.FatalError(<#The "..$" not supportend in name context.#>);
    }
    public Transform(xmlAst : XmlAst) : PExpr
    {
      Util.locate(ToLocation(xmlAst), TransformImpl(xmlAst : XmlAst))
    }
    
    public TransformImpl(xmlAst : XmlAst) : PExpr
    {
      | Attr(name, Splicable.Expr as value)             => 
        def name1   = SplicablePExprName(name);
        def value1  = SplicablePExprStr(value);
        
        <[  { // If value of expression is null we return null which cause ignorin the attribute
              def value = $value1 : object;
              if (value == null) null else L.XAttribute($name1, value) 
            } ]>

      | Attr(name, value)             => 
        def name1   = SplicablePExprName(name);
        def value1  = SplicablePExprStr(value);
        
        <[ L.XAttribute($name1, $value1) ]>
        
      | Text(Splicable.Value(value))  => <[ L.XText($(value : string)) ]>
      | Text(_)  => assert(false);
      | Tag(name, content)            => 
        def name  = SplicablePExprName(name);
        def elems = content.Map(x => <[ $(Transform(x)) : object ]>);
          
        <[ L.XElement($name, array[..$elems]) ]>
        
      | TagSplice(expr, false)        => Parse(ToLocation(xmlAst), expr)
      | TagSplice(expr, true)         => <[ NColl.NToArray($(Parse(ToLocation(xmlAst), expr))) ]>
      | AttrSplice(expr, false)       => <[ $(Parse(ToLocation(xmlAst), expr)) : L.XAttribute ]>
      | AttrSplice(expr, true)        => 
        def x = Parse(ToLocation(xmlAst), expr);
        //assert2(false);
        <[ NColl.NToArray.[L.XAttribute]($(x) : IEnumerable[L.XAttribute]) ]> 
    }
  }
}
