using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle;

using System;
using System.Collections.Generic;
using LRPEGCC;

namespace Parsers
{
  /// <summary>
  /// Description of CalcParser.
  /// </summary>  
  [PegGrammar(start,
  grammar
  {  
    any                    = ['\u0000'..'\uFFFF'];
    digit                  = ['0'..'9'];
    spaces                 = ' '*;
    
    num             : int = digit + spaces;
    unaryMinus      : int = '-' spaces simplExpr;
    parenthesesExpr : int = '(' spaces sumOrSub ')' spaces;
    simplExpr       : int = num / parenthesesExpr / unaryMinus;
    mulOrDiv        : int = simplExpr (('*' / '/') spaces simplExpr)*;
    sumOrSub        : int = mulOrDiv  (('+' / '-') spaces mulOrDiv )*;
    start           : int = spaces sumOrSub !any;
  })]
  public class CalcParser : ParserBase[int]
  {  
    public this(text : string)
    {
        base(text);
    }
           
    private DoOperation(nonTerminalSymbols : list[int], terminalSymbols : list[string]) : int
    { 
      def doOp(x : int, y : int, op : string) : int
      {
        // TODO: there  is a bug : there spaces at the end of 'op'
        match(op[0])
        {
          | ('*') => x * y
          | ('/') => x / y
          | ('+') => x + y
          | ('-') => x - y
        }
      }
           
      mutable r = nonTerminalSymbols.Nth(0);
      for(mutable i = 0 ; i < terminalSymbols.Length; i++)
        r = doOp(r, nonTerminalSymbols.Nth(i + 1), terminalSymbols.Nth(i));
    
      r   
    }
    
    protected override DoGenerateResult(ruleName : string, nonTerminalSymbols : list[int], terminalSymbols : list[string]) : int
    {
    // TODO: autogenerate this block (probably)
    | ("start", [nts], [])         
    | ("parenthesesExpr", [nts], [])         
    | ("simplExpr", [nts], [])  => 
          nts
    // TODO: end todo
         
    | ("num", [], [tss])        =>
         int.Parse(tss)  
         
    | ("unaryMinus", _, _)      =>
         nonTerminalSymbols.Nth(0) * -1
         
    | ("mulOrDiv", _, _)        =>        
         DoOperation(nonTerminalSymbols, terminalSymbols)
                  
    | ("sumOrSub", _, _)        =>  
         DoOperation(nonTerminalSymbols, terminalSymbols)                  
         
    |  _                        =>
         throw Exception("sds");
    }
  }
}
