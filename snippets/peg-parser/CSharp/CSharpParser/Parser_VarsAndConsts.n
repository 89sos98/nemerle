using System;
using SCG = System.Collections.Generic;

using Nemerle.Assertions;
using Nemerle.Collections;
using Nemerle.Utility;
using Nemerle.Peg;
using LRPEGCC;

namespace CSharpParser
{
  public partial class Parser
  {
    //expressionInitializer   : VariableInitializer     = expression;
    expressionInitializer(expr : VToken[Expr]) : VariableInitializer
    {
      VariableInitializer.Expr(GetLocation(_), expr.Value)
    }

    //propertyInitializer     : VariableInitializer     = identifier "="s (complexInitializer / expressionInitializer);
    propertyInitializer(id    : VToken[Identifier],
                        _eq   : NToken, _ : NToken,
                        init  : VToken[VariableInitializer]) : VariableInitializer
    {
      VariableInitializer.Property(GetLocation(_), id.Value, init.Value)
    }

    //stackallocInitializer   : VariableInitializer     = "stackalloc"S anyType "["s expression "]"s;
    stackallocInitializer(_stackalloc : NToken, _ : NToken,
                          allocType   : VToken[TypeRef],
                          _lparen     : NToken, _ : NToken,
                          expr        : VToken[Expr],
                          _rparen     : NToken, _ : NToken) : VariableInitializer
    {
      VariableInitializer.StackAlloc(GetLocation(_, _rparen), allocType.Value, expr.Value)
    }

    //variableInitializerList : VariableInitializerList = variableInitializer (","s variableInitializer)*;
    variableInitializerList(head : VToken[VariableInitializer],
                            tail  : SCG.List[NToken * NToken * VToken[VariableInitializer]]) : list[VariableInitializer]
    {
      head.Cons(tail)
    }

    //complexInitializer      : VariableInitializer     = "{"s (variableInitializerList (","s)?)? "}"s;
    complexInitializer( _lparen : NToken, _ : NToken,
                        init    : option[VToken[list[VariableInitializer]] * option[NToken * NToken]],
                        _rparen : NToken, _ : NToken) : VariableInitializer
    {
      def loc = GetLocation(_);
      match(init) {
        | Some( (init, _) ) => VariableInitializer.Complex(loc, init.Value)
        | _                 => VariableInitializer.Complex(loc, [])
      }
    }

    //variableInitializer     : VariableInitializer     = complexInitializer
    //                                                  / stackallocInitializer
    //                                                  / expressionInitializer;
    variableInitializer(opt : VToken[VariableInitializer]) : VariableInitializer
    {
      opt.Value
    }

    //constantDeclarator  : ConstantDeclarator      = identifier "="s constantExpression;
    constantDeclarator( identifier          : VToken[Identifier],
                        _eq                 : NToken, _ : NToken,
                        constantExpression  : VToken[Expr]) : Location * Identifier * Expr
    {
      (GetLocation(_), identifier.Value, constantExpression.Value)
    }

    //constantDeclarators : ConstantDeclaratorList  = constantDeclarator (","s constantDeclarator)*;
    constantDeclarators(head : VToken[Location * Identifier * Expr],
                        tail  : SCG.List[NToken * NToken * VToken[Location * Identifier * Expr]]) : list[Location * Identifier * Expr]
    {
      head.Cons(tail)
    }

    //variableDeclarator  : VariableDeclarator      = identifier ("="s variableInitializer)?;
    variableDeclarator(identifier           : VToken[Identifier],
                       variableInitializer  : option[NToken * NToken * VToken[VariableInitializer]]) : Location * Identifier * VariableInitializer
    {
      def loc = GetLocation(_);
      match(variableInitializer) {
        | Some( (_, _, init) ) => (loc, identifier.Value, init.Value)
        | _                    => (loc, identifier.Value, null)
      }
    }

    //variableDeclarators : VariableDeclaratorList  = variableDeclarator (","s variableDeclarator)*;
    variableDeclarators(head : VToken[Location * Identifier * VariableInitializer],
                        tail  : SCG.List[NToken * NToken * VToken[Location * Identifier * VariableInitializer]]) : list[Location * Identifier * VariableInitializer]
    {
      head.Cons(tail)
    }
  }
}