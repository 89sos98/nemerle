using System;
using Nemerle;
using Nemerle.Collections;
using System.Collections.Generic;
using System.Windows.Forms;
using Nemerle.Compiler;
using System.Text;
using System.IO;
using System.Console;

namespace CSharpToNemerle
{
  class Engine
  {
    conv : CSharpToNemerleConvert=CSharpToNemerleConvert();
        
    mutable nfiles : list[string]=[];     
    mutable csfiles : list[string]=[];
    mutable out_dir : string;
    mutable err : bool;
     
    public IsErr : bool
    {
      get
      {
        nfiles.IsEmpty() && csfiles.IsEmpty()
      }
    }
     
    Error(str : string) : void
    {
      err=true;
      WriteLine($"Неизвестный параметр $str\n");
    }
     
    public ProcessArgs(args : list[string]) : bool
    {
      |"/d" :: dir :: tail => csfiles=csfiles+Directory.GetFiles(dir,"*.cs",SearchOption.AllDirectories).NToList();
          ProcessArgs(tail)
      |"/nd" :: dir :: tail =>  nfiles=nfiles+Directory.GetFiles(dir,"*.n",SearchOption.AllDirectories).NToList();       
          ProcessArgs(tail) 
      |"/nf" :: file :: tail => nfiles::=file;ProcessArgs(tail)            
      |"/out" :: dir :: tail => out_dir=dir;ProcessArgs(tail)
      |file :: tail => if (File.Exists(file)) csfiles::=file; else Error(file);
          ProcessArgs(tail)                         
      |_ => !IsErr
    }
     
    
    public Run() : void
    {
      def create_out_name(f)
      {          
        def path=if (out_dir!=null)
                  {              
                    def fname=Path.GetFileName(f);
                    Path.Combine(out_dir,fname);
                  } else
                  {
                    def out_file=Path.GetFileNameWithoutExtension(f)+"_conv.n";
                    Path.Combine(Path.GetDirectoryName(f),out_file);
                  }   
        if (File.Exists(path)) path else throw FileNotFoundException("Файл не найден",path);
      }
            
      try
      {               
        foreach (file in csfiles)
        {
          WriteLine($"Converting file $file");
          conv.Convert(file,out_dir);
        }
        foreach (file in nfiles)
        {
          WriteLine($"Parsing and printing file $file");
          conv.PrintNFile(file,create_out_name(file));
        }
        WriteLine("Conversion completed successfully");
      } 
      catch
      {
        |x is FileNotFoundException => WriteLine($"$(x.Message) $(x.FileName)");          
        |x => WriteLine($"Возникло исключение: $(x.Message)");
      }      
    }
  }
    
  module Program
  {
    /// <summary>
    /// The main entry point for the application.
    /// </summary>    
    Main(args : array[string]) : void
    {        
      def engine=Engine();        
      WriteLine(<#Конвертер C# в Nemerle v1.0#>);
      if (engine.ProcessArgs(args.NToList()))             
        engine.Run(); else
      {                
        WriteLine(<#Использование: cs2nemerle.exe [flags] file1,file2...
Где: flags:
     /d directory - показывает директорию в которой лежат файлы .cs для конвертации
     /nd directory - директория файлов Nemerle для парсинга и вывода
     /nf file - файл Nemerle для парсинга и вывода
     /out directory - выходная директория
     file1... - файлы .cs через пробел для конвертации
                #>);
      }      
      WriteLine("Press any key to exit");
      _ = ReadLine();      
    }
  }
}
