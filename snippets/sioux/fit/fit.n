namespace Sioux
{

using Nemerle.Collections;
using Nemerle.Xml;
using System.Xml;
using System.Xml.Xsl;

public class FitApp extends Application
{
  static submissions : Hashtable (string, Submission);
  static mutable list_xsl : XslTransform;
  static mutable list_xml : XmlDocument;
  
  class Submission 
  {
    values : Hashtable (string, string);

    val (name : string) : string
    {
      get_val (values, name)
    }

    generate_id () : void
    {
      def sb = System.Text.StringBuilder (17);
      for (mutable i <- 0; i < 16; i <- i + 1) {
        ignore (sb.Append (((random.Next (24) + ('a' :> int)) :> char)))
      };
      values.Set ("id", sb.ToString ());
    }

    internal store () : void
    {
      lock (submissions)
        submissions.Set (val ("id"), this);
    }

    internal read_post (pv : Hashtable (string, string)) : void
    {
      def old_id = val ("id");
      def read (name) { values.Set (name, get_val (pv, name).Trim ()) };
      iter_all (read);
      when (val ("id") == "")
        values.Set ("id", old_id);
      store ()
    }

    internal serialize (doc : XmlDocument) : XmlNode
    {
      def n = doc.CreateElement ("submission");
      def add_field (name) {
        def n' = doc.CreateElement (name);
        ignore (n.AppendChild (n'));
        ignore (n'.AppendChild (doc.CreateTextNode (val (name))));
      };
      iter_all (add_field);
      n
    }

    internal unserialize (n : XmlNode) : void
    {
      def add_field (n : XmlNode) {
        when (n != null) {
          when (n.NodeType == XmlNodeType.Element) {
            values.Set (n.Name, n.InnerText)
          };
          add_field (n.NextSibling)
        }
      };
      add_field (n.FirstChild)
    }

    iter_all (f : string -> void) : void
    {
      List.Iter (f, text_fields);
      List.Iter (f, bool_fields);
      List.Iter (f, edit_fields);
      f ("id");
    }

    internal dump () : string
    {
      def res = System.Text.StringBuilder ();
      def read (name) { ignore (res.Append (sprintf ("%s='%s'\n", name, val (name)))) };
      iter_all (read);
      res.ToString ()
    }

    internal this ()
    {
      values <- Hashtable ();
      generate_id ();
    }
    
    internal get_form () : XmlTemplate
    {
      def t = XmlTemplate ("fit/submission-form.xml");
      
      List.Iter (fun (name) {
                   t.SetText (name + "/value", val (name))
                 }, "id" :: text_fields);
      List.Iter (fun (name) {
                   when (val (name) != "")
                     t.SetText (name + "/checked", "checked")
                 }, bool_fields);
      List.Iter (fun (name) {
                   t.SetText (name, val (name))
                 }, edit_fields);
      
      t
    }

    internal get_confirm_page () : XmlTemplate
    {
      def t = XmlTemplate ("fit/submission.xml");
      List.Iter (fun (name) { t.SetText (name, val (name)) }, ["email"]);
      t.SetText ("dump_here", dump ());
      t.SetText ("edit/href", "/edit?id=" + val ("id"));
      t
    }

    static text_fields : list (string);
    static bool_fields : list (string);
    static edit_fields : list (string);
    static random : System.Random;

    static this ()
    {
      text_fields <- [
        "first_name",
        "last_name",
        "email",
        "account",
        "university",
        "department",
        "institute"
      ];
      bool_fields <- [
        "vega",
        "treking",
        "single_room"
      ];
      edit_fields <- ["remarks"];
      random <- System.Random ();
    }
  }
  
  static get_val (h : Hashtable (string, string), key : string) : string
  {
    match (h.Get (key)) {
      | Some (res) => res
      | None => ""
    }
  }

  submission_form () : void
  {
    def subm = Submission ();
    FormTemplate <- subm.get_form ();
  }

  submission () : void
  {
    def subm = Submission ();
    subm.read_post (PostVars);
    
    FormTemplate <- subm.get_confirm_page ();
  }

  edit () : void
  {
    def res = lock (submissions) submissions.Get (get_val (GetVars, "id"));
    match (res) {
      | Some (subm) =>
        FormTemplate <- subm.get_form ();
      | None =>
        FormTemplate <- XmlTemplate ("fit/error.xml");
        FormTemplate.SetText ("msg", "ZgÅ‚oszenie nie istnieje.")
    }
  }

  list_data () : void
  {
    FormTemplate <- XmlTemplate (list_xml);
  }

  main () : void
  {
    FormTemplate <- XmlTemplate ("fit/main.xml");
  }

  static cycle_backup () : void
  {
    for (mutable i <- 49; i >= 0; i <- i - 1) {
      try {
        System.IO.File.Delete (sprintf ("fit/data/data.xml.%d", i + 1))
      } catch { _ : System.Exception => () };
      try {
        System.IO.File.Move (sprintf ("fit/data/data.xml.%d", i),
                             sprintf ("fit/data/data.xml.%d", i + 1))
      } catch { _ : System.Exception => () };
    };
    try {
      System.IO.File.Move ("fit/data/data.xml", "fit/data/data.xml.0");
    } catch { _ : System.Exception => () };
  }

  static serialize () : void
  {
    def doc = XmlDocument ();
    def n = doc.CreateElement ("submissions");
    ignore (doc.AppendChild (n));
    def add (_, subm : Submission) {
      ignore (n.AppendChild (subm.serialize (doc)))
    };
    lock (submissions) {
      cycle_backup ();
      submissions.Iter (add);
      doc.Save ("fit/data/data.xml");

      def reader = list_xsl.Transform (n, null, (null : XmlResolver));
      def doc' = XmlDocument ();
      doc'.XmlResolver <- null;
      doc'.Load (reader);
      list_xml <- doc';
    }
  }

  static unserialize () : void
  {
    def doc = XmlDocument ();
    doc.Load ("fit/data/data.xml");
    def add (n : XmlNode) {
      when (n != null) {
        when (n.NodeType == XmlNodeType.Element) {
          def subm = Submission ();
          subm.unserialize (n);
          subm.store ();
        };
        add (n.NextSibling)
      }
    };
    add (doc.DocumentElement.FirstChild);
  }
  
  override protected virtual Run () : void
  {
    match (PageName) {
      | "/submission" => submission (); serialize ()
      | "/edit" => edit ()
      | "/submission_form" => submission_form ()
      | "/list" => list_data ()
      | _ => main ()
    }
  }

  static this ()
  {
    list_xsl <- XslTransform ();
    list_xsl.Load ("fit/display-table.xsl", null);
    
    submissions <- Hashtable (1000);
    try {
      unserialize ()
    } catch {
      | _ : System.IO.FileNotFoundException => ()
    };
    
    serialize ();
  }
}


} // end Sioux
