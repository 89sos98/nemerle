
  postadd macro Invariant (ty : TypeBuilder, body) {
    ty.Define ( <[ decl:
      public mutable _N_invariant_lock : bool;
    ]> );
    ty.Define ( <[ decl:
      public _N_invariant () : void
      {
        assert ($body)
      }
    ]> );
  }

  macro @expose (exposed, body) 
  syntax ("expose", "(", exposed, ")", body) {
    <[ 
      def e = $exposed;
      assert (e != null);
      lock (e) {
        when (e._N_invariant_lock)
          throw System.Exception ();
      
        e._N_invariant_lock <- true
      };
      mutable need_to_check <- false;
            
      try {
        try {
          $body;
    	  need_to_check <- true
        }
        catch {
        /*
          | e : IChecked => need_to_check <- true; rethrow
         */ 
          | _e : System.Exception => ()	  
        }
      }
      finally {
        lock (e) {
          e._N_invariant_lock <- false;
	  when (need_to_check)
	    e._N_invariant ()        
        };
      }
    ]>     
  }

