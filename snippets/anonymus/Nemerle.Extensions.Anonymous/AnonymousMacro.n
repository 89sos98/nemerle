using System;

using Nemerle;
using Nemerle.Assertions;
using Nemerle.Utility;
using Nemerle.Compiler;
using Nemerle.Collections;

using PT = Nemerle.Compiler.Parsetree;
using TT = Nemerle.Compiler.Typedtree;

namespace Nemerle.Extensions {

    public macro AnonymousClassCtor(params ctor_def : array[expr]) 
        syntax("new", "(", ctor_def, ")" )
    {
        AnonymousMacroImpl.MakeAnonymousClassCtor(Macros.ImplicitCTX(), NList.ToList(ctor_def))
    }

    internal module AnonymousMacroImpl {

        [Record]
        private class FieldInfo {
            [Accessor] private generic_arg : PT.Name;
            [Accessor] private field : PT.Name;
            [Accessor] private value : PT.PExpr;
            public PropertyName : string { get { field.ToString() } }
            public FieldName : string { get { "_" + field.ToString() } }
        }

        public MakeAnonymousClassCtor(typer : Typer, ctor_def : list[PT.PExpr]) : PT.PExpr {
            def fields = ctor_def.FoldRight([], fun(cd, acc) {
                def append_field_info(field_init) {
                    | <[ $(field : name) = $value ]> => FieldInfo(Macros.NewSymbol(field.Id), field, value) :: acc
                    | _ => Message.Error($"Expected 'a = foo()' got '$cd'."); acc
                }
                match(cd) {
                    | PT.PExpr.Member(_, member) as member_access =>
                        append_field_info(<[ $(member.GetName() : name) = $member_access ]>)
                    | _ => append_field_info(cd)
                }
            });
            def name = GetAnonymousClassName(fields);
            def ty = BuildAnonymousClass(typer, name, fields);
            <[ $(ty.FullName : usesite) ( ..$(fields.Map(f => f.Value) ) ) ]>
        }

        private GetAnonymousClassName(fields : list[FieldInfo]) : string {
            fields.FoldLeft(System.Text.StringBuilder("_N_Anonymous"),
                (f, sb) => sb.Append("_<").Append(f.PropertyName).Append(">"))
            .ToString()
        }

        private equality_comparer(t : PT.PExpr) : PT.PExpr {
            <[ System.Collections.Generic.EqualityComparer.[ $t ].Default ]>
        }

        private equality_comparer(t : PT.Name) : PT.PExpr {
            <[ System.Collections.Generic.EqualityComparer.[ $(t : name) ].Default ]>
        }

        private BuildAnonymousClass(typer : Typer, name : string, fields : list[FieldInfo]) : TypeInfo {
            match(typer.Env.LookupType([name])) {
                | Some(ty) => ty
                | None() => {
                    // Entire class definition is placed in global namespace
                    def aclass = {
                        // Format string for debugger view
                        def debugger_display_fmt = {
                            mutable prefix = null : string;
                            fields.FoldLeft(System.Text.StringBuilder("\\{"),
                                (f, sb) => sb.Append({prefix = if(null == prefix) " " else ", "; prefix })
                                    .Append($"$(f.PropertyName) = {$(f.PropertyName)}")
                            ).Append(" \\}").ToString()
                        }

                        // fields and properties declaration
                        def members = fields.Map(field => <[ decl :
                                [Nemerle.Utility.Accessor($(field.PropertyName : usesite))]
                                private $(field.FieldName : usesite) : $(field.GenericArg : name);
                        ]>);

                        typer.Manager.CoreEnv.Define(match(fields) {
                            | [] =>
                                <[ decl:
                                    [Record,
                                        System.Diagnostics.DebuggerDisplay( $(debugger_display_fmt : string) )]
                                    internal sealed class $(name : usesite) : Nemerle.Extensions.IAnonymous {
                                        ..$members
                                    }
                                ]>
                            | _ =>
                                def generic_args_decl = fields.Map(f => PT.Splicable.Name(f.GenericArg));
                                <[ decl:
                                    [Record,
                                        System.Diagnostics.DebuggerDisplay( $(debugger_display_fmt : string) )]
                                    internal sealed class $(name : usesite) [ ..$generic_args_decl ] : Nemerle.Extensions.IAnonymous {
                                        ..$members
                                    }
                                ]>
                        })
                    }

                    def generic_args = fields.Map(f => <[ $(f.GenericArg : name) ]>);

                    // GetFields() implementation
                    {
                        def field_array_name = Macros.NewSymbol("field_array");
                        def field_array = fields.Map(f => <[ $(f.PropertyName : string) ]>);
                        aclass.Define(<[ decl: 
                                private static $(field_array_name : name) : System.Collections.ObjectModel.ReadOnlyCollection[string] = System.Collections.ObjectModel.ReadOnlyCollection(array[ ..$field_array ]);
                        ]>);
                        aclass.Define(<[ decl:
                                GetFields() : System.Collections.ObjectModel.ReadOnlyCollection[string] implements Nemerle.Extensions.IAnonymous.GetFields {
                                    $(field_array_name : name)
                                }
                        ]>);
                    }


                    // Item indexer implementation
                    {
                        def body = match(fields) {
                            | [] => <[ ignore(field); null ]>
                            | _ =>
                                def cases = fields.FoldRight([<[ case : | _ => null ]>],
                                    (f, cases) => <[ case: | $(f.PropertyName : string) => this.$(f.FieldName : usesite) : object ]> :: cases );
                                <[ match(field) { ..$cases } ]>
                        }
                        aclass.Define(<[ decl:
                                Item[field : string] : object implements Nemerle.Extensions.IAnonymous.Item {
                                    get { $body }
                                }
                        ]>);
                    }


                    // GetContent implementation 
                    {
                        def body = match(fields) {
                            | [] => <[ null ]>
                            | field :: [] => <[ this.$(field.FieldName : usesite) : object ]>
                            | _ =>
                                def tuple_args = fields.Map(f => <[ this.$(f.FieldName : usesite) ]>);
                                <[ Nemerle.Builtins.Tuple.[ ..$generic_args ] ( ..$tuple_args ) : object ]>
                        }
                        aclass.Define(<[ decl:
                                GetContent() : object implements Nemerle.Extensions.IAnonymous.GetContent {
                                    $body 
                                }
                        ]>);
                    }


                    // ToString implementation
                    {
                        def sb_name = Macros.NewSymbol("sb");
                        mutable prefix = null : string;
                        def sb_list = fields.Map(f => <[ 
                            _ = $(sb_name : name)
                                .Append($( {prefix = if(null == prefix) " " else ", "; prefix} : string))
                                .Append($(f.PropertyName + " = " : string))
                                .Append(this.$(f.FieldName : usesite)) ]> );
                        aclass.Define(<[ decl:
                                public override ToString() : string {
                                    def $(sb_name : name) = System.Text.StringBuilder("{");
                                    { ..$sb_list }
                                    $(sb_name : name).Append(" }") .ToString()
                                }
                        ]>);
                    }

                    // GetHashCode implementation, I think that it is better that StructuralHashCode
                    {
                        def hash_method = match(fields) {
                            | [] => <[ decl:
                                    public override GetHashCode() : int { 0 }
                                ]>
                            | _ =>
                                def (p1, p2) = HashHelper.GetPrimePair(fields.Length);
                                def body = fields.Map(f => <[
                                    hash = unchecked( ($(p2 : int) * hash)
                                        + $(equality_comparer(f.GenericArg)).GetHashCode(this.$(f.FieldName : usesite)) ) ]>);
                                <[ decl:
                                    public override GetHashCode() : int { 
                                        mutable hash : int = $(p1 : int);
                                        { ..$body }
                                        hash
                                    }
                                ]>
                        }
                        aclass.Define(hash_method);
                    }

                    def equals_generic_body = fields.FoldLeft(<[ true ]>, (f, acc) => <[
                        $acc && $(equality_comparer(f.GenericArg)).Equals(this.$(f.FieldName : usesite), other.$(f.FieldName : usesite))
                    ]>);

                    def equals_anonymous_body = <[
                        this.GetType().FullName.Equals(other.GetType().FullName)
                            && $(equality_comparer( <[object]> )).Equals( (this : Nemerle.Extensions.IAnonymous).GetContent(), other.GetContent())
                    ]>;

                    // Equals(other : object) implementation
                    {
                        def body = match(fields) {
                            | [] => <[
                                    match(other) {
                                        | _ is $(name : usesite) => true
                                        | other is Nemerle.Extensions.IAnonymous => (other.GetFields().Count == 0)
                                        | _ => false
                                    }
                                ]>
                            | _ =>
                                <[
                                    match(other) {
                                        | other is $(name : usesite).[ ..$generic_args ] => $equals_generic_body
                                        | other is Nemerle.Extensions.IAnonymous => $equals_anonymous_body
                                        | _ => false
                                    }
                                ]>
                        }
                        aclass.Define(<[ decl:
                            public override Equals(other : object) : bool { $body }
                        ]>);
                    }

                    // Equals(other : IAnonymous) implementation
                    {
                        def body = match(fields) {
                            | [] => <[
                                    match(other) {
                                        | _ is $(name : usesite) => true
                                        | _ => !ReferenceEquals(null, other) && (other.GetFields().Count == 0)
                                    }
                                ]>
                            | _ => <[ 
                                    match(other) {
                                        | other is $(name : usesite).[ ..$generic_args ] => $equals_generic_body
                                        | _ => !ReferenceEquals(null, other) && $equals_anonymous_body
                                    }
                                ]>
                        }
                        aclass.Define(<[ decl:
                            Equals(other : IAnonymous) : bool implements System.IEquatable[Nemerle.Extensions.IAnonymous].Equals {
                                $body
                            }
                        ]>);
                    }


                    // Typed Equals implementation
                    {
                        def (ty, body) = match(fields) {
                            | [] =>
                                (<[ $(name : usesite) ]>,
                                    <[ !ReferenceEquals(null, other) ]>)
                            | _ =>
                                (<[ $(name : usesite) [ ..$generic_args ] ]>,
                                    <[ !ReferenceEquals(null, other) && $equals_generic_body ]>)
                        }
                        aclass.Define(<[ decl:
                            public Equals(other : $ty) : bool { $body }
                        ]>);
                    }


                    // Equality operation
                    {
                        def define_op(a, b, t) {
                            aclass.Define(<[ decl:
                                public static @== (a : $a, b : $b) : bool { $(equality_comparer(t)).Equals(a, b) }
                            ]>);
                            aclass.Define(<[ decl:
                                public static @!= (a : $a, b : $b) : bool { ! $(equality_comparer(t)).Equals(a, b) }
                            ]>);
                        }

                        def ty = match(fields) {
                            | [] => <[ $(name : usesite) ]>
                            | _ => <[ $(name : usesite) [ ..$generic_args ] ]>
                        }
                        def an = <[ Nemerle.Extensions.IAnonymous ]>;
                        def ob = <[ object ]>;
                        
                        define_op(ty, ty, ty);
                        define_op(ty, an, an);
                        define_op(an, ty, an);
                        define_op(ty, ob, ob);
                        define_op(ob, ty, ob);
                    }


                    // Tuple explicit cast
                    match(fields) {
                        | [] => ()
                        | field :: [] =>
                            aclass.Define(<[ decl:
                                public static @:> ([Nemerle.Assertions.NotNull] obj : $(name : usesite)[ $(field.GenericArg : name) ]) : $(field.GenericArg : name) {
                                    obj.$(field.FieldName : usesite)
                                }
                            ]>)
                        | _ =>
                            def tuple_args = fields.Map(f => <[ obj.$(f.FieldName : usesite) ]>);
                            aclass.Define(<[ decl:
                                public static @:> ([Nemerle.Assertions.NotNull] obj : $(name : usesite)[ ..$generic_args ]) : Nemerle.Builtins.Tuple[ ..$generic_args ] {
                                    Nemerle.Builtins.Tuple( ..$tuple_args )
                                }
                            ]>)
                    }


                    unless(typer.InErrorMode)
                        aclass.Compile();
                    aclass
                }
            }
        }

    }

    internal module HashHelper {

        private primes : array[int] = array[
            885551, 959773, 909287, 842801,
            791081, 966041, 728647, 967139,
            912869, 801137, 804511, 913943,
            914461, 700127, 940421, 962561,
            756127, 757927, 953789, 954181 ];

        public GetPrimePair(x : int) : int * int {
            def half_len = primes.Length / 2;
            def pos = x % half_len;
            (primes[pos], primes[pos + half_len])
        }
    }

}
