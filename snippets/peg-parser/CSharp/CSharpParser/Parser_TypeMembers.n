using System;
using SCG = System.Collections.Generic;

using Nemerle.Assertions;
using Nemerle.Collections;
using Nemerle.Utility;
using Nemerle.Peg;
using LRPEGCC;

namespace CSharpParser
{
  public partial class Parser
  {
    #region Constant

    //constantDeclaration : TypeMemberList = attributes? modifiers? "const"S anyType constantDeclarators ";"s;
    constantDeclaration(attributes  : option[VToken[list[CustomAttribute]]],
                        modifiers   : option[VToken[list[Modifier]]],
                        _const      : NToken, _ : NToken,
                        memberType  : VToken[TypeRef],
                        decls       : VToken[list[Location * Identifier * Expr]],
                        _semicolon  : NToken, _ : NToken) : list[TypeMember]
    {
      decls.Value.Map((loc, id, expr) =>
        TypeMember.Constant(
          loc,
          attributes.WithDefault([]),
          modifiers.WithDefault([]),
          Expr.Ref(id.Location, id),
          memberType.Value,
          expr))
    }

    #endregion

    #region Field

    //fieldDeclaration    : TypeMemberList = attributes? modifiers? anyType variableDeclarators ";"s;
    fieldDeclaration( attributes  : option[VToken[list[CustomAttribute]]],
                      modifiers   : option[VToken[list[Modifier]]],
                      memberType  : VToken[TypeRef],
                      decls       : VToken[list[Location * Identifier * VariableInitializer]],
                      _semicolon  : NToken, _ : NToken) : list[TypeMember]
    {
      decls.Value.Map((loc, id, init) =>
        TypeMember.Field(
          loc,
          attributes.WithDefault([]),
          modifiers.WithDefault([]),
          Expr.Ref(id.Location, id),
          memberType.Value,
          init))
    }

    #endregion

    #region Fixed size buffer

    //fixedSizeBufferDeclarator   : FixedSizeBufferDeclarator     = identifier "["s constantExpression "]"s;
    fixedSizeBufferDeclarator(identifier          : VToken[Identifier],
                              _lparen             : NToken, _ : NToken,
                              constantExpression  : VToken[Expr],
                              _rparen             : NToken, _ : NToken) : Location * Identifier * Expr
    {
      (GetLocation(_), identifier.Value, constantExpression.Value)
    }

    //fixedSizeBufferDeclarators  : FixedSizeBufferDeclaratorList = fixedSizeBufferDeclarator (","s fixedSizeBufferDeclarator)*;
    fixedSizeBufferDeclarators( head : VToken[Location * Identifier * Expr],
                                tail : SCG.List[NToken * NToken * VToken[Location * Identifier * Expr]]) : list[Location * Identifier * Expr]
    {
      head.Cons(tail)
    }

    //fixedSizeBufferDeclaration  : TypeMemberList                = attributes? modifiers? "fixed"S anyType fixedSizeBufferDeclarators ";"s;
    fixedSizeBufferDeclaration( attributes  : option[VToken[list[CustomAttribute]]],
                                modifiers   : option[VToken[list[Modifier]]],
                                _fixed      : NToken, _ : NToken,
                                elementType : VToken[TypeRef],
                                decls       : VToken[list[Location * Identifier * Expr]],
                                _semicolon  : NToken, _ : NToken) : list[TypeMember]
    {
      decls.Value.Map((loc, id, size) =>
        TypeMember.FixedSizeBuffer(
          loc,
          attributes.WithDefault([]),
          modifiers.WithDefault([]),
          Expr.Ref(id.Location, id),
          elementType.Value,
          size))
    }

    #endregion

    #region Method

    //methodBody  : Statement = emptyStatement / block;
    methodBody(opt : VToken[Statement])  : Statement
    {
      opt.Value
    }

    //methodDeclaration : TypeMemberList = attributes? modifiers? anyType qualifiedIdentifier typeParameterList? "("s formalParameterList? ")"s typeParameterConstraintsClauses? methodBody;
    methodDeclaration(attributes          : option[VToken[list[CustomAttribute]]],
                      modifiers           : option[VToken[list[Modifier]]],
                      returnType          : VToken[TypeRef],
                      name                : VToken[Expr],
                      typeParameterList   : option[VToken[list[TypeParameter]]],
                      _lparen             : NToken, _ : NToken,
                      formalParameterList : option[VToken[list[FormalParameter]]],
                      _rparen             : NToken, _ : NToken,
                      constraints         : option[VToken[list[TypeParameterConstraintClause]]],
                      body                : VToken[Statement]) : list[TypeMember]
    {
      [TypeMember.Method(
        GetLocation(_),
        attributes.WithDefault([]),
        modifiers.WithDefault([]),
        name.Value,
        returnType.Value,
        formalParameterList.WithDefault([]),
        typeParameterList.WithDefault([]),
        constraints.WithDefault([]),
        body.Value)]
    }

    #endregion

    #region Property

    //getAccessorDeclaration  : PropertyAccessor  = attributes? modifiers? "get"S methodBody;
    getAccessorDeclaration( attributes  : option[VToken[list[CustomAttribute]]],
                            modifiers   : option[VToken[list[Modifier]]],
                            _get        : NToken, _ : NToken,
                            body        : VToken[Statement]) : PropertyAccessor
    {
      PropertyAccessor(
        GetLocation(_),
        attributes.WithDefault([]),
        modifiers.WithDefault([]),
        body.Value)
    }

    //setAccessorDeclaration  : PropertyAccessor  = attributes? modifiers? "set"S methodBody;
    setAccessorDeclaration( attributes  : option[VToken[list[CustomAttribute]]],
                            modifiers   : option[VToken[list[Modifier]]],
                            _set        : NToken, _ : NToken,
                            body        : VToken[Statement]) : PropertyAccessor
    {
      PropertyAccessor(
        GetLocation(_),
        attributes.WithDefault([]),
        modifiers.WithDefault([]),
        body.Value)
    }

    //accessorDeclarations_1  : PropertyAccessors = getAccessorDeclaration setAccessorDeclaration?;
    accessorDeclarations_1( getter : VToken[PropertyAccessor],
                            setter : option[VToken[PropertyAccessor]]) : PropertyAccessor * PropertyAccessor
    {
      (getter.Value, setter.WithDefault(null))
    }

    //accessorDeclarations_2  : PropertyAccessors = setAccessorDeclaration getAccessorDeclaration?;
    accessorDeclarations_2( setter : VToken[PropertyAccessor],
                            getter : option[VToken[PropertyAccessor]]) : PropertyAccessor * PropertyAccessor
    {
      (getter.WithDefault(null), setter.Value)
    }

    //accessorDeclarations    : PropertyAccessors = accessorDeclarations_1 / accessorDeclarations_2;
    accessorDeclarations(opt : VToken[PropertyAccessor * PropertyAccessor]) : PropertyAccessor * PropertyAccessor
    {
      opt.Value
    }

    //propertyDeclaration     : TypeMemberList    = attributes? modifiers? anyType qualifiedIdentifier "{"s accessorDeclarations "}"s;
    propertyDeclaration(attributes  : option[VToken[list[CustomAttribute]]],
                        modifiers   : option[VToken[list[Modifier]]],
                        memberType  : VToken[TypeRef],
                        name        : VToken[Expr],
                        _lparen     : NToken, _ : NToken,
                        accessors   : VToken[PropertyAccessor * PropertyAccessor],
                        _rparen     : NToken, _ : NToken) : list[TypeMember]
    {
      def (getter, setter) = accessors.Value;
      [TypeMember.Property(
        GetLocation(_),
        attributes.WithDefault([]),
        modifiers.WithDefault([]),
        name.Value,
        memberType.Value,
        getter,
        setter)]
    }

    #endregion

    #region Event

    //addAccessorDeclaration      : PropertyAccessor   = attributes? "add"S block;
    addAccessorDeclaration( attributes  : option[VToken[list[CustomAttribute]]],
                            _add        : NToken, _ : NToken,
                            body        : VToken[Statement]) : PropertyAccessor
    {
      PropertyAccessor(
        GetLocation(_),
        attributes.WithDefault([]),
        [],
        body.Value)
    }

    //removeAccessorDeclaration   : PropertyAccessor   = attributes? "remove"S block;
    removeAccessorDeclaration(attributes  : option[VToken[list[CustomAttribute]]],
                              _remove     : NToken, _ : NToken,
                              body        : VToken[Statement]) : PropertyAccessor
    {
      PropertyAccessor(
        GetLocation(_),
        attributes.WithDefault([]),
        [],
        body.Value)
    }

    //eventAccessorDeclarations_1 : EventAccessors  = addAccessorDeclaration removeAccessorDeclaration;
    eventAccessorDeclarations_1(adder   : VToken[PropertyAccessor],
                                remover : VToken[PropertyAccessor]) : PropertyAccessor * PropertyAccessor
    {
      (adder.Value, remover.Value)
    }

    //eventAccessorDeclarations_2 : EventAccessors  = removeAccessorDeclaration addAccessorDeclaration;
    eventAccessorDeclarations_2(remover : VToken[PropertyAccessor],
                                adder   : VToken[PropertyAccessor]) : PropertyAccessor * PropertyAccessor
    {
      (adder.Value, remover.Value)
    }

    //eventAccessorDeclarations   : EventAccessors  = eventAccessorDeclarations_1 / eventAccessorDeclarations_2;
    eventAccessorDeclarations(opt : VToken[PropertyAccessor * PropertyAccessor]) : PropertyAccessor * PropertyAccessor
    {
      opt.Value
    }

    //eventDeclaration_1  : TypeMemberList = attributes? modifiers? "event"S anyType qualifiedIdentifier "{"s eventAccessorDeclarations "}"s;
    eventDeclaration_1( attributes  : option[VToken[list[CustomAttribute]]],
                        modifiers   : option[VToken[list[Modifier]]],
                        _event      : NToken, _ : NToken,
                        memberType  : VToken[TypeRef],
                        name        : VToken[Expr],
                        _lparen     : NToken, _ : NToken,
                        accessors   : VToken[PropertyAccessor * PropertyAccessor],
                        _rparen     : NToken, _ : NToken) : list[TypeMember]
    {
      def (adder, remover) = accessors.Value;
      [TypeMember.Event(
        GetLocation(_),
        attributes.WithDefault([]),
        modifiers.WithDefault([]),
        name.Value,
        memberType.Value,
        adder,
        remover,
        null)]
    }

    //eventDeclaration_2  : TypeMemberList = attributes? modifiers? "event"S anyType variableDeclarators ";"s;
    eventDeclaration_2( attributes  : option[VToken[list[CustomAttribute]]],
                        modifiers   : option[VToken[list[Modifier]]],
                        _event      : NToken, _ : NToken,
                        memberType  : VToken[TypeRef],
                        decls       : VToken[list[Location * Identifier * VariableInitializer]],
                        _semicolon : NToken, _ : NToken)  : list[TypeMember]
    {
      decls.Value.Map((loc, id, init) =>
        TypeMember.Event(
          loc,
          attributes.WithDefault([]),
          modifiers.WithDefault([]),
          Expr.Ref(id.Location, id),
          memberType.Value,
          null,
          null,
          init))
    }

    //eventDeclaration    : TypeMemberList = eventDeclaration_1 / eventDeclaration_2;
    eventDeclaration(opt : VToken[list[TypeMember]]): list[TypeMember]
    {
      opt.Value
    }

    #endregion

    #region Indexer

    //indexerDeclaration  : TypeMemberList = attributes? modifiers? anyType (qualifiedIdentifier "."s)? "this"S "["s formalParameterList? "]"s "{"s accessorDeclarations "}"s;
    indexerDeclaration( attributes        : option[VToken[list[CustomAttribute]]],
                        modifiers         : option[VToken[list[Modifier]]],
                        returnType        : VToken[TypeRef],
                        ifaceType         : option[VToken[Expr] *  NToken * NToken],
                        _this             : NToken, _ : NToken,
                        _lparen1          : NToken, _ : NToken,
                        formalParameters  : option[VToken[list[FormalParameter]]],
                        _rparen1          : NToken, _ : NToken,
                        _lparen2          : NToken, _ : NToken,
                        accessors         : VToken[PropertyAccessor * PropertyAccessor],
                        _rparen2          : NToken, _ : NToken,) : list[TypeMember]
    {
      def name = match(ifaceType) {
        | Some( (ifaceType, _, _) ) =>
          def iface = ifaceType.Value;
          def member = Expr.This(GetLocation(_this));
          Expr.Member(iface.Location + member.Location, iface, member)
        | _ => Expr.This(GetLocation(_this))
      }
      def (getter, setter) = accessors.Value;
      [TypeMember.Indexer(
        GetLocation(_),
        attributes.WithDefault([]),
        modifiers.WithDefault([]),
        name,
        returnType.Value,
        formalParameters.WithDefault([]),
        getter,
        setter)]
    }

    #endregion

    #region Operator

    //overloadableOperator : Identifier  = ("++" / "--" / "!" / "~" / "+" / "-" / "*" / "/" / "%" / "&" / "|"
    //                                     / "^" / "<<" / ">>" / "==" / "!=" / ">" / "<" / ">=" / "<=" / "true" / "false")S;
    overloadableOperator(op : NToken, _ : NToken) : Identifier
    {
      Identifier(GetLocation(_), op.GetText())
    }

    //overloadableOperatorDeclarator  : OperatorDeclarator = anyType "operator"S overloadableOperator;
    overloadableOperatorDeclarator( returnType  : VToken[TypeRef],
                                    _operator   : NToken, _ : NToken,
                                    op          : VToken[Identifier]) : Identifier * TypeRef
    {
      (op.Value, returnType.Value)
    }

    //conversionOperatorDeclarator    : OperatorDeclarator = ("implicit" / "explicit")S "operator"S anyType;
    conversionOperatorDeclarator( op          : NToken, _ : NToken,
                                  _operator   : NToken, _ : NToken,
                                  returnType  : VToken[TypeRef]) : Identifier * TypeRef
    {
      (Identifier(GetLocation(op), op.GetText()), returnType.Value)
    }

    //operatorDeclarator : OperatorDeclarator = conversionOperatorDeclarator
    //                                        / overloadableOperatorDeclarator;
    operatorDeclarator(decl : VToken[Identifier * TypeRef]) : Identifier * TypeRef
    {
      decl.Value
    }

    //operatorDeclaration : TypeMemberList = attributes? modifiers? operatorDeclarator "("s formalParameterList? ")"s methodBody;
    operatorDeclaration(attributes        : option[VToken[list[CustomAttribute]]],
                        modifiers         : option[VToken[list[Modifier]]],
                        decl              : VToken[Identifier * TypeRef],
                        _lparen           : NToken, _ : NToken,
                        formalParameters  : option[VToken[list[FormalParameter]]],
                        _rparen           : NToken, _ : NToken,
                        body              : VToken[Statement]) : list[TypeMember]
    {
      def (op, returnType) = decl.Value;
      [TypeMember.Operator(
        GetLocation(_),
        attributes.WithDefault([]),
        modifiers.WithDefault([]),
        op,
        returnType,
        formalParameters.WithDefault([]),
        body.Value)]
    }

    #endregion

    #region Constructor

    //constructorInitializer : ConstructorInitializer = ":"s ("this" / "base")S "("s argumentList? ")"s;
    constructorInitializer( _colon  : NToken, _ : NToken,
                            keyword : NToken, _ : NToken,
                            _lparen : NToken, _ : NToken,
                            args    : option[VToken[list[FunctionArgument]]],
                            _rparen : NToken, _ : NToken) : ConstructorInitializer
    {
      match(keyword.GetText()) {
        | "this" => ConstructorInitializer.This(GetLocation(_), args.WithDefault([]))
        | "base" => ConstructorInitializer.Base(GetLocation(_), args.WithDefault([]))
        | _ => throw ArgumentOutOfRangeException("keyword")
      }
    }

    //constructorDeclaration : TypeMemberList = attributes? modifiers? identifier "("s formalParameterList? ")"s constructorInitializer? methodBody;
    constructorDeclaration( attributes        : option[VToken[list[CustomAttribute]]],
                            modifiers         : option[VToken[list[Modifier]]],
                            name              : VToken[Identifier],
                            _lparen           : NToken, _ : NToken,
                            formalParameters  : option[VToken[list[FormalParameter]]],
                            _rparen           : NToken, _ : NToken,
                            init              : option[VToken[ConstructorInitializer]],
                            body              : VToken[Statement]) : list[TypeMember]
    {
      def ctor_name = name.Value;
      [TypeMember.Constructor(
        GetLocation(_),
        attributes.WithDefault([]),
        modifiers.WithDefault([]),
        Expr.Ref(ctor_name.Location, ctor_name),
        formalParameters.WithDefault([]),
        init.WithDefault(ConstructorInitializer.Default(ctor_name.Location)),
        body.Value)]
    }

    #endregion

    #region Destructor

    //destructorDeclaration  : TypeMemberList = attributes? modifiers? "~"s identifier "("s formalParameterList? ")"s methodBody;
    destructorDeclaration(attributes        : option[VToken[list[CustomAttribute]]],
                          modifiers         : option[VToken[list[Modifier]]],
                          _tild : NToken, _ : NToken,
                          name              : VToken[Identifier],
                          _lparen           : NToken, _ : NToken,
                          formalParameters  : option[VToken[list[FormalParameter]]],
                          _rparen           : NToken, _ : NToken,
                          body              : VToken[Statement]) : list[TypeMember]
    {
      def dtor_name = name.Value;
      [TypeMember.Destructor(
        GetLocation(_),
        attributes.WithDefault([]),
        modifiers.WithDefault([]),
        Expr.Ref(dtor_name.Location, dtor_name),
        formalParameters.WithDefault([]),
        body.Value)]
    }

    #endregion

    //nestedTypeDeclaration  : TypeMemberList = typeDeclaration;
    nestedTypeDeclaration(decl : VToken[TypeDeclaration]) : list[TypeMember]
    {
      def loc = GetLocation(_);
      def decl = decl.Value;
      [TypeMember.TypeDeclaration(loc, decl.Attributes, decl.Modifiers, decl)]
    }

    //typeMemberDeclaration  : TypeMemberList = constantDeclaration
    //                                        / fixedSizeBufferDeclaration
    //                                        / eventDeclaration
    //                                        / operatorDeclaration
    //                                        / indexerDeclaration 
    //                                        / fieldDeclaration
    //                                        / methodDeclaration
    //                                        / propertyDeclaration 
    //                                        / constructorDeclaration
    //                                        / destructorDeclaration
    //                                        / typeDeclaration;
    typeMemberDeclaration(opt : VToken[list[TypeMember]]) : list[TypeMember]
    {
      opt.Value
    }

    //typeMemberDeclarations : TypeMemberList = typeMemberDeclaration*;
    typeMemberDeclarations(members : SCG.List[VToken[list[TypeMember]]]) : list[TypeMember]
    {
      members.Values().Flatten()
    }
  }
}