/*
 * Copyright (c) 2003-2005 The University of Wroclaw.
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions
 * are met:
 *    1. Redistributions of source code must retain the above copyright
 *       notice, this list of conditions and the following disclaimer.
 *    2. Redistributions in binary form must reproduce the above copyright
 *       notice, this list of conditions and the following disclaimer in the
 *       documentation and/or other materials provided with the distribution.
 *    3. The name of the University may not be used to endorse or promote
 *       products derived from this software without specific prior
 *       written permission.
 * 
 * THIS SOFTWARE IS PROVIDED BY THE UNIVERSITY ``AS IS'' AND ANY EXPRESS OR
 * IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES
 * OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN
 * NO EVENT SHALL THE UNIVERSITY BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
 * SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED
 * TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR
 * PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF
 * LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING
 * NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS
 * SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 */

using System.Globalization;
using Nemerle.Collections;
using Nemerle.Utility;

namespace Nemerle.Compiler {

  public struct Location : Nemerle.IComparable [Location]
  {
    /** The format of token is fixed and holding following info:
        19 bits for end line number
        8 bits for end column number
        19 bits for (start) line number (0-524287 line numbers)
        8 bits for (start) column number (0-255 column numbers)
        10 bits for filename index  (1024 filenames)
        = 64 bits of data
     */       
    token : ulong;

    this (t : ulong) { token = t; }

    public this (file_idx : int, line : int, col : int) {
      token = ComputeToken (file_idx, line, col, line, col);
    }    

    public this (file_idx : int, line : int, col : int, end_line : int, end_col : int) {
      token = ComputeToken (file_idx, line, col, end_line, end_col);
    }    
    
    public static Default : Location = Location (0UL);

    public static @== (x : Location, y : Location) : bool { x.token == y.token }
    public static @!= (x : Location, y : Location) : bool { x.token != y.token }
    
    public static @+ (x : Location, y : Location) : Location {
      Location ((x.token %& (~(end_line_mask %| end_col_mask))) %|
                (y.token %& (end_line_mask %| end_col_mask)))
    }    
    
    /** the bit number (from least important as 0) where line info begins */
    static line_bits : int = 18;
    static col_bits : int = 10;
    static end_line_bits : int = 45;
    static end_col_bits : int = 37;
    static end_line_mask : ulong = ((1UL << 19) - 1UL) << 45;
    static end_col_mask : ulong = ((1UL << 8) - 1UL) << 37;

    /** bits reserved for index of file */
    static file_mask : ulong = 0b1111111111UL;

    /** mapping from index to file name */
    static file_names : array [string] = array (1024);
    static mutable file_names_amount : int;
    
    /** encountered file names with mapping to their indices */
    static file_indices : Hashtable [string, int] = Hashtable ();

    public static Init () : void
    {
      file_indices.Clear ();
      file_indices.Add ("", 0);
      file_names [0] = "";
      file_names_amount = 1;
    }
    
    public static ComputeToken (file_idx : int, line : int, col : int, 
                                end_line : int, end_col : int) : ulong
    {
      (file_idx :> ulong) %|
      ((end_line :> ulong) << end_line_bits) %| 
      ((if (end_col > 255) 255UL else (end_col :> ulong)) << end_col_bits) %|
      ((line :> ulong) << line_bits) %| 
      ((if (col > 255) 255UL else (col :> ulong)) << col_bits)
    }

    /** Adds new filename to locations index. If filename in already in
        store, an error message is outputted.
     */
    public static AddFile (name : string) : int
    {
      when (file_indices.Contains (name))
        Message.error ($"file $name occured twice on the list to compile");
      GetFileIndex (name)
    }

    /** Gets index of given filename in locations store. If one doesn't exist
        it is being added and new index is returned.
     */
    static public GetFileIndex (name : string) : int
    {
      match (file_indices.Get (name)) {
        | Some (idx) => idx
        | None =>
          if (file_names_amount == file_names.Length) {
            Message.warning ("too many filenames... Location cache overflow");
            0
          }
          else {
            assert (file_names_amount > 0);
            file_indices.Add (name, file_names_amount);
            file_names [file_names_amount] = name;
            ++file_names_amount;
            file_names_amount - 1
          }
      }
    }
    
    public File : string
    {
      [Nemerle.Assertions.Ensures (value != null)]
      get {
        file_names [(token %& file_mask) :> int];
      }
    }

    public Line : int
    {
      get { ((token >> line_bits) %& ((1UL << 19) - 1UL)) :> int }
    }

    /** we allow only columns in range 0-255 */
    public Column : int
    {
      get { ((token >> col_bits) %& 255UL) :> int }
    }

    public EndLine : int
    {
      get { ((token %& end_line_mask) >> end_line_bits) :> int }
    }

    /** we allow only columns in range 0-255 */
    public EndColumn : int
    {
      get { ((token %& end_col_mask) >> end_col_bits) :> int }
    }

    public CompareTo (x : Location) : int
    {
      if (token %& file_mask == x.token %& file_mask)
        if (Line == x.Line)
          Column.CompareTo (x.Column)
        else
          Line.CompareTo (x.Line)
      else {
        File.CompareTo (x.File)
      }
    }

    public override ToString () : string {
      $"($Line,$Column-$EndLine,$EndColumn)"
    }

    [Nemerle.OverrideObjectEquals]
    public Equals (other : Location) : bool
    {
      token == other.token
    }
  }
  
  public class Located
  {
    public mutable loc : Location;
    public this () { loc = Location_stack.top (); }
    public this (loc : Location) { this.loc = loc }

    public Location : Location { get { loc } }
  }

  [System.Flags]
  public enum NemerleAttributes
  {
    | None        = 0x00000
    | Public      = 0x00001
    | Private     = 0x00002
    | New         = 0x00004 
    | Protected   = 0x00008
    | Abstract    = 0x00010
    | Virtual     = 0x00020
    | Sealed      = 0x00040
    | Static      = 0x00080
    | Mutable     = 0x00100      
    | Internal    = 0x00200
    | Override    = 0x00400
    | Struct      = 0x01000
    | Macro       = 0x02000
    | Volatile    = 0x04000
    | SpecialName = 0x08000
    | Partial     = 0x10000      

    | AccessModifiers = Public %| Private %| Protected %| Internal
  }                 

  
  public class Modifiers 
  {
    public mutable mods : NemerleAttributes;
    public mutable custom_attrs : list [Parsetree.PExpr];
    public mutable macro_attrs : list [string * Parsetree.PExpr];

    public static Empty : Modifiers;

    public IsEmpty : bool
    {
      get {
        custom_attrs.IsEmpty && macro_attrs.IsEmpty
      }
    }

    public this (mods : NemerleAttributes, custom_attrs : list [Parsetree.PExpr])
    {
      this.mods = mods;
      this.custom_attrs = custom_attrs;
      this.macro_attrs = [];
    }

    public static this ()
    {
      Empty = Modifiers (NemerleAttributes.None, []);
    }

    public AddCustomAttribute (target : System.AttributeTargets, 
                               expr : Parsetree.PExpr) : void
    {
      ignore (target);
      custom_attrs = expr :: custom_attrs;
    }

    internal SaveCustomAttributes (ti : TypeBuilder,
                                   adder : System.AttributeTargets * 
                                           System.Reflection.Emit.CustomAttributeBuilder -> void) 
                                  : void
    {
      def target = System.AttributeTargets.All;
      foreach (expr in custom_attrs) {
        try {
          adder (target, AttributeCompiler.CompileAttribute (ti.env, ti, expr))
        } catch {
          | _ : Recovery => ()
        }
      };
      foreach ((suff, expr) in macro_attrs) {
        try {
          def (m, parms) = 
            match (MacroRegistry.lookup_macro (ti.env, expr, suff)) {
              | Some ((_, m, parms)) => (m, parms)
              | _ => Util.ice ("macro is not a macro?" +
                               PrettyPrint.SprintExpr (None (), expr))
            };
          def sparms = List.Map (parms, fun (x) { PrettyPrint.SprintExpr (None (), x) });
          def concatenated = NString.Concat ("@", sparms);
//          def _x = ti.env.GetMacroContext ();
          def serialized = <[ 
            Nemerle.Internal.MacroAttribute ($(m.GetNamespace () + "." + m.GetName () : string),
          //                                   $(ti.env.GetMacroContext () : int),
                                             0,
                                             $(concatenated : string)) 
          ]>;
          adder (target, AttributeCompiler.CompileAttribute (ti.env, ti, serialized))
        } catch {
          | _ : Recovery => ()
        }
      };
    }
  }

  public variant Literal
  {
    | Void
    | Null
    | String { val : string; }
    | Float { val : float; }
    | Double { val : double; }
    | Decimal { val : decimal; }
    | Char { val : char; }
    | Bool { val : bool; }
    | SByte { val : sbyte; }
    | Byte { val : byte; }
    | Short { val : short; }
    | UShort { val : ushort; }
    | Int { val : int; }
    | UInt { val : uint; }
    | Long { val : long; }
    | ULong { val : ulong; }

    | Enum { val : Literal; ty : TypeInfo; }

      
    /**
     * Converts 'this' literal to an equivalent (lexable) string
     */
    public override ToString () : string
    {
      match (this) {
        | Literal.Void           => "()"
        | Literal.Null           => "null"

        | Literal.String   (val) => "\"" + val.Replace ("\n", "\\n") + "\""
        | Literal.Float    (val) => val.ToString (NumberFormatInfo.InvariantInfo) + "f"
        | Literal.Double   (val) => val.ToString (NumberFormatInfo.InvariantInfo) + "d"
        | Literal.Decimal  (val) => val.ToString (NumberFormatInfo.InvariantInfo) + "m"
        | Literal.Char     (val) => "'" + val.ToString () + "'"
        | Literal.Bool     (val) => if (val) "true" else "false"
        | Literal.SByte    (val) => val.ToString () + "sb"
        | Literal.Byte     (val) => val.ToString () + "b"
        | Literal.Short    (val) => val.ToString () + "s"
        | Literal.UShort   (val) => val.ToString () + "us"
        | Literal.Int      (val) => val.ToString ()
        | Literal.UInt     (val) => val.ToString () + "u"
        | Literal.Long     (val) => val.ToString () + "l"
        | Literal.ULong    (val) => val.ToString () + "ul"
          
        | Literal.Enum (val, ty) => "(" + val.ToString () + " :> " + ty.FullName + ")"
      }
    }
    
    public GetSystemType () : System.Type {
      match (this) {
        | Literal.Void => SystemType.Void
        | Literal.Null => SystemType.Object
        | Literal.Byte => SystemType.Byte
        | Literal.SByte => SystemType.SByte
        | Literal.Short => SystemType.Int16
        | Literal.UShort => SystemType.UInt16                
        | Literal.Int => SystemType.Int32
        | Literal.UInt => SystemType.UInt32                
        | Literal.Long => SystemType.Int64
        | Literal.ULong => SystemType.UInt64                
        | Literal.Char => SystemType.Char
        | Literal.String => SystemType.String
        | Literal.Float => SystemType.Single
        | Literal.Double => SystemType.Double
        | Literal.Decimal => SystemType.Decimal                
        | Literal.Bool => SystemType.Boolean
        // for enums we want to stay with original type
        // because e.g. ToString is called on enum not int
        | Literal.Enum (_, ty) => ty.GetSystemType ()
      }
    }
  }

  public variant FunBody
  {
    | Parsed { expr : Parsetree.PExpr; }
    | Typed { expr : Typedtree.TExpr; }
    | Compiled
    | Extern { name : string; }
    | Abstract  // for interface method
  }

  public variant FunKind
  {
    | Method { f_implements : list [Parsetree.PExpr]; }
    | BoundMethod { f_implements : list [IMethod]; }
    | Constructor
    | StaticConstructor
    | Function
  }
} // Nemerle.Compiler
