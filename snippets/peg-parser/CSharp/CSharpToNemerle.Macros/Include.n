using System;
using System.IO;
using SCG = System.Collections.Generic;

using Nemerle;
using Nemerle.Assertions;
using Nemerle.Compiler;
using Nemerle.Utility;

namespace CSharpToNemerle
{
  [MacroUsage(MacroPhase.BeforeInheritance, MacroTargets.Assembly)]
  macro Include(files : string)
  {
    //assert2(false);
    def manager = Macros.Manager();
    def converter = CSharpToNemerle.AstConverter(manager);
    def parser = CSharpParser.Parser();
    def projectDir = Path.GetDirectoryName(manager.Options.ProjectPath);
    foreach(fileName in Directory.GetFiles(projectDir, files)) {
      def fileIndex = Location.GetFileIndex(fileName);
      def source = Nemerle.Peg.SourceSnapshot(File.ReadAllText(fileName), fileIndex, fileName);
      match(parser.Parse(source)) {
        | Some(compilationUnit) =>
          def decls = converter.Convert(compilationUnit);
          decls.Iter(manager.ScanningPipeline)
        | _ =>
          def loc = Nemerle.Peg.Location(source, parser.MaxRollbackPos, parser.MaxRollbackPos);
          Message.Error(loc.NLocation(), $"failed to parse file `$fileName'.")
      }
    }
  }
}