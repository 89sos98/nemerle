using System;
using SCG = System.Collections.Generic;

using ICSharpCode.SharpDevelop.Gui;
using ICSharpCode.TextEditor;
using ICSharpCode.TextEditor.Document;

using Nemerle.Assertions;
using Nemerle.Completion2;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Utility;

namespace Nemerle.SharpDevelop.CodeCompletion {

    public sealed class DocumentNemerleSource : ISource, IDisposable {

        public this([NotNull] source : Source, [NotNull] document : IDocument) {
            this.source = source;
            this.document = document;
            this.document.DocumentAboutToBeChanged += OnDocumentAboutToBeChanged;
            this.document.DocumentChanged += OnDocumentChanged;
        }

        source : Source;

        [Accessor] document : IDocument;

        [Accessor] relocation_requests_queue : SCG.List[RelocationRequest] = SCG.List();

        [Accessor] mutable current_version : int = 0;

        public CompileUnit : CompileUnit {
            get;
            set;
        }

        public FileIndex : int {
            get { source.FileIndex }
        }

        public LineCount : int {
            get { Invoke(fun() { document.TotalNumberOfLines }) }
        }

        public LockWrite() : void { }

        public UnlockWrite() : void { }

        public LockReadWrite() : void { }

        public UnlocReadkWrite() : void { }

        public GetTextCurrentVersionAndFileIndex() : string * int * int {
            LockWrite();
            try {
                (GetText(), CurrentVersion, FileIndex)
            } finally {
                UnlockWrite();
            }
        }

        public SetRegions(_regions : SCG.IList[RegionInfo], _sourceVersion : int) : void { }

        public SetTopDeclarations(_topDeclarations : array[TopDeclaration]) : void { }

        public GetText() : string {
            Invoke(fun() { document.TextContent })
        }

        public GetRegion(lineStart : int, colStart : int, lineEnd : int, colEnd : int) : string {
            Invoke(fun() {
                def start = document.PositionToOffset(TextLocation(colStart - 1, lineStart - 1));
                def end   = document.PositionToOffset(TextLocation(colEnd - 1,   lineEnd - 1));
                document.GetText(start, end - start)
            })
        }

        public GetRegion(location : Location) : string {
            GetRegion(location.Line, location.Column, location.EndLine, location.EndColumn)
        }

        public GetLine(line : int) : string {
            Invoke(fun() {
                def segment = document.GetLineSegment(line);
                document.GetText(segment)
            })
        }

        public GetPositionOfLineIndex(line : int, col : int) : int {
            Invoke(fun() {
                document.PositionToOffset(TextLocation(col - 1, line - 1))
            })
        }

        public GetLineIndexOfPosition(pos : int) : int * int {
            Invoke(fun() {
                def loc = document.OffsetToPosition(pos);
                (loc.Line + 1, loc.Column + 1)
            })
        }

        mutable start_loc : TextLocation;
        mutable old_end_loc : TextLocation;

        private OnDocumentAboutToBeChanged(_ : object, e : DocumentEventArgs) : void {
            start_loc = document.OffsetToPosition(e.Offset);
            old_end_loc = document.OffsetToPosition(e.Offset + if(e.Length > 0) e.Length else 0);
        }

        private OnDocumentChanged(_ : object, e : DocumentEventArgs) : void {
            current_version += 1;

            def new_end_loc = document.OffsetToPosition(e.Offset + if(string.IsNullOrEmpty(e.Text)) 0 else e.Text.Length);

            source.UpdateCompileUnit(
                new_end_loc.Line + 1, new_end_loc.Column + 1,
                old_end_loc.Line + 1, old_end_loc.Column + 1,
                start_loc.Line + 1, start_loc.Column + 1);
        }

        #region Disposable

        [Accessor] mutable disposed : bool = false;

        public Dispose() : void {
            unless(disposed) {
                document.DocumentAboutToBeChanged -= OnDocumentAboutToBeChanged;
                document.DocumentChanged -= OnDocumentChanged;
                disposed = true;
            }
        }

        protected override Finalize() : void {
            Dispose();
        }

        #endregion

        #region Utils

        private static Invoke[T](f : void -> T) : T {
            if(WorkbenchSingleton.InvokeRequired)
                WorkbenchSingleton.SafeThreadFunction(f)
            else
                f()
        }

        #endregion

    }

}