using Nemerle.Collections;
using Nemerle.Peg;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle;

using System;
using System.Collections.Generic;
using LRPEGCC;

namespace Calculator
{
  //type LoopTokens = NToken[int] * NToken[int] * VToken[int];
  
  /// <summary>
  /// Description of CalcParser.
  /// </summary>  
  [PegGrammar(start,
  grammar
  {  
    any                   = ['\u0000'..'\uFFFF'];
    digit                 = ['0'..'9']+;
    spaces                = ' '*;
    
    num             : int = digit spaces;
    unaryMinus      : int = '-' spaces simplExpr;
    parenthesesExpr : int = '(' spaces sumOrSub ')' spaces;
    simplExpr       : int = num / parenthesesExpr / unaryMinus;
    mulOrDiv        : int = simplExpr (('*' / '/') spaces simplExpr)*;
    sumOrSub        : int = mulOrDiv  (('+' / '-') spaces mulOrDiv )*;
    start           : int = spaces sumOrSub !any;
  })]
  public class CalcParser : ParserBase[int]
  {    
//Requred for parser --------------------------------------------------------------------------------------------    
    
    //TODO: beter readability of tokens
    private num(digit : NToken, _ : NToken) : int
    {
      int.Parse(digit.GetText())
    }
    
    private unaryMinus(_ : NToken, _ : NToken, se : VToken[int]) : int
    {
      -se.Value
    }
    
    private parenthesesExpr(_ : NToken, _ : NToken, se : VToken[int], _ : NToken, _ : NToken) : int
    {
      se.Value
    }
    
    // TODO: autogenerate this block
    private simplExpr(se : VToken[int]) : int
    {
      se.Value
    }
    // TODO: end todo
    
    private start(_ : NToken, se : VToken[int], _ : NToken) : int
    {
      se.Value
    }
    
    private mulOrDiv(se : VToken[int], lst : List[NToken * NToken * VToken[int]]) : int
    {
      DoOpHelper(se, lst)
    }
    
    private sumOrSub(se : VToken[int], lst : List[NToken * NToken * VToken[int]]) : int
    { 
      DoOpHelper(se, lst)
    }
     
//End requred for parser --------------------------------------------------------------------------------------------    

    private DoOpHelper(se : VToken[int], lst : List[NToken * NToken * VToken[int]]) : int
    {
      def doOp(x : int, y : int, op : string) : int
      {
        match (op)
        {
          | ("*") => x * y
          | ("/") => x / y
          | ("+") => x + y
          | ("-") => x - y
          | _     => assert(false);
        }
      }
           
      mutable r = se.Value;
      
      foreach ((opTok, _, secondTok) in lst)
        r = doOp(r, secondTok.Value, opTok.GetText());
    
      r  
    }
  }
}
