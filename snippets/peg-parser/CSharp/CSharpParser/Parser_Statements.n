using System;
using SCG = System.Collections.Generic;

using Nemerle.Assertions;
using Nemerle.Collections;
using Nemerle.Utility;
using Nemerle.Peg;

namespace CSharpParser
{
  public partial class Parser
  {
    #region Empty statement

    //emptyStatement : Statement = ";"s;
    emptyStatement(_semicolon : NToken, _ : NToken) : Statement
    {
      Statement.Empty()
    }

    #endregion

    #region Statement with label

    //labeledStatement : Statement = identifier ":"s statement;
    labeledStatement(label : VToken[Identifier], _colon : NToken, _ : NToken, stmt : VToken[Statement]) : Statement
    {
      Statement.Label(label.Value, stmt.Value)
    }

    #endregion

    #region Declaration statements

    //localConstantDeclaration : Statement = "const"S anyType constantDeclarators;
    localConstantDeclaration( _const        : NToken, _ : NToken,
                              constantType  : VToken[TypeRef],
                              decls         : VToken[list[Identifier * Expr]]) : Statement
    {
      Statement.ConstantDecl(constantType.Value, decls.Value)
    }

    //localVariableType_var : TypeRef = "var"S;
    localVariableType_var(_var : NToken, _ : NToken) : TypeRef 
    {
      null
    }

    //localVariableType_any : TypeRef = anyType;
    localVariableType_any(t : VToken[TypeRef]) : TypeRef 
    {
      t.Value
    }

    //localVariableType     : TypeRef = localVariableType_var / localVariableType_any;
    localVariableType(opt : VToken[TypeRef]): TypeRef
    {
      opt.Value
    }

    //localVariableDeclaration : Statement = localVariableType variableDeclarators;
    localVariableDeclaration( variableType  : VToken[TypeRef],
                              decls         : VToken[list[Identifier * VariableInitializer]]) : Statement
    {
      Statement.VariableDecl(variableType.Value, decls.Value)
    }

    //declarationStatement : Statement = localConstantDeclaration / localVariableDeclaration;
    declarationStatement(opt : VToken[Statement]) : Statement
    {
      opt.Value
    }

    #endregion

    #region Expression statements

    //statementExpression     : Expr          = expression;
    statementExpression(opt : VToken[Expr]) : Expr
    {
      opt.Value
    }

    //statementExpressionList : ExprList  = statementExpression (","s statementExpression)*;
    statementExpressionList(head : VToken[Expr],
                            tail : SCG.List[NToken * NToken * VToken[Expr]]) : list[Expr]
    {
      head.Cons(tail)
    }

    //expressionStatement     : Statement = statementExpression ";"s;
    expressionStatement(expr : VToken[Expr], _semicolon : NToken, _ : NToken) : Statement
    {
      Statement.Expression(expr.Value)
    }

    #endregion

    #region Selection statements

    //ifStatement : Statement = "if"S "("s booleanExpression ")"s embeddedStatement ("else"S embeddedStatement)?;
    ifStatement(_if       : NToken, _ : NToken,
                _lparen   : NToken, _ : NToken,
                expr      : VToken[Expr],
                _rparen   : NToken, _ : NToken,
                trueStmt  : VToken[Statement],
                falseStmt : option[NToken * NToken * VToken[Statement]]) : Statement
    {
      def falseStmt = match(falseStmt) {
        | Some( (_, _, stmt) ) => stmt.Value
        | _ => null
      }
      Statement.If(expr.Value, trueStmt.Value, falseStmt)
    }

    //switchLabel_case    : Expr = "case"S constantExpression ":"s;
    switchLabel_case( _case   : NToken, _ : NToken,
                      id      : VToken[Expr],
                      _colon  : NToken, _ : NToken) : Expr
    {
      id.Value
    }

    //switchLabel_default : Expr = "default"S ":"s;
    switchLabel_default(_default : NToken, _ : NToken, _colon : NToken, _ : NToken) : Expr
    {
      null
    }

    //switchSection       : SwitchCaseEntry     = (switchLabel_case / switchLabel_default) statementList?;
    switchSection(lbl : VToken[Expr], stmts : option[VToken[list[Statement]]]) : SwitchCaseEntry
    {
      match(lbl.Value) {
        | null => SwitchCaseEntry.Default(stmts.WithDefault([]))
        | id =>   SwitchCaseEntry.Case(id, stmts.WithDefault([]))
      }
    }

    //switchSections      : SwitchCaseEntryList = switchSection+;
    switchSections(sections : SCG.List[VToken[SwitchCaseEntry]]) : list[SwitchCaseEntry]
    {
      NList.ToList(sections).Map(s => s.Value)
    }

    //switchStatement     : Statement           = "switch"S "("s expression ")"s "{"s switchSections? "}"s;
    switchStatement(_switch   : NToken, _ : NToken,
                    _lparen   : NToken, _ : NToken,
                    expr      : VToken[Expr],
                    _rparen   : NToken, _ : NToken,
                    _lparen2  : NToken, _ : NToken,
                    sections  : option[VToken[list[SwitchCaseEntry]]],
                    _rparen2  : NToken, _ : NToken) : Statement
    {
      Statement.Switch(expr.Value, sections.WithDefault([]))
    }

    //selectionStatement  : Statement = ifStatement
    //                                / switchStatement;
    selectionStatement(opt : VToken[Statement]) : Statement
    {
      opt.Value
    }

    #endregion

    #region Iteration statements

    //whileStatement  : Statement = "while"S "("s booleanExpression ")"s embeddedStatement;
    whileStatement( _while  : NToken, _ : NToken,
                    _lparen : NToken, _ : NToken,
                    expr    : VToken[Expr],
                    _rparen : NToken, _ : NToken,
                    stmt    : VToken[Statement]) : Statement
    {
      Statement.While(expr.Value, stmt.Value)
    }

    //doStatement     : Statement = "do"S embeddedStatement "while"S "("s booleanExpression ")"s ";"s;
    doStatement(_do         : NToken, _ : NToken,
                stmt        : VToken[Statement],
                _while      : NToken, _ : NToken,
                _lparen     : NToken, _ : NToken,
                cond        : VToken[Expr],
                _rparen     : NToken, _ : NToken,
                _semicolon  : NToken, _ : NToken) : Statement
    {
      Statement.DoWhile(cond.Value, stmt.Value)
    }

    //forInitializer_var  : object = localVariableDeclaration;
    forInitializer_var(decl : VToken[Statement]) : object
    {
      decl.Value
    }

    //forInitializer_expr : object = statementExpressionList;
    forInitializer_expr(init : VToken[list[Expr]]) : object
    {
      init.Value
    }

    //forInitializer      : object = forInitializer_var / forInitializer_expr;
    forInitializer(opt : VToken[object] ): object
    {
      opt.Value
    }

    //forCondition : Expr = booleanExpression;
    forCondition(expr : VToken[Expr]) : Expr
    {
      expr.Value
    }

    //forIterator  : ExprList = statementExpressionList;
    forIterator(iter : VToken[list[Expr]]) : list[Expr]
    {
      iter.Value
    }

    //forStatement : Statement = "for"S "("s forInitializer? ";"s forCondition? ";"s forIterator? ")"s embeddedStatement;
    forStatement( _for : NToken, _ : NToken,
                  _lparen : NToken, _ : NToken,
                  init : option[VToken[object]],
                  _semicolon1 : NToken, _ : NToken,
                  cond : option[VToken[Expr]],
                  _semicolon2 : NToken, _ : NToken,
                  iter : option[VToken[list[Expr]]],
                  _rparen : NToken, _ : NToken,
                  stmt : VToken[Statement]) : Statement
    {
      def make_ForVar(decl) {
        Statement.ForVar(decl, cond.WithDefault(null), iter.WithDefault([]), stmt.Value)
      }

      def make_ForExpr(init) {
        Statement.ForExpr(init, cond.WithDefault(null), iter.WithDefault([]), stmt.Value)
      }

      match(init) {
        | Some(init) =>
          match(init.Value) {
            | decl is Statement.VariableDecl => make_ForVar(decl)
            | init is list[Expr]             => make_ForExpr(init)
            | _ => throw ArgumentOutOfRangeException("init")
          }
        | _ => make_ForExpr([])
      }
    }

    //foreachStatement : Statement = "foreach"S "("s localVariableType identifier "in"s expression ")"s embeddedStatement;
    foreachStatement( _foreach      : NToken, _ : NToken,
                      _lparen       : NToken, _ : NToken,
                      variableType  : VToken[TypeRef],
                      variable      : VToken[Identifier],
                      _in           : NToken, _ : NToken,
                      expr          : VToken[Expr],
                      _rparen       : NToken, _ : NToken,
                      body          : VToken[Statement]) : Statement
    {
      Statement.Foreach(
        variable.Value,
        variableType.Value,
        expr.Value,
        body.Value)
    }

    //iterationStatement : Statement = whileStatement
    //                               / doStatement
    //                               / forStatement
    //                               / foreachStatement;
    iterationStatement(opt : VToken[Statement]) : Statement
    {
      opt.Value
    }

    #endregion

    #region Jump statements

    //breakStatement        : Statement = "break"S ";"s;
    breakStatement(_break : NToken, _ : NToken, _semicolon : NToken, _ : NToken) : Statement
    {
      Statement.Break()
    }

    //continueStatement     : Statement = "continue"S ";"s;
    continueStatement(_continue : NToken, _ : NToken, _semicolon : NToken, _ : NToken) : Statement
    {
      Statement.Continue()
    }

    //gotoStatement_case    : Statement = "goto"S "case"S constantExpression ";"s;
    gotoStatement_case( _goto       : NToken, _ : NToken,
                        _case       : NToken, _ : NToken,
                        expr        : VToken[Expr],
                        _semicolon  : NToken, _ : NToken): Statement
    {
      Statement.GotoCase(expr.Value)
    }

    //gotoStatement_default : Statement = "goto"S "default"S ";"s;
    gotoStatement_default(_goto       : NToken, _ : NToken,
                          _default    : NToken, _ : NToken,
                          _semicolon  : NToken, _ : NToken) : Statement
    {
      Statement.GotoDefault()
    }

    //gotoStatement_id      : Statement = "goto"S identifier ";"s;
    gotoStatement_id( _goto       : NToken, _ : NToken,
                      label       : VToken[Identifier],
                      _semicolon  : NToken, _ : NToken) : Statement
    {
      Statement.Goto(label.Value)
    }

    //gotoStatement         : Statement = gotoStatement_case / gotoStatement_default / gotoStatement_id;
    gotoStatement(opt : VToken[Statement]) : Statement
    {
      opt.Value
    }

    //returnStatement       : Statement = "return"S expression? ";"s;
    returnStatement(_return     : NToken, _ : NToken,
                    expr        : option[VToken[Expr]],
                    _semicolon  : NToken, _ : NToken) : Statement
    {
      Statement.Return(expr.WithDefault(null))
    }

    //throwStatement        : Statement = "throw"S expression? ";"s;
    throwStatement( _throw      : NToken, _ : NToken,
                    expr        : option[VToken[Expr]],
                    _semicolon  : NToken, _ : NToken) : Statement
    {
      Statement.Throw(expr.WithDefault(null))
    }

    //jumpStatement : Statement = breakStatement
    //                          / continueStatement
    //                          / gotoStatement
    //                          / returnStatement
    //                          / throwStatement;
    jumpStatement(opt : VToken[Statement]) : Statement
    {
      opt.Value
    }

    #endregion


    #region Try-catch-finally

    //catchClause   : TryCatchEntry     = "catch"S ("("s anyType identifier? ")"s)? block;
    catchClause(_catch    : NToken, _ : NToken,
                exception : option[NToken * NToken * VToken[TypeRef] * option[VToken[Identifier]] * NToken * NToken],
                body      : VToken[Statement]) : TryCatchEntry
    {
      def (exceptionType, variable) = match(exception) {
        | Some( (_, _, exceptionType, variable, _, _) ) =>
          match(variable) {
            | Some(variable) => (exceptionType.Value, variable.Value)
            | _              => (exceptionType.Value, null)
          }
        | _ => (null, null)
      }
      TryCatchEntry(exceptionType, variable, body.Value)
    }

    //catchClauses  : TryCatchEntryList = catchClause+;
    catchClauses(clauses : SCG.List[VToken[TryCatchEntry]]) : list[TryCatchEntry]
    {
      NList.ToList(clauses).Map(c => c.Value)
    }

    //finallyClause : Statement         = "finally"S block;
    finallyClause(_finally : NToken, _ : NToken, body : VToken[Statement]) : Statement
    {
      body.Value
    }

    //tryStatement_catch    : TryCatchFinallyTail = catchClauses finallyClause?;
    tryStatement_catch( catchList     : VToken[list[TryCatchEntry]],
                        finallyBlock  : option[VToken[Statement]]) : list[TryCatchEntry] * Statement
    {
      (catchList.Value, finallyBlock.WithDefault(null))
    }

    //tryStatement_finally  : TryCatchFinallyTail = finallyClause;
    tryStatement_finally(finallyBlock  : VToken[Statement]) : list[TryCatchEntry] * Statement
    {
      ([], finallyBlock.Value)
    }

    //tryStatement          : Statement           = "try"S block (tryStatement_catch / tryStatement_finally);
    tryStatement( _try          : NToken, _ : NToken,
                  body          : VToken[Statement],
                  tail : VToken[list[TryCatchEntry] * Statement]) : Statement
    {
      def (catchList, finallyBlock) = tail.Value;
      Statement.Try(body.Value, catchList, finallyBlock)
    }

    #endregion

    #region Checked-unchecked

    //checkedStatement    : Statement = "checked"S block;
    checkedStatement(_checked : NToken, _ : NToken, body : VToken[Statement]) : Statement
    {
      Statement.Checked(body.Value, true)
    }

    //uncheckedStatement  : Statement = "unchecked"S block;
    uncheckedStatement(_unchecked : NToken, _ : NToken, body : VToken[Statement]) : Statement
    {
      Statement.Checked(body.Value, false)
    }

    #endregion

    #region Lock

    //lockStatement : Statement = "lock"S "("s expression ")"s embeddedStatement;
    lockStatement(_lock   : NToken, _ : NToken,
                  _lparen : NToken, _ : NToken,
                  expr    : VToken[Expr],
                  _rparen : NToken, _ : NToken,
                  body    : VToken[Statement]) : Statement
    {
      Statement.Lock(expr.Value, body.Value)
    }

    #endregion

    #region Using

    //resourceAcquisition_var   : object = localVariableDeclaration;
    resourceAcquisition_var(decl : VToken[Statement]) : object
    {
      decl.Value
    }

    //resourceAcquisition_expr  : object = expression;
    resourceAcquisition_expr(expr : VToken[Expr]) : object
    {
      expr.Value
    }

    //resourceAcquisition       : object = resourceAcquisition_var / resourceAcquisition_expr;
    resourceAcquisition(opt : VToken[object]) : object
    {
      opt.Value
    }

    //usingStatement  : Statement = "using"S "("s resourceAcquisition ")"s embeddedStatement;
    usingStatement( _using    : NToken, _ : NToken,
                    _lparen   : NToken, _ : NToken,
                    resource  : VToken[object],
                    _rparen   : NToken, _ : NToken,
                    body      : VToken[Statement]) : Statement
    {
      match(resource.Value) {
        | decl is Statement.VariableDecl  => Statement.UsingVar(decl, body.Value)
        | expr is Expr                    => Statement.UsingExpr(expr, body.Value)
        | _ => throw ArgumentOutOfRangeException("resource")
      }
    }

    #endregion

    #region Yied statements

    //yieldStatement_return : Statement = "yield"S "return"S expression ";"s;
    yieldStatement_return(_yield      : NToken, _ : NToken,
                          _return     : NToken, _ : NToken,
                          expr        : VToken[Expr],
                          _semicolon  : NToken, _ : NToken) : Statement
    {
      Statement.YieldReturn(expr.Value)
    }

    //yieldStatement_break  : Statement = "yield"S "break"S ";"s;
    yieldStatement_break( _yield      : NToken, _ : NToken,
                          _break      : NToken, _ : NToken,
                          _semicolon  : NToken, _ : NToken) : Statement
    {
      Statement.YieldBreak()
    }

    //yieldStatement        : Statement = yieldStatement_return / yieldStatement_break;
    yieldStatement(opt : VToken[Statement]) : Statement
    {
      opt.Value
    }

    #endregion

    #region Unsafe and Fixed

    //unsafeStatement : Statement = "unsafe"S block;
    unsafeStatement(_unsafe : NToken, _ : NToken, body : VToken[Statement]) : Statement
    {
      Statement.Unsafe(body.Value)
    }

    //fixedStatement  : Statement = "fixed"S "("s localVariableDeclaration ")"s embeddedStatement;
    fixedStatement( _fixed  : NToken, _ : NToken,
                    _lparen : NToken, _ : NToken,
                    decl    : VToken[Statement],
                    _rparen : NToken, _ : NToken,
                    body    : VToken[Statement]) : Statement
    {
      Statement.Fixed(decl.Value :> Statement.VariableDecl, body.Value)
    }

    #endregion

    //embeddedStatement : Statement = block
    //                              / emptyStatement
    //                              / expressionStatement
    //                              / selectionStatement
    //                              / iterationStatement
    //                              / jumpStatement
    //                              / tryStatement
    //                              / checkedStatement
    //                              / uncheckedStatement
    //                              / lockStatement
    //                              / usingStatement
    //                              / yieldStatement
    //                              / unsafeStatement
    //                              / fixedStatement;
    embeddedStatement(opt : VToken[Statement]) : Statement
    {
      opt.Value
    }

    //statement     : Statement     = labeledStatement
    //                              / declarationStatement
    //                              / embeddedStatement;
    statement(opt : VToken[Statement]) : Statement
    {
      opt.Value
    }

    //statementList : StatementList = statement+;
    statementList(stmts : SCG.List[VToken[Statement]]) : list[Statement]
    {
      NList.ToList(stmts).Map(s => s.Value)
    }

    //block         : Statement     = "{"s statementList? "}"s;
    block(_lparen : NToken, _ : NToken,
          stmts   : option[VToken[list[Statement]]],
          _rparen : NToken, _ : NToken) : Statement
    {
      Statement.Block(stmts.WithDefault([]))
    }
  }
}