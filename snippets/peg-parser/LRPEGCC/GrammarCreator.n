using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Compiler.Parsetree.PExpr;
using Nemerle.Compiler.Token;

using System;
using System.Collections.Generic;
using System.Linq;

using Nemerle.Compiler.Util;

namespace LRPEGCC
{
  /// <summary>
  /// Description of UnnamedTerminalSymbolsSplitter.
  /// </summary>
  module GrammarCreator
  {  
    private static UnnamedRuleString = "unnamed_rule"; 
  
    public CreateGrammar(typer : Typer, _ : TypeBuilder, startRule : PExpr, rules : PExpr) : Grammar
    {

      def grammar = Parsing.ParseRules(typer, RuleRef(startRule.Location, startRule.ToString()), rules);
      
      def grammar = SplitUnnamedTerminalSymbols(grammar);      
      
      def grammar = Optimizer.OptimizeGrammar(grammar);              
              
      Print(grammar);
      
      grammar
    }    
    
    private Print(grammar : Grammar) : void
    {
      using(def f = System.IO.StreamWriter("grammar.txt"))
        f.WriteLine(grammar.ToString());
    }
    
    // this function replaces usages of terminal symbols in the right part of nonterminal rules by the call and created the terminal rule for this call
    private SplitUnnamedTerminalSymbols(grammar : Grammar) : Grammar
    {
      mutable newGrammar = Grammar(grammar.StartRuleName);
      
      foreach(name in grammar.Names)
      {
        def rule = grammar.GetRule(name);

        def (newRule, newRules) = SplitUnnamedTerminalSymbols(rule);

        newGrammar = newGrammar.Add(name, newRule);

        foreach(r is Rule.CaptureNamedTerminalSymbol in newRules)
        {
          newGrammar = newGrammar.Add(r.name, r);
        }
      }
      newGrammar
    }
        
    // this function replaces usages of terminal symbols in the right part of nonterminal 
    // rules by the call and created the terminal rule for this call
    // returns : (a , b)
    // a - modified nonterminal rule
    // b - list of new terminal rules called from a
    private SplitUnnamedTerminalSymbols(rule : Rule) : Rule * list[Rule]
    {  
      if(CanBeSplited(rule))
      {
        def name = RuleRef(Location.Default, tmpname(UnnamedRuleString));
        def r1   = Rule.CaptureNamedTerminalSymbol(name, rule);
        def r2   = Rule.Call(name);
        
        (r2, [r1])      
      }
      else 
      {
        match(rule)
        {
          | Capture(_, _ , _, r) 
          | RepeatMin(_, r)
          | RepeatMinMax(_, _, r)   =>
            def (newRule, splitedList) = SplitUnnamedTerminalSymbols(r);
              
            def retRule =
              match(rule)
              {
                | Capture(n, r1, r2,_)  => Rule.Capture(n, r1, r2, newRule);
                | RepeatMin(a, _)       => Rule.RepeatMin(a, newRule);
                | RepeatMinMax(a, b, _) => Rule.RepeatMinMax(a, b, newRule);
                | _                     => assert(false);
              };
            
            (retRule, splitedList)
          
          | Sequence(rules)
          | Choice(rules)           =>
            mutable newRules : list[Rule] = [];
            mutable splitedRules = [];
            
            foreach(r in rules)
            {          
              def (newRule, splitedList) = SplitUnnamedTerminalSymbols(r);
              
              newRules = newRules + [newRule];
              splitedRules = splitedRules + splitedList;          
            }
            
            def retRule = 
              match(rule)
              {
                | Sequence(_) => Rule.Sequence(newRules);
                | Choice(_)   => Rule.Choice(newRules);
                | _           => assert(false);
              };
            
            (retRule, splitedRules);
            
          | _                       => (rule, []) 
        } 
      }
    }
    
    /// Determins whether the rule is terminal (combination of terminal symbols 
    /// whithout calling any rule)
    private CanBeSplited(rule : Rule) : bool
    {
      | Chars                       => true
      | CaptureNamedTerminalSymbol  
      | Capture                     
      | Not                  
      | And                  
      | Call                        => false
      | RepeatMin(_, rule)          
      | RepeatMinMax(_, _, rule)    => CanBeSplited(rule)
      | Sequence(rules)             
      | Choice(rules)               => !rules.Exists(r => !CanBeSplited(r))
      | ExtensionPoint              => throw NotImplementedException();
    }
  }
}
