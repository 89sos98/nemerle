using System;
using System.CodeDom;
using System.CodeDom.Compiler;
using System.Collections;
using System.IO;
using System.Reflection;

using Nemerle.Compiler;
using Nemerle.Compiler.Typedtree;
using Nemerle.Collections;
using SCG = System.Collections.Generic;

namespace Nemerle.Compiler.Utils
{
  public module CodeDomHelper
  {
    public IsPartial(attrs: NemerleAttributes): bool
    {
      attrs %&& NemerleAttributes.Partial
    }

    public GetMemberAttributes(attrs: NemerleAttributes, field: bool = false): MemberAttributes
    {
      mutable res: MemberAttributes;

      when(attrs %&& NemerleAttributes.Static) res |= MemberAttributes.Static;
      when(attrs %&& NemerleAttributes.Public) res |= MemberAttributes.Public;
      when(attrs %&& NemerleAttributes.Private) res |= MemberAttributes.Private;

      if(attrs %&& NemerleAttributes.Internal && attrs %&& NemerleAttributes.Protected)
        res |= MemberAttributes.FamilyOrAssembly
      else
      {
        when(attrs %&& NemerleAttributes.Internal) res |= MemberAttributes.FamilyAndAssembly;
        when(attrs %&& NemerleAttributes.Protected) res |= MemberAttributes.Family;
      }

      when(field && !(attrs %&& NemerleAttributes.Mutable)) res |= MemberAttributes.Const;

      when(attrs %&& NemerleAttributes.New) res |= MemberAttributes.New;
      when(attrs %&& NemerleAttributes.Override) res |= MemberAttributes.Override;
      when(attrs %&& NemerleAttributes.Sealed) res |= MemberAttributes.Final;
      when(attrs %&& NemerleAttributes.Virtual) res |= MemberAttributes.VTableMask;

      when(res == 0)
      {
        if(field)
          res |= MemberAttributes.Private
        else
          res |= MemberAttributes.Assembly
      }

      res;  
    }

    public GetTypeAttributes(attrs: NemerleAttributes): TypeAttributes
    {
      if(attrs == NemerleAttributes.None)
        TypeAttributes.NotPublic;
      else
      {
        mutable res: TypeAttributes = 
          if(attrs %&& NemerleAttributes.Public) 
            TypeAttributes.Public
          else
            TypeAttributes.NotPublic;
 
        when(attrs %&& NemerleAttributes.Abstract) res |= TypeAttributes.Abstract;
        when(attrs %&& NemerleAttributes.Sealed) res |= TypeAttributes.Sealed;
        when(attrs %&& NemerleAttributes.SpecialName) res |= TypeAttributes.SpecialName;
        res;
      }
    }

    public GetLinePragma(expr: TExpr) : CodeLinePragma
    {
      def loc = expr.Location;
      CodeLinePragma(loc.File,loc.Line)
    }

    public MapFilterByType[T1,T2](this collection: SCG.IEnumerable[T1]) : list[T2]
    {
      def res = SCG.List();
      foreach(e is T2 in collection)
        res.Add(e);

      res.ToList()
    }

    public MapFilterByType[T2](this collection: IEnumerable) : list[T2]
    {
      def res = SCG.List();
      foreach(e is T2 in collection)
        res.Add(e);

      res.ToList()
    }

    public StringToListOfLines(s: string) : SCG.List[string]
    {
      def res = SCG.List();
      
      using(def sr = StringReader(s))
        for(mutable s1 = sr.ReadLine(); s1 != null; s1 = sr.ReadLine())
          res.Add(s1);
      
      res
    }

    // Debug output method
    public ToString(unit: CodeCompileUnit, generator: ICodeGenerator) : string 
    {
      mutable s : string = null;
      using (def sw = StringWriter())
      {
        //def codeGen = NemerleCodeGenerator() : ICodeGenerator;
        generator.GenerateCodeFromCompileUnit(unit,sw,null);
        s = sw.ToString();
      }
      s
    }

    public PrintMacro(the_macro: IMacro) : string
    {
      $"[Macro:\n GetName = $(the_macro.GetName())\n"
        " GetNamespace () = $(the_macro.GetNamespace ())\n"
        " Keywords = $(the_macro.Keywords)\n"
        " Usage = $(the_macro.Usage)\n";
        //" SyntaxExtension = $(the_macro.SyntaxExtension())]";
    }
  }
}
