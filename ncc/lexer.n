/*
 * Copyright (c) 2003, 2004 The University of Wroclaw.
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions
 * are met:
 *    1. Redistributions of source code must retain the above copyright
 *       notice, this list of conditions and the following disclaimer.
 *    2. Redistributions in binary form must reproduce the above copyright
 *       notice, this list of conditions and the following disclaimer in the
 *       documentation and/or other materials provided with the distribution.
 *    3. The name of the University may not be used to endorse or promote
 *       products derived from this software without specific prior
 *       written permission.
 * 
 * THIS SOFTWARE IS PROVIDED BY THE UNIVERSITY ``AS IS'' AND ANY EXPRESS OR
 * IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES
 * OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN
 * NO EVENT SHALL THE UNIVERSITY BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
 * SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED
 * TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR
 * PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF
 * LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING
 * NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS
 * SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 */

using System.Text;
using System.Globalization;
using System;

using Nemerle.Collections;

namespace Nemerle.Compiler {

  public enum NumberMode {
    | Float
    | Decimal
    | Octal
    | Hex
    | Binary
  }
 

public variant Token
{
  | Identifier { name : string; }
  | Keyword { name : string; }
  | Operator { name : string; priority : int; }

  | StringLiteral { value : string; }
  | CharLiteral { value : char; }

  | SByteLiteral { value : SByte; }
  | ByteLiteral { value : Byte; }
  | ShortLiteral { value : Int16; }
  | UShortLiteral { value : UInt16; }
  | IntLiteral { value : int; }
  | UIntLiteral { value : UInt32; }
  | LongLiteral { value : Int64; }
  | ULongLiteral { value : UInt64; }

  | FloatLiteral { value : float; }
  | DoubleLiteral { value : Double; }
  | DecimalLiteral { value : Decimal; }
  | Comment { value : string; } 

  | EndOfFile
}

public abstract class LexerBase
{
  protected mutable line : int;
  protected mutable col : int;
  protected mutable putback : bool;
  protected mutable putbackVal : char;
  protected mutable isPendingChar : bool;  // is there already some first char
  protected mutable pendingChar : char;
  protected mutable negated_literal : bool;
  
  protected mutable last_line : int;
  protected mutable last_col : int;

  protected static keywords : Hashtable <string, int>;

  public class Error : System.Exception
  {
    public name : string;
    public this (name : string) { this.name = name; }
  }

  public this ()
  {
    line = 1;
    col = 1;
    putback = false;
    isPendingChar = false;
    negated_literal = false;
  }

  abstract protected do_read () : char;
        
  protected read () : char
  {
    if (!putback)
      do_read ()
    else {
      putback = false;
      putbackVal
    }
  }

  protected peek () : char
  {
    unless (putback)
      putbackVal = do_read ();
    putback = true;
    putbackVal
  }

  protected is_id_beg (ch : char) : bool
  {
    Char.IsLetter (ch) || ch == '_'
  }

  static is_opchar (ch : char) : bool
  {
    match (ch) {
      | '=' | '<' | '>' | '@' | '^' | '|' | '&' | '+'
      | '-' | '*' | '/' | '$' | '%' | '!' | '?' | '~'
      | '.' | ':' | '#' => true
      | _ => false
    }
  }

  protected get_op (priority : int, first_ch : char) : Token
  {
    def buf = Text.StringBuilder (5);
    ignore (buf.Append (first_ch));

    mutable go = true;
    while (go) {
      if (is_opchar (peek ())) {
        def c = read ();
        if (c == '/')
          if (comment_beginning () == '/')
            ignore (buf.Append ('/'))
          else
            go = false
        else
          ignore (buf.Append (c))
      }
      else
        go = false;
    };

    def s = buf.ToString ();
    match (s) {
      | "|" | "=>" | "=" | "<-" =>
        // cannot use this as regular operator
        Token.Operator (s, 0)
        
      | ":" | ":>" =>
        Token.Operator (s, 9)

      | _ => 
        Token.Operator (s, priority)
    }
  }

  protected get_number (first_ch : char) : Token
  {
    def buf = Text.StringBuilder (10);
    when (negated_literal) {
      ignore (buf.Append ('-'));
      negated_literal = false;
    };
    mutable already_seen_type = false;  // for the case 0b0 vs 0b
    
    mutable mode =
      match (first_ch) {
        | '.' => NumberMode.Float
        | '0' =>
          match (peek ()) {
            | 'x' | 'X' => ignore (read ()); NumberMode.Hex
            | 'o' | 'O' => ignore (read ()); NumberMode.Octal
            | 'b' | 'B' =>
              ignore (read ());
              unless (Char.IsDigit (peek ())) already_seen_type = true;
              NumberMode.Binary
              
            | x when Char.IsDigit (x) =>
              Message.warning (this.Location, "trailing zeros look like"
                               " octal modifiers, but they are not");
              NumberMode.Decimal
              
            | _ => NumberMode.Decimal
          }
        | _ => NumberMode.Decimal
      };
    mutable last_was_digit = true;

    ignore (buf.Append (first_ch));

    // read digits and . between them if it is present
    def loop () {
      match (peek ()) {
        | '.' => 
          when (mode == NumberMode.Decimal) {
            mode = NumberMode.Float;
            ignore (read ());
            if (Char.IsDigit (peek ())) {
              ignore (buf.Append ('.'));
              loop ()
            }
            else {
              isPendingChar = true;
              pendingChar = '.';
              last_was_digit = false;
              mode = NumberMode.Decimal;
            }
          }

        | 'a' | 'A' | 'b' | 'B' | 'c' | 'C' | 'd' | 'D'
        | 'e' | 'E' | 'f' | 'F' =>
          when (mode == NumberMode.Hex) {
            ignore (buf.Append (read ()));
            loop ()
          }

        | x when Char.IsDigit (x) =>
          ignore (buf.Append (read ()));
          loop ()
        | _ => ()
      }
    };
    loop ();
    
    def exponent_part (only_realsuf) {
      when (!only_realsuf) {
        match (peek ()) {
          | 'E' | 'e' =>
            ignore (buf.Append (read ()));
            match (peek ()) {
              | '+' | '-' =>
                ignore (buf.Append (read ()));
              | _ => ()
            };
            if (Char.IsDigit (peek ()))
              do {
                ignore (buf.Append (read ()));
              } while (Char.IsDigit (peek ()))
            else
              throw Error ("no digits after exponent sign in float literal")
          | _ => ()
        }
      };
      match (Char.ToLower (peek (), CultureInfo.InvariantCulture)) {
        | 'f' =>
          ignore (read ());
          Token.FloatLiteral (Single.Parse (buf.ToString (),
                                           NumberFormatInfo.InvariantInfo))
        | 'd' =>
          ignore (read ());          
          Token.DoubleLiteral (Double.Parse (buf.ToString (),
                                           NumberFormatInfo.InvariantInfo))
        | 'm' =>
          ignore (read ());          
          Token.DecimalLiteral (Decimal.Parse (buf.ToString (),
                                              NumberFormatInfo.InvariantInfo))
        | _ =>
          Token.DoubleLiteral (Double.Parse (buf.ToString (),
                                            NumberFormatInfo.InvariantInfo))
      }
    };

    // convert given object to appropriate integer value according to type suffix
    def check_type_suffix (val : object) {
      /// we should have integer number here

      def special (c) {
        | 'l' | 's' | 'b' | 'u' =>
          true
        | _ =>
          false
      };

      // check suffixes to make special types conversions
      mutable ch =
        if (already_seen_type)
          'b'
        else
          Char.ToLower (peek (), CultureInfo.InvariantCulture);
      if (special (ch)) {
        unless (already_seen_type) ignore (read ());
        mutable unsigned = ch == 'u';

        // we can have two letter suffixes
        def ch' = Char.ToLower (peek (), CultureInfo.InvariantCulture);
        if (special (ch')) {
          ignore (read ());
          if (ch' == 'u') {
            when (unsigned)
              throw Error ("bad integer suffix (unsigned twice)");
            unsigned = true;
          }
          else {
            // both chars can be different than 'u' and then they must be 'bs'
            match ((ch, ch')) {
              | ('s', 'b') | ('b', 's') =>
                ch = 'b'
              | _ =>
                unless (unsigned)
                  throw Error ("bad integer suffix (type contraint twice)");
                ch = ch';
            }
          }
        }
        else
          // only 'b' suffix, so it is unsigned byte
          when (ch == 'b') unsigned = true;

        // [ch] now contains type meaning or 'u' (then it is uint for sure)
        match (ch) {
          | 'b' =>
            if (unsigned)
              Token.ByteLiteral (Convert.ToByte (val, CultureInfo.InvariantCulture))
            else
              Token.SByteLiteral (Convert.ToSByte (val, CultureInfo.InvariantCulture))
          | 's' =>
            if (unsigned)
              Token.UShortLiteral (Convert.ToUInt16 (val, CultureInfo.InvariantCulture))
            else
              Token.ShortLiteral (Convert.ToInt16 (val, CultureInfo.InvariantCulture))
          | 'l' =>
            if (unsigned)
              Token.ULongLiteral (Convert.ToUInt64 (val, CultureInfo.InvariantCulture))
            else
              Token.LongLiteral (Convert.ToInt64 (val, CultureInfo.InvariantCulture))
          | _ =>
            Token.UIntLiteral (Convert.ToUInt32 (val, CultureInfo.InvariantCulture))
        }
      }
      else
        Token.IntLiteral (Convert.ToInt32 (val, CultureInfo.InvariantCulture))
    };
    
    try {
      match (mode) {
        | NumberMode.Float =>
          match (peek ()) {
            | 'E' | 'e' => 
              exponent_part (false)
            | _ =>
              exponent_part (true)
          }
        | NumberMode.Decimal =>
          if (last_was_digit)
            match (Char.ToLower (peek (), CultureInfo.InvariantCulture)) {
              | 'e' =>
                exponent_part (false)
              | 'f' | 'd' | 'm' =>
                exponent_part (true)
              | _ =>
                check_type_suffix (buf.ToString ())
            }
          else
            Token.IntLiteral (Convert.ToInt32 (buf.ToString ()))

        | NumberMode.Hex =>
          def val = UInt64.Parse (buf.ToString (), Globalization.NumberStyles.HexNumber,
                                  CultureInfo.InvariantCulture);
          check_type_suffix (val)

        | NumberMode.Binary =>
          mutable value = 0ul;
          for (mutable i = 0; i < buf.Length; ++i)
            match (buf[i]) {
              | '0' => value <<= 1
              | '1' => value <<= 1; value += 1ul
              | x =>
                Message.error (this.Location, $"binary literal numer must be"
                               " composed of 1 and 0s, while there is `$(x)'")
            };
          check_type_suffix (value)

        | NumberMode.Octal =>
          mutable value = 0ul;
          for (mutable i = 0; i < buf.Length; ++i)
            match (Convert.ToUInt64 (buf[i]) - 48ul) {
              | x when 0ul <= x && x <= 7ul => value <<= 3; value += x;
              | x =>
                when (0ul <= x && x <= 7ul) Util.ice ("matching error");
                Message.error (this.Location, $"octal literal number must be"
                               " composed of 0 to 7 digits while it has `$(x)'")
            };
          check_type_suffix (value)
      }
    }
    catch {
      _ : System.OverflowException =>
        Message.error (this.Location,
                       $ "number literal $buf is too large for given type");
        Token.IntLiteral (0)
    }
  }

  protected get_id (first_ch : char) : Token
  {
    if (first_ch == '\'' && !is_id_beg (peek ())) {
      get_char ()
    } else {
      def buf = Text.StringBuilder ();
      ignore (buf.Append (first_ch));

      while (is_id_beg (peek ()) || 
             Char.IsDigit (peek ()) || 
             peek () == '\'') {
        ignore (buf.Append (read ()))
      };
          
      def str = buf.ToString ();
      
      if (first_ch == '\'' && str.Length == 3 && str[2] == '\'')
        Token.CharLiteral (str[1])
      else if (keywords.Contains (str))
        Token.Keyword (str)
      else
        Token.Identifier (str)
    }
  }

  protected escape_value (ch : char) : char
  {
    match (ch) {
      | 'n' => '\n'
      | 't' => '\t'
      | 'r' => '\r'
      | 'b' => '\b'
      | 'e' => '\e'
      | '"' => '"'
      | '\'' => '\''
      | '\\' => '\\'
      | '0' => '\0'
      | _ => throw Error ("bad escape character")
    }
  }
    
  protected get_char () : Token
  {
    match (get_string ('\'')) {
      | Token.StringLiteral (v) =>
        if (v.Length == 0)
          throw Error ("empty character literal")
        else {
          if (v.Length != 1)
            throw Error ("character literal too long")
          else
            Token.CharLiteral (v[0])
        }
      | _ => Util.ice ("Value is not string in get_char")
    }
  }

  virtual protected ignore_comments () : void { () }
  
  protected get_string (end_ch : char) : Token
  {
    def buf = StringBuilder ();
    mutable go = true;
              
    while (go) {
      match (read ()) {
        | '\\' =>
          def esc = escape_value (read ());
          ignore (buf.Append (esc));
        | '\n' =>
          throw Error ("newline before end of string")
        | ch when ch != end_ch => 
          ignore (buf.Append (ch))
        | _ =>
          def ch = eat_whitespace (' ');
          if (ch == end_ch)
            ignore_comments ();
          else {
            isPendingChar = true;
            pendingChar = ch;
            go = false
          }
      }
    };
     
    Token.StringLiteral (buf.ToString ())
  }

  protected get_monkey_string () : Token
  {
    def buf = StringBuilder (32);

    def loop () {
      match (read ()) {
        | '"' =>
          match (peek ()) {
            | '"' =>
              ignore (buf.Append ('"'));
              ignore (read ());
              loop ()
            | _ =>
              Token.StringLiteral (buf.ToString ())
          }
        | ch =>
          ignore (buf.Append (ch)); loop ()
      }
    };

    loop ()
  }

  abstract protected comment_beginning () : char;
  
  abstract protected eat_whitespace (ch : char) : char;

  protected do_get_token (ch : char) : Token
  {
    last_line = line;
    last_col = col;
    
    match (ch) {
      | '"' => get_string ('"')

      | '\'' => get_id ('\'')

      | '.' => 
        if (Char.IsDigit (peek ()))
          get_number (ch)
        else
          Token.Operator (ch.ToString (), 0)

      | '{' | '}' 
      | '[' | ']' 
      | ',' | ';' 
      | '(' | ')' => Token.Operator (String (ch, 1), 0)

      | '@' =>
        def next = read ();
        if (is_opchar (next))
          match (get_op (0, next)) {
            | Token.Operator (s, _) => Token.Identifier (s)
            | _ => Util.ice ()
          }
        else if (is_id_beg (next))
          match (get_id (next)) {
            | Token.Identifier (x)
            | Token.Keyword (x) => Token.Identifier (x)
            | _ => Util.ice ()
          }
        else if (next == '"')
          get_monkey_string ()
        else
          throw Error ("expecting operator, identifier or string literal after '@'")
      
      | '*' =>
        if (peek () == '*')
          get_op (1, ch)
        else
          get_op (2, ch)

      | '/' =>
        get_op (2, ch)
                              
      | '%' | '\\'
        => get_op (2, ch)

      | '-' =>
        if (Char.IsDigit (peek())) {
          negated_literal = true;
          Token.Operator ("+", 3)
        }
        else
          if (peek () != '.') get_op (3, ch) 
          else Token.Operator (ch.ToString (), 3)          

      | '+'  
        =>
           if (peek () != '.') get_op (3, ch) 
           else Token.Operator (ch.ToString (), 3)
      
      | '^' | '$' | '~' | '?' | '#'
        => get_op (4, ch)
        
      | '=' | '<' | '>' | '!' 
        => get_op (5, ch)
        
      | '&' => get_op (6, ch)
      | '|' => get_op (7, ch)
      | ':' => get_op (8, ch)

      // after executing eat_whitespace it is the only possibility for space
      | ' ' => Token.EndOfFile ()
        
      | _ =>
        if (Char.IsDigit (ch))
          get_number (ch)
        else
          if (is_id_beg (ch))
            get_id (ch)
          else
            throw Error ("invalid character")
    }      
  }

  abstract public GetToken () : Token;

  abstract public Location : Location { get; }

  abstract public CurrentLocation : Location { get; }

  public static this () 
  {
    def tab = array [
        "_", "abstract", "and", "array", "as", "base", "catch",
        "class", "def", "delegate", "enum", "event",
        "extern", "false", "finally", "fun", "implements",
        "interface", "internal", "is", "macro", "match", "matches",
        "module", "mutable", "namespace", "new", "null", "out",
        "override", "params", "private", "protected", "public",
        "ref", "sealed", "static", "struct", "syntax", "this",
        "throw", "true", "try", "type", "typeof", "using",
        "variant", "virtual", "void", "volatile", "when", "where"
    ];
    keywords = Hashtable ();
    foreach (el in tab) keywords.Add (el, 1);
  }

  public static AddKeyword (str : string) : void 
  {
    match (keywords.Get (str)) {
      | Some (level) =>
        keywords.Set (str, level + 1)
      | _ =>
        keywords.Set (str, 1)
    }
  }

  public static RemoveKeyword (str : string) : void 
  {
    match (keywords.Get (str)) {
      | Some (1) =>
        keywords.Remove (str)
      | Some (level) =>
        keywords.Set (str, level - 1)
      | _ =>
        Util.ice ("trying to remove non-existant keyword from lexer")
    }
  }
  
  public static IsOperator (str : string) : bool
  {
    def len = str.Length;
    mutable is_op = true;
    for (mutable i = 0; i < len && is_op; ++i) {
      def c = str[i];
      is_op = is_opchar (c) || c == '(' || c == ')' || c == ';';
    };
    is_op
  }

  public static IsKeyword (str : string) : bool
  {
    def len = str.Length;
    mutable is_kw = true;
    for (mutable i = 0; i < len && is_kw; ++i)
      is_kw = Char.IsLetter (str[i]);
    is_kw
  }
} // end class LexerBase

public class LexerFile : LexerBase, IDisposable
{
  reader : System.IO.TextReader;
  mutable file_name : string;
  mutable file_idx : int;
  comment_store : StringBuilder;
  
 #region PREPROCESSOR VARIABLES
  /** if there was only white chars from beginnig of line */
  mutable white_beginning : bool;  

  mutable eating_stack : list<int>; // stack of nested #if

  /** -1 = now inside true preprocessing section
       0 = no preprocessing currently in action
       1 = there were no true preprocessing sections in this set
       2 = there already was true preprocessing section in this set
       3 = we are now inside #else section, but it is not analyzed   */
  mutable eating_now : int;
  mutable line_stack : int; // real line number at moment of `#line 3' occurence
  mutable line_start : int; // how to compute real line after `#line default' occurence
  file_real : string;           // real filename to revert after `#line 4 "bla"'
  
  protected defines : Hashtable <string, bool>;

 #endregion PREPROCESSOR VARIABLES

  public static command_defines : Hashtable <string, bool>;
  public static mutable store_comments : bool;
 
  private check_last_line_for_lf (file : IO.FileStream) : void
  {
    def _ = file.Seek ((-1 :> Int64), IO.SeekOrigin.End);
    
    def inp = file.ReadByte ();
    
    if (inp >= 0) {
      def ch = Convert.ToChar (inp);
      when (ch != '\n')
        Message.warning (this.Location, "no new line at the end of the file");
    }
    else 
      throw LexerBase.Error ("unexpected end of file");
      
    def _ = file.Seek ((0 :> Int64), IO.SeekOrigin.Begin);
    ()
  }

  public static this () {
    store_comments = false;
    command_defines = Hashtable ();
  }

  public Dispose () : void {
    (reader : IDisposable).Dispose ();
  }
  
  public this (fn : string)
  {
    base ();
    comment_store = StringBuilder (300);
    file_name = fn;
    file_idx = Location.GetFileIndex (file_name);
    file_real = fn;
    line_stack = -1;
    def file = IO.FileStream (fn, IO.FileMode.Open, IO.FileAccess.Read);
    reader = IO.StreamReader (file, Text.Encoding.UTF8);
    when (Flags.pedantic_lexer)
      check_last_line_for_lf (file);

    white_beginning = true;
    defines = Hashtable (25);
    foreach (el in command_defines) defines.Add (el.Key, true);
    eating_stack = [];
    eating_now = 0;
  }

  override protected do_read () : char
  {
    def inp = reader.Read ();
    if (inp >= 0) {
      def ch = (inp :> char);
      match (ch) {
        | '\n' =>
          line = line + 1;
          col = 1
        | '\r' =>
          when (Flags.pedantic_lexer)
            Message.warning (this.Location,
                             "CR character found in input stream")
        | '\t' =>
          when (Flags.pedantic_lexer)
            Message.warning (this.Location,
                             "tab character found in input stream")
        | _ =>
          col = col + 1
      };
      ch
    } else 
      throw LexerBase.Error ("unexpected end of file")
  }
        
  override protected comment_beginning () : char
  {
    match (peek ()) {
      | '/' =>
        // we are for sure in one line comment
        _ = read ();
        if (store_comments && peek () == '/') {
          last_line = line;
          last_col = col - 2;
          
          _ = read ();
          mutable cc = ' ';
          do {
            cc = read ();                
            ignore (comment_store.Append (cc))
          } while (cc != '\n')
        }
        else
          while (read () != '\n') {};
          white_beginning = true;
        // pass whitespace, so next read would be eof checked
        ' '

      | '*' =>
        // multiline comment
        _ = read ();
        def loop1 (seen_star, store) {
          def cc = read ();
          when (store)
            ignore (comment_store.Append (cc));

          match (cc) {
            | '*' => loop1 (true, store)
            | '/' when seen_star => ()
            | _ => loop1 (false, store)
          }
        };
        if (store_comments && peek () == '*') {
          last_line = line;
          last_col = col - 2;
       
          _ = read ();
          loop1 (true, true);
          if (comment_store.Length == 1)
            ignore (comment_store.Remove (0, 1))
          else
            ignore (comment_store.Remove (comment_store.Length - 2, 2))
        }
        else
          loop1 (false, false);

        // pass whitespace, so next read would be eof checked            
        ' '

      | _ => '/'
    }
  }

  override protected eat_whitespace (ch : char) : char
  {
    mutable eof = false;
    
    def loop (c) {
      | ' ' | '\t' | '\r' | '\n' =>
        when (c == '\n') white_beginning = true;
        def ch = 
          try { 
            read ()
          } catch { _ : LexerBase.Error => 
            eof = true; '_'  };
        if (eof) ' ' else loop (ch)
      | '/' =>
        white_beginning = false;
        match (comment_beginning ()) {
          | '/' => '/'
          | c => loop (c)
        }
        
      | '#' =>
        if (white_beginning) {
          parse_preprocessor ();
          loop (' ')
        }
        else
          throw LexerBase.Error ("preprocessor directives must occur only in"
                                 " lines beginning with whitespaces")
        
      | _ =>
        white_beginning = false;
        c
    };
    loop (ch)
  }

  override protected ignore_comments () : void {
    when (comment_store.Length > 0) {
      Message.warning (this.Location,
                       "documentation comments between literals are ignored");
      ignore (comment_store.Remove (0, comment_store.Length))
    }
  }
  
  parse_preprocessor () : void
  {
    def read_to_the_end_of_line () {
      mutable c = ' ';
      def line = StringBuilder (80);
      while (c != '\n') {
        ignore (line.Append (c));
        try { c = read () }
        catch { _ : LexerBase.Error => c = '\n' }
      };
      line.ToString ()
    };
    // eof isn't checked, because we are eating and expecting something
    // not white before end of line
    def eat_spaces () : char {
      mutable c = ' ';
      while (Char.IsWhiteSpace (c) && c != '\n') 
        c = read ();
      c
    };
    def read_word () : string {
      def word = StringBuilder (eat_spaces ().ToString ());
      try {
        while (is_id_beg (peek ()) || Char.IsDigit (peek ()))
          ignore (word.Append (read ()))
      } catch { _ : LexerBase.Error => () };
      word.ToString ()
    };

    def directive = read_word ();
    when (directive == "")
      throw LexerBase.Error ("expected preprocessing directive after `#'");

    match (directive) {
      | "if" =>
        eating_stack = eating_now :: eating_stack;
        if (evaluate_preprocessing_expr (read_to_the_end_of_line ().Trim ()))
          unless (eating_now > 0)          
            eating_now = -1
        else
          eating_now = 1

      | "elif" =>
        def now = evaluate_preprocessing_expr (read_to_the_end_of_line ().Trim ());
        match (eating_stack) {
          | x :: _ =>
            unless (x > 0)
              match (eating_now) {
                | 1 => when (now) eating_now = -1
                | -1 | 2 => eating_now = 2
                | _ => throw LexerBase.Error ("unbalanced preprocessing directives")
              }
          | [] => throw LexerBase.Error ("unbalanced #else");
        }

      | "else" =>
        ignore (read_to_the_end_of_line ());
        match (eating_stack) {
          | x :: _ =>
            unless (x > 0)
              match (eating_now) {
                | 1 => eating_now = -1
                | -1 | 2 => eating_now = 3
                | _ => throw LexerBase.Error ("unbalanced preprocessing directives")
              }
          | [] => throw LexerBase.Error ("unbalanced #else");
        }
        
      | "endif" =>
        ignore (read_to_the_end_of_line ());
        match (eating_stack) {
          | x :: xs =>
            eating_now = x; eating_stack = xs
          | [] => throw LexerBase.Error ("unbalanced #endif");
        }

      | "line" =>
        mutable c = eat_spaces ();
        def (new_line, new_file) =
          if (c == 'd') {
            if (read_word () == "efault") {
              ignore (read_to_the_end_of_line ());
              (-1, null)
            }
            else
              throw LexerBase.Error ("expecting line number or `default' indicator")
          }
          else {
            def num = StringBuilder (c.ToString ());
            try {
              while (Char.IsDigit (peek ()))
                ignore (num.Append (read ()))
            } catch { _ : LexerBase.Error => c = ' ' };

            (Int32.Parse (num.ToString ()), read_to_the_end_of_line ().Trim ())
          };
        if (new_line == -1) {
          line = line - line_start + line_stack;
          file_name = file_real;
          file_idx = Location.GetFileIndex (file_real);
          line_stack = -1;
        }
        else {
          // if there is already something on stack, bring real line first
          when (line_stack != -1)
            line = line - line_start + line_stack;
          line_start = new_line;
          line_stack = line;
          line = new_line;
          when (new_file != "") {
            file_name = new_file;
            file_idx = Location.GetFileIndex (new_file);
          }
        }

      | "error" => 
        Message.error (CurrentLocation, read_to_the_end_of_line ().Trim ());
        
      | "warning" =>
        Message.warning (CurrentLocation, read_to_the_end_of_line ().Trim ());

      | "region" =>
        def line = read_to_the_end_of_line ().Trim ();
        ignore (comment_store.Append ("<region>").Append (line).
                Append ("</region>"));
        
      | "endregion" =>
        def line = read_to_the_end_of_line ();
        ignore (comment_store.Append ("<endregion>").Append (line).
                Append ("</endregion>"));

      | "define" =>
        defines.Set (read_word (), true);
        ignore (read_to_the_end_of_line ())

      | "undef" =>
        defines.Set (read_word (), false);
        ignore (read_to_the_end_of_line ())
        
      | x => throw LexerBase.Error ("unsupported preprocessing directive `" + x + "'")
    };
  }

  evaluate_preprocessing_expr (str : string) : bool {
    def traverse (line : string, acc) {
      mutable i = 0;
      while (i < line.Length && Char.IsWhiteSpace (line [i])) ++i;
      def line = line.Substring (i);
      
      match (line) {
        | x when x.StartsWith ("false") =>
          traverse (x.Substring (5), "false" :: acc)
        | x when x.StartsWith ("true") =>
          traverse (x.Substring (4), "true" :: acc)
        | x when x.StartsWith ("||") =>
          traverse (x.Substring (2), "||" :: acc)
        | x when x.StartsWith ("&&") =>
          traverse (x.Substring (2), "&&" :: acc)
        | x when x.StartsWith ("==") =>
          traverse (x.Substring (2), "==" :: acc)
        | x when x.StartsWith ("!=") =>
          traverse (x.Substring (2), "!=" :: acc)
        | x when x.StartsWith ("(") =>
          traverse (x.Substring (1), "(" :: acc)
        | x when x.StartsWith (")") =>
          traverse (x.Substring (1), ")" :: acc)
        | x when x.StartsWith ("!") =>
          traverse (x.Substring (1), "!" :: acc)
        | "" => List.Rev (acc)
        | x =>
          mutable j = 0;
          while (j < x.Length && (is_id_beg (x[j]) || Char.IsDigit (x[j]))) ++j;
          when (j == 0)
            throw LexerBase.Error ("bad preprocessing condition format");
          def val = 
            match (defines.Get (x.Substring (0, j))) {
              | Some (v) => v
              | None => false
            };
          if (val) traverse (x.Substring (j), "true" :: acc)
          else traverse (x.Substring (j), "false" :: acc)
      }
    };
    mutable tokens = traverse (str, []);

    def lowest () {
      match (tokens) {
        | "(" :: xs =>
          tokens = xs;
          def res = highest ();
          match (tokens) {
            | ")" :: xs => tokens = xs
            | _ =>
              throw LexerBase.Error ("unbalanced parenthesis in preprocessing expression")
          };
          res
        | "!" :: xs => tokens = xs; !lowest (); 
        | "false" :: xs => tokens = xs; false
        | "true" :: xs => tokens = xs; true
        | _ => throw LexerBase.Error ("bad expression")
      }
    }
    and low () {
      mutable res = lowest ();
      def loop () {
        match (tokens) {
          | "&&" :: xs =>
            tokens = xs; res = lowest () && res; loop ()
          | _ => ()
        }
      };
      loop ();
      res
    }
    and high () {
      mutable res = low ();
      def loop () {
        match (tokens) {
          | "||" :: xs =>
            tokens = xs; res = low () || res; loop ()
          | _ => ()
        }
      };
      loop ();
      res
    }
    and highest () {
      def h = high ();
      match (tokens) {
        | "==" :: xs => tokens = xs; h == high ();
        | "!=" :: xs => tokens = xs; h != high ();
        | _ => h
      }
    };
    highest ()
  }

  private take_token () : Token
  {
    def ch = 
      if (isPendingChar) {
        isPendingChar = false;
        eat_whitespace (pendingChar)
      }
      else
        eat_whitespace (' ');

    if (comment_store.Length > 0) {
      isPendingChar = true;
      pendingChar = ch;

      def res = Token.Comment (comment_store.ToString ());
      ignore (comment_store.Remove (0, comment_store.Length));
      res
    }
    else
      do_get_token (ch)
  }
  
  public override GetToken () : Token
  {
    mutable result = null;
    do {
      match (take_token ()) {
        | Token.EndOfFile =>
          if (eating_now > 0) {
            Message.error (CurrentLocation, "unexpected end of file"
                           " before finishing preprocessor directive");
            eating_now = 0;
            result = Token.EndOfFile ()
          }
          else {
            unless (eating_stack matches [])
              Message.error (CurrentLocation, "there are still open"
                             " preprocessing directives at the end of file");
            result = Token.EndOfFile ()
          }
        | x => result = x
      };
    } while (eating_now > 0);
    result
  }

  public override Location : Location
  {
    get { Location (file_idx, last_line, last_col) }
  }

  public override CurrentLocation : Location
  {
    get { Location (file_idx, line, col) }
  }
} // end class LexerFile


public class LexerString : LexerBase
{
  reader : string;
  mutable pos : int;
  location : Location;

  public this (fn : string, loc : Location)
  {
    base ();
    reader = fn;
    pos = 0;
    location = loc;
  }

  override protected do_read () : char
  {
    if (pos < reader.Length) {
      def ch = reader[pos];
      ++pos;
      match (ch) {
        | '\n' =>
          line = line + 1;
          col = 1

        | _ => col = col + 1
      };
      ch
    } else throw LexerBase.Error ("unexpected end of file")
  }

  override protected comment_beginning () : char
  {
    match (peek ()) {
      | '/' =>
        // we are for sure in one line comment
        while (read () != '\n') {};
        // pass whitespace, so next read would be eof checked
        ' '

      | '*' =>
        // multiline comment
        ignore (read ());
        def loop1 (seen_star) {
          def cc = read ();

          match (cc) {
            | '*' => loop1 (true)
            | '/' when seen_star => ()
            | _ => loop1 (false)
          }
        };
        loop1 (false);
        // pass whitespace, so next read would be eof checked            
        ' '

      | _ => '/'
    }
  }
  
  override protected eat_whitespace (ch : char) : char
  {
    mutable eof = false;
    
    def loop (c) {
      | ' ' | '\t' | '\r' | '\n' =>
        def ch = 
          try { read () }
          catch { _ : LexerBase.Error => eof = true; '_'  };
        if (eof) ' ' else loop (ch)
      | '/' =>
        match (comment_beginning ()) {
          | '/' => '/'
          | c => loop (c)
        }
        
      | '#' =>
        throw LexerBase.Error ("preprocessor directives may not occur in"
                               " string programs")
        
      | _ => c
    };
    loop (ch)
  }

  public override GetToken () : Token
  {
    def ch = 
      if (isPendingChar) {
        isPendingChar = false;
        eat_whitespace (pendingChar)
      }
      else
        eat_whitespace (' ');

    do_get_token (ch)
  }

  public override Location : Location
  {
    get { location }
  }

  public override CurrentLocation : Location
  {
    get { location }
  }
} // LexerString

} // end ns
