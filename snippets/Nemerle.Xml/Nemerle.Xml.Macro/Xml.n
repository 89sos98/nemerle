using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;

using System.Collections.Generic;

namespace Nemerle.Xml
{
  public macro XmlQuoting(expr)
  syntax ("xml", expr)
  {
    Impl.ToXLinq(Nemerle.Macros.ImplicitCTX(), expr)
  }

  internal module Impl
  {
    internal ToXLinq(typer : Typer, expr : PExpr) : PExpr
    {
      _ = typer;
      //assert2(false);

      match (expr)
      {
        | PExpr.Literal(Literal where(RawString = rs)) =>
          if (rs.Length == 0 || rs[0] != '<')
            Message.FatalError(expr.Location, "The literal in 'xml' macro must be recursive string.")
          else
          {
            def str = rs.Substring(2, rs.Length - 4);
            def loc = expr.Location;
            def loc = Location(loc.FileIndex, loc.Line, loc.Column + 2, loc.EndLine, loc.EndColumn - 2);
            def parser = XmlQuotParser(loc, str);
            match (parser.Parse(str))
            {
              | Some(result) => 
                def transformer = XmlAstToXLinq(typer, parser);
                transformer.Transform(result, Set())
                
              | _ => Message.FatalError(parser.ToLocation(parser.MaxRollbackPos, parser.MaxRollbackPos + 1), 
                      "Invalid XML literal")
            }
          }
          
        | _ => Message.FatalError(expr.Location, "You must pass recursive string with XML literal into 'xml' macro.")
      }
    }
  }
}
