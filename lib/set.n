/*
 * Copyright (c) 2004 The University of Wroclaw.
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions
 * are met :
 *    1. Redistributions of source code must retain the above copyright
 *       notice, this list of conditions and the following disclaimer.
 *    2. Redistributions in binary form must reproduce the above copyright
 *       notice, this list of conditions and the following disclaimer in the
 *       documentation and/or other materials provided with the distribution.
 *    3. The name of the University may not be used to endorse or promote
 *       products derived from this software without specific prior
 *       written permission.
 * 
 * THIS SOFTWARE IS PROVIDED BY THE UNIVERSITY ``AS IS'' AND ANY EXPRESS OR
 * IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES
 * OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN
 * NO EVENT SHALL THE UNIVERSITY BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
 * SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED
 * TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR
 * PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF
 * LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING
 * NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS
 * SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 */

using Nemerle;

namespace Nemerle.Collections
{

  public class Set <'a>
    where 'a : IComparable <'a>
  {
    public static Singleton<'a> (elem : 'a) : Set <'a>
      where 'a : IComparable <'a>
    {
      Set ().Add (elem)
    }

    root : Tree.Node <'a>;
    
    public this ()
    {
      root = Tree.Node.Leaf ();
    }

    this (x : Tree.Node <'a>)
    {
      root = x;
    }

    public Add (elem : 'a) : Set <'a>
    {
      Set (Tree.Insert (root, elem, false))
    }

    public Replace (elem : 'a) : Set <'a>
    {
      Set (Tree.Insert (root, elem, true))
    }

    public Fold<'b> (ini : 'b, f : 'a * 'b -> 'b) : 'b
    {
      Tree.Fold (root, f, ini)
    }

    public Iter (f : 'a -> void) : void
    {
      _ = Tree.Fold (root, fun (e, _) { f (e); null }, null)
    }

    public Contains (elem : 'a) : bool
    {
      Tree.Get (root, elem) matches Some
    }

    public Remove (elem : 'a) : Set <'a>
    {
      Set (Tree.Delete (root, elem, false))
    }

    public Join (s : Set <'a>) : Set <'a>
    {
      Set (Tree.Fold (s.root, fun (e, s) { Tree.Insert (s, e, true) }, root))
    }

    public Substract (s : Set <'a>) : Set <'a>
    {
      Set (Tree.Fold (s.root, fun (e, s) { Tree.Delete (s, e, false) }, root))
    }

    public Intersect (s : Set <'a>) : Set <'a>
    {
      Set (Tree.Fold (s.root, fun (e, s) { 
          match (Tree.Get (root, e)) {
            | Some => Tree.Insert (s, e, false)
            | None => s
          }
        }, Tree.Node.Leaf ()))
    }

    public Xor (o : Set <'a>) : Set <'a>
    {
      def s1 = 
        Tree.Fold (root, fun (e, s) {
            match (Tree.Get (o.root, e)) {
              | Some => s
              | None => Tree.Insert (s, e, false)
            }
          }, Tree.Node.Leaf ());
      def s2 =
        Tree.Fold (o.root, fun (e, s) {
            match (Tree.Get (root, e)) {
              | Some => s
              | None => Tree.Insert (s, e, false)
            }
          }, s1);

      Set (s2)
    }
    
    public Item [elem : 'a] : bool
    {
      get { Contains (elem) }
    }
  }

}
