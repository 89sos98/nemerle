using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Assertions;

using System;
using System.Collections.Generic;
using System.Linq;

namespace LRPEGCC
{
  [Record]
  public variant Transition
  {
    public From : int;
    public To : int;
    | Symbol { Chars : RangeSet }
    | Epsilon

    public override ToString() : string
    {
      match (this)
      {
        | Symbol(chars) => $"Symbol($From $To [$chars])"
        | Epsilon       => $"Epsilon($From $To)"
      }
    }
  }

  public class FSM
  {
    [Accessor]
    private _stateCount : int;
    [Accessor]
    private _transitions : list[Transition];
    [Accessor]
    private _startState : int;
    [Accessor]
    private _okState : int;

    public this()
    {
      _stateCount  = 2;
      _transitions = [];
      _startState  = 0;
      _okState     = 1;
    }

    private this(stateCount : int, transitions : list[Transition], startState : int, okState : int)
    {
      _stateCount  = stateCount;
      _transitions = transitions;
      _startState  = startState;
      _okState     = okState;
    }

    public NewState() : FSM * int
    {
      def fsm = FSM(StateCount + 1, Transitions, StartState, OkState);
      (fsm, StateCount);
    }

    public NewState(state : int) : FSM
    {
      if (state < StateCount)
        this;
      else
        FSM(state + 1, Transitions, StartState, OkState);
    }

    public NewStartState() : FSM * int
    {
      def fsm = FSM(StateCount + 1, Transitions, StateCount, OkState);
      (fsm, StateCount);
    }

    public NewOkState() : FSM * int
    {
      def fsm = FSM(StateCount + 1, Transitions, StartState, StateCount);
      (fsm, StateCount);
    }

    public SetStartState(state : int) : FSM
    {
      FSM(StateCount, Transitions, state, OkState);
    }

    public SetOkState(state : int) : FSM
    {
      FSM(StateCount, Transitions, StartState, state);
    }

    public SetTransitions(transitions : list[Transition]) : FSM
    {
      FSM(StateCount, transitions, StartState, OkState);
    }

    public AddTransition(t : Transition) : FSM
      requires  0 <= t.To && t.To < _stateCount && 0 <= t.From && t.From < _stateCount
    {
      FSM(StateCount, t :: Transitions, StartState, OkState);
    }

    public IncludeFSM(fsm : FSM) : FSM * int * int
    {
      def transitions = fsm.Transitions.Fold(_transitions, (transition, transitions) =>
      {
        def from = transition.From + StateCount;
        def to = transition.To + StateCount;
        def transition = match (transition : Transition)
        {
        | Symbol(chars) => Transition.Symbol(from, to, chars);
        | Epsilon       => Transition.Epsilon(from, to);
        };
        transition :: transitions;
      });
      def newFsm = FSM(StateCount + fsm.StateCount, transitions, StartState, OkState);
      (newFsm, fsm.StartState + StateCount, fsm.OkState + StateCount)
    }
  }
}
