using Nemerle.Compiler;
using Nemerle.Utility;

namespace Nemerle.Completion2
{
  module Relocation
  {
    public Relocate(this decl : Decl.Namespace, fileIndex : int, line : int, ch : int, lineOffset : int, chOffset : int) : void
    {
      assert(decl.Location.FileIndex == fileIndex);
      Relocate(decl, RelocationInfo (fileIndex, line, ch, lineOffset, chOffset));
      when (line == 1 && ch == 1)
      { // Положение начала файла не должно изменяться даже если редактирование происходит в его начале!
        def x = decl;
        def start = Location(x.Location.FileIndex, 1, 1, 1, 1);

        x.HeaderLocation        = start;
        x.BodyOpenTokenLocation = start;
        x.Location              = start +x.BodyCloseTokenLocation;
      }
    }
    
    Relocate(decl : Decl, info : RelocationInfo) : void
    {
      def loc = decl.Location;
      def isLocationChanged = loc.EndLine >  info.Line 
                           || loc.EndLine == info.Line && loc.EndColumn >= info.Char;
      when (isLocationChanged)
      {
        match (decl)
        {
          | GlobalAttribute => () //TODO: Add attribute support.
          | Using as x => 
            x.NameLocations = x.NameLocations.Map(Completion.Relocate(_, info));
            x.AliasLocation = Completion.Relocate(x.AliasLocation, info);

          | Namespace as x =>
            foreach (decl in x.Decls)
              Relocate(decl, info);
            x.NameLocations          = x.NameLocations.Map(Completion.Relocate(_, info));
            x.HeaderLocation         = Completion.Relocate(x.HeaderLocation, info);
            x.BodyOpenTokenLocation  = Completion.Relocate(x.BodyOpenTokenLocation, info);
            x.BodyCloseTokenLocation = Completion.Relocate(x.BodyCloseTokenLocation, info);

          | Type(ast) => (ast : ISupportRelocation).RelocateImpl(info);
          | None => ()
        }

        decl.Location = Completion.Relocate(loc, info);
      }
    }
  } // module Relocation
} // namespace Nemerle.Completion2
