
using Nemerle.Collections;
using Nemerle.IO;


module TestCollections
{
  test_hashtables (seed : int) : void
  {
    printf ("Testing hashtables...\n");
    
    def ht = Hashtable (seed) :> Hashtable <int, string>;

    ht [10] = "Ala";
    ht [20] = "ma";
    ht [30] = "kota";

    def cl = ht.Clone ();

    ht.Clear ();
    
    cl.Iter (fun (i : int, s : string) { printf ("[%i : %s] ", i, s) });

    printf ("total: %i elements\n", cl.Count);

    def folded = cl.Fold ("", fun (_, s : string, a : string) { a + s + " " });

    printf ("folded: %s\n", folded);

    def mapped = cl.Map (fun (i : int, s : string) { (i + s.Length, "`" + s + "'") });

    mapped.Iter (fun (i : int, s : string) { printf ("[%i : %s] ", i, s) });

    printf ("\nremoved 13: ");
    
    mapped.Remove (13);

    mapped.Iter (fun (i : int, s : string) { printf ("[%i : %s] ", i, s) });

    mapped.Set (13, "Bumtarara");

    printf ("\nset 13: ");
    
    match (mapped.Get (13)) {
      | Some => printf ("Bumtarara!\n")
      | _ => printf ("bad!\n")
    }
    
    match (mapped.Get (14)) {
      | Some => printf ("bad!\n")
      | _ => printf ("LO XIV glosno spiewa...\n")
    }

    when (!mapped.ContainsKey (13))
      printf ("bad!\n");

    when (mapped.ContainsKey (14))
      printf ("bad!\n");
      
    when (!mapped.ContainsValue ("`ma'"))
      printf ("bad!\n");
      
    when (mapped.ContainsValue ("matryca"))
      printf ("bad!\n");

    mapped.Clear ();

    printf ("After clearing: %i elements left\n", mapped.Count);

    try {
      mapped.Add (13, "lyzeczka!");
      mapped.Add (13, "bach!");
      mapped.Add (13, "dedelec!");
      mapped.Add (13, "bach!");
      printf ("bad!\n")
    }
    catch {
      _ : System.Exception => printf ("Good, can't add more than once!\n")
    }
  }

  Main () : void
  {
    test_hashtables (10)
  }
}


/*
BEGIN-OUTPUT
Testing hashtables...
[10 : Ala] [20 : ma] [30 : kota] total: 3 elements
folded: Ala ma kota 
[13 : `Ala'] [34 : `kota'] [22 : `ma'] 
removed 13: [34 : `kota'] [22 : `ma'] 
set 13: Bumtarara!
LO XIV glosno spiewa...
After clearing: 0 elements left
Good, can't add more than once!
END-OUTPUT
*/
