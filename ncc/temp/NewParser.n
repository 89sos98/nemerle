/*
 * Copyright (c) 2003-2005 The University of Wroclaw.
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions
 * are met:
 *    1. Redistributions of source code must retain the above copyright
 *       notice, this list of conditions and the following disclaimer.
 *    2. Redistributions in binary form must reproduce the above copyright
 *       notice, this list of conditions and the following disclaimer in the
 *       documentation and/or other materials provided with the distribution.
 *    3. The name of the University may not be used to endorse or promote
 *       products derived from this software without specific prior
 *       written permission.
 * 
 * THIS SOFTWARE IS PROVIDED BY THE UNIVERSITY ``AS IS'' AND ANY EXPRESS OR
 * IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES
 * OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN
 * NO EVENT SHALL THE UNIVERSITY BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
 * SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED
 * TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR
 * PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF
 * LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING
 * NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS
 * SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 */
#if NEWPARSER
 
using Nemerle.Compiler;
using Nemerle.Collections;
using Nemerle.Compiler.Parsetree;

namespace Nemerle.Compiler
{
  public enum TokenSeparator {
    | None 
    | Comma
    | Semicolon
/*
    public override ToString () : string {
      match (this) {
        | None => " "
        | Comma => ", "
        | Semicolon => "; "
      }
    }
*/    
  }

  class PreParserException : System.Exception { }

  /** Transforms stream of tokens from given LexerBase to token tree
      with matched brackets.
   */
  public class PreParser
  {
    lexer : LexerBase;
    mutable last_token : Token = null;

    mutable finished : bool = false;
    
    /** Parent stream is the stack of processed token nodes,
        which are already assigned to be in currently build sequence.
        For example:
          a; b; c (); d e _we_are_here_
        'a, b, c()', are alredy known to be in parent sequence,
        while 'd e' are in current temporary sequence, which might
        get added to parent_stream if separator (e.g. ';') occurs
     */
    mutable parent_stream : array [Token] = array (1000);
    mutable parent_pos : int;

    /** Currently builded stream of token nodes is an array of
        loose tokens, which have occured after last separator.
        It will probably form LooseGroup as an element of parent
        sequence or all elements will constitue parent
     */
    mutable current_stream : array [Token] = array (200);
    mutable current_pos : int;

    public this (lex : LexerBase) {
      lexer = lex;
    }

    /** Fetch next token (from one token buffer or lexer if it's empty */
    get_token () : Token {
      if (last_token != null) {
        def result = last_token;
        last_token = null;
        result;
      }
      else
        lexer.GetToken ()
    }

    /** Store token in our mini one token buffer */
    push_back (tok : Token) : void {
      assert (last_token == null);
      last_token = tok;
    }

    /** makes list of Tokens from specified subarray */
    static make_list (tokens : array [Token], start : int, end : int) : list [Token]
    {
      make_list (tokens, start, end, []);
    }

    /** makes list of Tokens from specified subarray and concatenate given one at end */
    static make_list (tokens : array [Token], start : int, end : int,
                      mutable initial : list [Token]) : list [Token]
    {
      for (mutable i = end - 1; i >= start; --i) 
        initial = tokens [i] :: initial;
      initial
    }

    static store (tokens : ref array [Token], idx : ref int, tok : Token) : void
    {
      tokens [idx] = tok;
      ++idx;
    }
    
    public static Dump (tok : Token, ident : string) : string {
      def (open, close, sepstr, elements) =
        match (tok) {
          | Token.RoundGroup (separator, elements) =>    // ( ... )
            ("(", ")", separator.ToString (), elements)
          | Token.BracesGroup (elements) =>
            ("{\n" + ident, "}", ";\n" + ident, elements)
          | Token.SquareGroup (elements) =>
            ("[", "]", ", ", elements)
          | Token.QuoteGroup (elements) =>
            ("<[\n", "]>", "; ", elements)
          | Token.LooseGroup (elements)   =>
            ("", "", " ", elements)

          | _ => ("", tok.ToString (), "", [])
        }
      def builder = System.Text.StringBuilder (open);
      foreach (e in elements) {
        _ = builder.Append (Dump (e, ident + "  ")).Append (sepstr);
      }
      builder.Append (close).ToString ()
    }

    /** Closes both currently created LooseGroup and parent group.
        Returns list of tokens composing parent group */
    finish_parent (parent_begin : int, current_begin : int) : list [Token] {
      def parent_group = 
        if (parent_begin == parent_pos)
          if (current_begin == current_pos)
            [] // case of `(` `)`
          else {
            // case of `(` a b `)`
            def current_group = make_list (current_stream, current_begin, current_pos);
            [Token.LooseGroup (current_group)]
          }
        else
          if (current_begin == current_pos)
            // case of `(` A `,` B `,` `)`
            make_list (parent_stream, parent_begin, parent_pos)
          else {
            // case of `(` A `,` B `,` c d `)`
            def current_group = make_list (current_stream, current_begin, current_pos);
            make_list (parent_stream, parent_begin, parent_pos,
                       [Token.LooseGroup (current_group)]);
          }
      parent_pos = parent_begin;
      current_pos = current_begin;
      parent_group
    }

    /** Closes currently created LooseGroup and adds it at the end of the
        parent group. After that we are ready to make another LooseGroup.

        It is called mainly when separator token occurs.
     */   
    finish_current (current_begin : int) : void {
      unless (current_begin == current_pos) {
        def loose_group = make_list (current_stream, current_begin, current_pos);
        store (ref parent_stream, ref parent_pos, Token.LooseGroup (loose_group));
        current_pos = current_begin;
      }
    }

    /** Handle standard situations when new bracket group is beginning
        or there is erronous situation. Any non bracket token is
        appended to current LooseGroup.

        Throws PreParserException when there is unmatched end bracket.
     */
    handle_default_token (current_begin : int, tok : Token) : void {
      match (tok) {
        | Token.BeginBrace =>
          def brace_group = parse_brace_group (tok.Location, parent_pos, current_pos);
          if (current_begin == current_pos)
            store (ref parent_stream, ref parent_pos, brace_group)
          else {
            store (ref current_stream, ref current_pos, brace_group);
            finish_current (current_begin);
          }

        | Token.BeginRound =>
          def round_group = parse_round_group (tok.Location, parent_pos, current_pos);
          store (ref current_stream, ref current_pos, round_group);

        | Token.BeginSquare =>
          def square_group = parse_square_group (tok.Location, parent_pos, current_pos);
          store (ref current_stream, ref current_pos, square_group);

        | Token.BeginQuote =>
          def quote_group = parse_quote_group (tok.Location, parent_pos, current_pos);
          store (ref current_stream, ref current_pos, quote_group);

        | Token.EndRound | Token.EndSquare | Token.EndQuote | Token.EndBrace =>
          push_back (tok);
          Message.error (tok.Location, "unexpected closing bracket: " + tok.ToString ());
          throw PreParserException ();

        | Token.EndOfFile =>
          Message.error (tok.Location, "unexpected end of file");
          throw PreParserException ();

        | _ => store (ref current_stream, ref current_pos, tok);
      }
    }
    
    parse_brace_group (loc : Location, parent_begin : int, current_begin : int) : Token.BracesGroup
    {
      def loop () {
        def tok = get_token ();
        match (tok) {
          // finish entire brace group
          | Token.EndBrace =>
            def brace_group = finish_parent (parent_begin, current_begin);
            Token.BracesGroup (loc + tok.Location, brace_group);

          // finish current loose group
          | Token.Semicolon => finish_current (current_begin); loop ()

          | Token.EndOfFile when parent_begin == 0 =>
            def brace_group = finish_parent (parent_begin, current_begin);
            finished = true;
            Token.BracesGroup (loc + tok.Location, brace_group);
            
          | _ => handle_default_token (current_begin, tok); loop ()
        }
      }
      try { loop () :> Token.BracesGroup }
      catch { _ : PreParserException =>
        def group = finish_parent (parent_begin, current_begin);
        Token.BracesGroup (loc, group) :> Token.BracesGroup;
      }
    }

    parse_round_group (loc : Location, parent_begin : int, current_begin : int) : Token.RoundGroup
    {
      mutable separator = TokenSeparator.None;
      
      def loop () {
        def tok = get_token ();
        match (tok) {
          // finish entire round group
          | Token.EndRound =>
            def round_group = finish_parent (parent_begin, current_begin);
            Token.RoundGroup (loc + tok.Location, separator, round_group);

          // finish current loose group
          | Token.Comma =>
            match (separator) {
              | TokenSeparator.None => separator = TokenSeparator.Comma;
              | TokenSeparator.Comma => finish_current (current_begin);
              | TokenSeparator.Semicolon =>
                Message.error (tok.Location, "inconsistent `,' separator (expected `;')");
                finish_current (current_begin); 
            }
            loop ()

          | Token.Semicolon =>
            match (separator) {
              | TokenSeparator.None => separator = TokenSeparator.Semicolon;
              | TokenSeparator.Semicolon => finish_current (current_begin);
              | TokenSeparator.Comma =>
                Message.error (tok.Location, "inconsistent `;' separator (expected `,')");
                finish_current (current_begin); 
            }
            loop ()

          | _ => handle_default_token (current_begin, tok); loop ()
        }
      }
      try { loop () :> Token.RoundGroup }
      catch { _ : PreParserException =>
        def group = finish_parent (parent_begin, current_begin);
        Token.RoundGroup (loc, separator, group) :> Token.RoundGroup;
      }
    }

    parse_square_group (loc : Location, parent_begin : int, current_begin : int) : Token.SquareGroup
    {
      def loop () {
        def tok = get_token ();
        match (tok) {
          // finish entire brace group
          | Token.EndSquare =>
            def group = finish_parent (parent_begin, current_begin);
            Token.SquareGroup (loc + tok.Location, group);

          // finish current loose group
          | Token.Comma => finish_current (current_begin); loop ()

          | _ => handle_default_token (current_begin, tok); loop ()
        }
      }
      try { loop () :> Token.SquareGroup }
      catch { _ : PreParserException =>
        def group = finish_parent (parent_begin, current_begin);
        Token.SquareGroup (loc, group) :> Token.SquareGroup;
      }
    }

    parse_quote_group (loc : Location, parent_begin : int, current_begin : int) : Token.QuoteGroup
    {
      def loop () {
        def tok = get_token ();
        match (tok) {
          // finish entire brace group
          | Token.EndQuote =>
            def group = finish_parent (parent_begin, current_begin);
            Token.QuoteGroup (loc + tok.Location, group) :> Token.QuoteGroup;

          // finish current loose group
          | Token.Semicolon => finish_current (current_begin); loop ()

          | _ => handle_default_token (current_begin, tok); loop ()
        }
      }
      try { loop () :> Token.QuoteGroup }
      catch { _ : PreParserException =>
        def group = finish_parent (parent_begin, current_begin);
        Token.QuoteGroup (loc, group) :> Token.QuoteGroup;
      }
    }
    
    public PreParse () : list [Token] {
      def top = parse_brace_group (Location.Default, 0, 0);
      unless (finished) Message.error (lexer.Location, "expected end of file, encountered finishing token");
      top.Elements
    }
  }


  public class MainParser
  {
    mutable env : GlobalEnv;
    mutable stream : list [Token];

    public this (env : GlobalEnv) {
      this.env = env;
    }
    
    public Parse (_toptokens : list [Token]) : void
    {
      MacroRegistry.RemoveSyntaxExtensions ();
    }

    public ParseExpr (expr : string) : PExpr
    {
      def lexer = LexerString (expr, Location_stack.top ());
      MacroRegistry.RemoveSyntaxExtensions ();
      def preparser = PreParser (lexer);
      def _tokens = preparser.PreParse ();
      null
    }

    Error (tok : Token, msg : string) : void {
      Message.error (tok.Location, "parse error near " + tok.ToString (true) + ": " + msg)
    }

    get_token () : Token {
      match (stream) {
        | x :: xs => stream = xs; x
        | _ => Util.ice ("there is no token to take")
      }
    } 
    
    get_splicable_id () : Splicable {
      def tok = get_token ();
      def loc = tok.Location;
      match (stream) {
        | Token.Identifier (n) :: rest =>
          stream = rest;
          Splicable.Name (loc, Name ([n]))
          
        | Token.Operator ("$", _) :: (Token.Identifier (id) as second) :: rest =>
          stream = rest;
          Splicable.Expression (loc + second.Location,
                                PExpr.Ref (second.Location, Name ([id])))

        | Token.Operator ("$", _) :: (Token.RoundGroup (_, [expr]) as second) :: rest =>
          def loc = loc + second.Location;
          def expr = parse_expr (expr);
          def result = 
            match (expr) {
              | PExpr.TypeEnforcement =>
                Splicable.Expression (loc, PExpr.Spliced (second.Location, expr))

              | x => Splicable.Expression (loc, x); 
            }
          stream = rest;
          result

        | Token.Operator ("$", _) :: (Token.Keyword ("_") as second) :: rest =>
          stream = rest;
          Splicable.Expression (loc + second.Location, PExpr.Wildcard (second.Location))

        | Token.Keyword ("_") :: rest =>
          stream = rest;
          Splicable.Name (loc, Name ([Util.tmpname ("u")]))
          
        | _ =>
          Message.error (loc, "expecting identifier");
          Splicable.Name (Name (["error"]))
      }
    }
    
    GetQid () : list [string]
    {
      match (stream) {
        | Token.Identifier (x) :: Token.Operator (".", _) :: rest =>
          stream = rest;
          x :: GetQid ()

        | Token.Identifier (x) :: rest => stream = rest; [x]

        | t :: _ => Error (t, "expected qualified identifier"); []
        | [] => Message.error ("expected qualified identifier"); []
      }
    }

    expect_empty (msg : string) : void {
      expect_empty (stream, msg);
    }

    expect_empty (toks : list [Token], msg : string) : void {
      match (toks) {
        | [] => ()
        | x :: _ => Error (x, "unexpected tokens after " + msg)
      }
    }

    expect_operator (op : string) : void {
      match (stream) {
        | Token.Operator (o, _) :: rest when o == op => stream = rest;
        | x :: _ => Error (x, $"expecting operator `$(op)'")
        | [] => ()
      }
    }
    
    /** This function parses top level group updating global environment
        when entering into new namespace, using directive, class, etc.
     */
    ParseTopLevel (tok : Token) : void
    {
      | Token.LooseGroup (tokens) =>
        match (tokens) {
          | Token.Keyword ("namespace") :: name_and_body =>
            stream = name_and_body;
            // get name of namespace
            def name = GetQid ();

            // body of namespace must be { ... } 
            match (stream) {
              | Token.BracesGroup (children) :: empty =>
                def begin_env = env;
                // update current environment
                env = env.EnterIntoNamespace (name);
                // parse elements of namespace with new environment enabled
                foreach (child in children) ParseTopLevel (child);
                // bring env from outside of namespace
                env = begin_env;

                expect_empty (empty, "namespace body")

              | _ => Message.error (tok.Location, "expected `{ }' as body of namespace")
            }

          | Token.Keyword ("using") :: what =>
            stream = what;
            def name = GetQid ();

            match (stream) {
              // using ID = Q.ID; 
              | Token.Operator ("=", _) :: body =>
                stream = body;
                def fullname = GetQid ();
                match (name) {
                  | [name] => env = env.AddNamespaceAlias (name, fullname, tok.Location);
                  | _ => Error (tok, "using alias must be simple name without dots")
                }
                
              // using Q.ID;
              | [] => env = env.AddOpenNamespace (name, tok.Location);
              | _ => expect_empty ("using specification")
            }

          // assembly attribute
          | Token.SquareGroup (Token.LooseGroup (Token.Identifier ("assembly") ::
                                                 Token.Operator (":", _) :: body1) :: body2) :: body3 =>
            stream = body1;
            AttributeCompiler.AddAssemblyAttribute (env, parse_expr ());
            
            foreach (tok in body2)
              AttributeCompiler.AddAssemblyAttribute (env, parse_expr (tok));

            // [assembly: .. ] [assembly : ..] ..
            unless (body3 matches []) ParseTopLevel (Token.LooseGroup (body3))

          | _ :: _ =>
            def _tydecl = ParseType (tokens);
            ()
//            handle_type (None (), None (), env, d);            

          | _ => Error (tok, "empty declaration")
        }

      | _ => Util.ice ("wrong token group at toplevel")
    }

    /** Parses toplevel type (like class, interface, etc.)
     */
    ParseType (toks : list [Token]) : TopDeclaration
    {
      stream = toks;
      mutable customs_token = get_customs ();
      def customs = take_attributes_out (ref customs_token, System.AttributeTargets.Class, true);      
      def mods = Modifiers (get_modifiers (), customs);
     
      def res =
        match (stream) {
          | (Token.Keyword (key) as firsttok) :: header =>
            stream = header;            
            match (key) {
              | "type" | "class" | "struct" | "module" | "interface" | "enum"
              | "variant" =>
                /// first get name of this declartion
                def name = get_splicable_id ();
                // now generic type parameters
                def tyvars = parse_tyvars ();                  
                def t_extends =
                  match (stream) {
                    | Token.Operator (":", _) :: Token.Operator (".", _)
                      :: Token.Operator (".", _) :: rest =>
                      stream = rest;
                      match (get_splicable_id ()) {
                        | Splicable.Expression (e) =>
                          [PExpr.Ellipsis (PExpr.Spliced (e))]

                        | x => Message.error (x.Location, "expected spliced expression"); []
                      }
                    | Token.Operator (":", _) :: rest =>
                      stream = rest;
                      // parse , separated sequence as one expression
                      match (parse_expr ()) {
                        | PExpr.Call (PExpr.Ref ({ idl = [","] }), parms) => parms
                        | e => [e]
                      }
                    | _ => []
                  };
                // where constraints for specified generic type parameters
                def typarms = parse_where_constraints (tyvars);

                def members =
                  if (key == "type") []
                  else
                    match (stream) {
                      | [Token.BracesGroup (children)] =>
                        List.Map (children, parse_class_member);
                      | x :: _ => Error (x, "expecting type body"); []
                      | [] => Error (firsttok, "expecting type with body"); []
                    }

                def td =
                  match (key) {
                    | "class" => TopDeclaration.Class (typarms, t_extends, members)
                    | "struct" =>
                      mods.mods |= NemerleAttributes.Struct;
                      TopDeclaration.Class (typarms, t_extends, members)

                    | "module" =>
                      mods.mods |= NemerleAttributes.Static;
                      TopDeclaration.Class (typarms, t_extends, members)

                    | "type" =>
                      expect_operator ("=");
                      def t = parse_expr ();
                      expect_empty ("type alias declaration");
                      TopDeclaration.Alias (typarms, t)

                    | "interface" => TopDeclaration.Interface (typarms, t_extends, members)
                    | "variant" => TopDeclaration.Variant (typarms, t_extends, members)
                    | "enum" => TopDeclaration.Enum (t_extends, members)

                    | _ => Util.ice ()
                  };
                td.name = name;
                td.loc = firsttok.Location + name.Location;                
                td

              | "delegate" =>
                def (h, _) = parse_fun_header (false, false, false, None ());
                def td = TopDeclaration.Delegate (h);
                expect_empty ("delegate declaraion");
                td.loc = firsttok.Location + h.Location;
                td

              | "macro" => 
                def (header, _) = parse_fun_header (true, false, false, None ());
                def synt = 
                  match (stream) {
                    | Token.Keyword ("syntax") :: Token.RoundGroup (TokenSeparator.Comma, syns) :: body =>
                      // FIXME: use parse_expr directly
                      def exprs = List.Map (syns, fun (x) { parse_expr (x) });
                      stream = body;
                      exprs
                    | _ => []
                  }
                def expr = parse_block ([]);
                def res = TopDeclaration.Macro (header, synt, expr);
                res.loc = firsttok.Location + header.Location;
                res

              | _ => Error (firsttok, "expecting type declaration"); TopDeclaration.Delegate (null);
            }
          | x :: _ => Error (x, "expecting type declaration"); TopDeclaration.Delegate (null);
          | [] => Message.fatal_error ("expecting type declaration")
        };
      res.modifiers = mods;                
      res
    }

    get_customs () : list [Token.SquareGroup] {
      def loop (acc) {
        match (stream) {
          | (Token.SquareGroup as gr) :: rest =>
            stream = rest;
            loop (gr :: acc)

          | _ => List.Rev (acc)
        }
      }
      loop ([])
    }

    get_modifiers () : NemerleAttributes
    {
      def loop (acc) {
        match (stream) {
          | (Token.Keyword (key) as tok) :: rest =>
            def add_and_loop (attr : NemerleAttributes) {
              when (attr %&& acc)
                Message.error (tok.Location, "attribute '" + key + "' specified more than once");
              stream = rest;
              loop (attr %| acc)
            }
            match (key) {
              | "mutable" => add_and_loop (NemerleAttributes.Mutable)
              | "public" => add_and_loop (NemerleAttributes.Public)
              | "private" => add_and_loop (NemerleAttributes.Private)
              | "static" => add_and_loop (NemerleAttributes.Static)
              | "new" => add_and_loop (NemerleAttributes.New)
              | "protected" => add_and_loop (NemerleAttributes.Protected)
              | "internal" => add_and_loop (NemerleAttributes.Internal)
              | "abstract" => add_and_loop (NemerleAttributes.Abstract)
              | "sealed" => add_and_loop (NemerleAttributes.Sealed)
              | "override" => add_and_loop (NemerleAttributes.Override)
              | "virtual" => add_and_loop (NemerleAttributes.Virtual)
              | "volatile" => add_and_loop (NemerleAttributes.Volatile)
              | "partial" => add_and_loop (NemerleAttributes.Partial)
              | _ => acc
            }
          | _ =>
            // perform some sanity checks on the declared attributes
            when (acc %&& NemerleAttributes.Virtual &&
                  acc %&& NemerleAttributes.Override)
              Message.warning ("the `virtual' attribute is redundant, `override' implies `virtual'");
            acc
        }
      }
      loop (NemerleAttributes.None)
    }
   
    [Nemerle.NotImplemented]
    parse_expr () : PExpr;

    [Nemerle.NotImplemented]
    parse_expr (group_token : Token) : PExpr;

    [Nemerle.NotImplemented]
    parse_tyvars () : list [Fun_parm];

    [Nemerle.NotImplemented]    
    parse_where_constraints (tyvars : list [Fun_parm]) : Typarms;

    [Nemerle.NotImplemented]
    parse_class_member (tok : Token) : ClassMember;

    [Nemerle.NotImplemented]    
    parse_fun_header (allow_inference : bool, 
                    allow_ctor : bool,
                    is_lambda : bool, 
                    knownname : option [Splicable]) : Fun_header * bool;

    [Nemerle.NotImplemented]
    parse_block (parms : list [Fun_parm]) : PExpr;                    
                    
    take_attributes_out (from : ref list [Token.SquareGroup], 
                         what : System.AttributeTargets,
                         comply_on_other : bool) : list [PExpr]
    {
      mutable result = [];      
      from = List.Filter (from, fun (x : Token.SquareGroup) {
        mutable removed = false;
        match (x.Elements) {
          | Token.LooseGroup (first :: Token.Operator (":", _) :: body1) :: body2
          | Token.LooseGroup ((first :: _) as body1) :: body2 =>
            def target = 
              match (first) {
                | Token.Identifier ("assembly") => System.AttributeTargets.Assembly
                | Token.Identifier ("field") => System.AttributeTargets.Field
                | Token.Keyword ("event") => System.AttributeTargets.Event
                | Token.Identifier ("method") => System.AttributeTargets.Method
                | Token.Keyword ("module") => System.AttributeTargets.Module
                | Token.Identifier ("param") => System.AttributeTargets.Parameter
                | Token.Identifier ("property") => System.AttributeTargets.Property
                | Token.Identifier ("return") => System.AttributeTargets.ReturnValue
                | Token.Keyword ("type") => System.AttributeTargets.Class
                // a little hack, because we cannot introduce default value in patterns
                // if there were no `:', then accept any identifier
                | Token.Identifier when List.Head (body1) == (first : object) =>
                  System.AttributeTargets.All
                | _ =>
                  Error (first, "wrong attribute target");
                  System.AttributeTargets.All
              }
            if (target %&& what || target == System.AttributeTargets.All) {
              removed = true;
              // parse body of first element in this [ , , ]
              stream = body1;
              result = parse_expr () :: result;
              // parse all remaining in current bracket group [ , , , ]
              foreach (tok in body2)
                result = parse_expr (tok) :: result;
            }
            else  
              when (comply_on_other)
                Error (first, $"unexpected attribute target `$(first)'");
              
          | [] | Token.LooseGroup :: _ => Error (x, "empty cutom attribute")
          | _ => Util.ice ("broken brackets in attribute")
        }
        removed;
      });
      List.Rev (result);
    }

  } // end MainParser
} // end namespace

 

#endif NEWPARSER
