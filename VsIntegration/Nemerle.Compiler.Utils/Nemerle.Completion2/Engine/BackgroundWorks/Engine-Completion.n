using System;
using System.Collections.Generic;
using System.Diagnostics;
using System.Linq;
using System.Threading;
using System.Reflection.BindingFlags;

using Nemerle.Assertions;
using Nemerle.Collections;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Compiler.Utils;
using Nemerle.Compiler.Utils.Async;
using Nemerle.Imperative;
using Nemerle.Surround;
using Nemerle.Utility;

using MethodBuilderEx = Nemerle.Completion2.Factories.IntelliSenseModeMethodBuilder;
using SCG             = System.Collections.Generic;

namespace Nemerle.Completion2
{
	public partial class Engine
	{
    public Completion(source : ISource, line : int, column : int) : array[CompletionElem]
    {
      def request = BeginCompletion(source, line, column);
      _ = request.AsyncWaitHandle.WaitOne();
      request.CompletionElems
    }

    public BeginCompletion(source : ISource, line : int, column : int) : CompletionAsyncRequest
    {
      def request = CompletionAsyncRequest(this, source, line, column);
      AsyncWorker.AddWork(request);
      request
    }
	
    internal Completion(request : AsyncRequest) : void
    {
      def request = request :> CompletionAsyncRequest;
      AsyncWorker.CheckCurrentThreadIsTheAsyncWorker();

      surroundwith (currentAsyncRequest)
      try
      {
        when (IsDefaultEngine)
        {
          //TODO: Реализовать выдачу всех токенов из открытых файлов для работы без проекта
          return;
        }
        
        request.CompletionElems = CompletionImpl(request.Source, request.Line, request.Column);
        request.MarkAsCompleted();
      }
      catch
      { | e is CompilationAbortedException =>
          def msg = $"The Completion operation aborted at: $(e.Message)";
          throw CompilationAbortedException(msg, e);
      }
    }
  }
}
