(*
 * Copyright (c) 2003 The University of Wroclaw.
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions
 * are met:
 *    1. Redistributions of source code must retain the above copyright
 *       notice, this list of conditions and the following disclaimer.
 *    2. Redistributions in binary form must reproduce the above copyright
 *       notice, this list of conditions and the following disclaimer in the
 *       documentation and/or other materials provided with the distribution.
 *    3. The name of the University may not be used to endorse or promote
 *       products derived from this software without specific prior
 *       written permission.
 * 
 * THIS SOFTWARE IS PROVIDED BY THE UNIVERSITY ``AS IS'' AND ANY EXPRESS OR
 * IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES
 * OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN
 * NO EVENT SHALL THE UNIVERSITY BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
 * SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED
 * TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR
 * PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF
 * LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING
 * NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS
 * SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 *)

open Nemerle.Collections;

namespace Nemerle.Compiler {
module CGopt {

  (**
   * Check if given method tail-calls anywhere.
   *)
  has_tail_call (meth : CG_method) : bool {
    def walk (ex : CG_expr) : bool {
      match (ex) {
        | CE_call => true
        | CE_let (_, e) => walk (e)
        | CE_if (_, e1, e2) => walk (e1) || walk (e2)
        | CE_sequence ([]) => false
        | CE_sequence (l) => walk (List.last (l))
        | _ => false
      }
    };

    walk (meth.body)
  }

  (**
   * Mark tail calls with CE_tail_call ().
   *)
  tail_call_optimize (meth : CG_method) : void {
    def replace_tailcalls (ex : CG_expr) : CG_expr {
      def r =
        match (ex) {
          | CE_call (CE_global_ref (mb), parms) 
              when Flags.self_tail_call_opt && 
                   (mb : object) == (meth.method_builder : object)
          | CE_call (CE_method_ref (CE_this, mb'), parms) 
              when Flags.self_tail_call_opt &&
                   (mb' : object) == (meth.method_builder : object) =>
            CE_self_tail_call (parms)
            
          | CE_call (ex, parms) => 
            CE_tail_call (ex, parms)
            
          | CE_let (v, e) => 
            CE_let (v, replace_tailcalls (e))
            
          | CE_if (c, e1, e2) => 
            CE_if (c, replace_tailcalls (e1), replace_tailcalls (e2))
            
          | CE_sequence (l) =>
            def loop (acc : list (CG_expr), l : list (CG_expr)) : list (CG_expr) {
              match (l) {
                | [x] => List.rev (replace_tailcalls (x) :: acc)
                | x :: xs => loop (x :: acc, xs)
                | [] => []
              }
            };
            CE_sequence (loop ([], l))

          | _ => ex
        };
      r.loc <- ex.loc;
      r.ty <- ex.ty;
      r
    };

    when (has_tail_call (meth)) {
      meth.body <- replace_tailcalls (meth.body)
    }
  }
  
  (**
   * Do some optimization on method body.
   *
   * Method is updated in-place.
   *)
  internal Optimize (meth : CG_method) : void {
    when (Flags.tail_call_opt)
      tail_call_optimize (meth);
  }
  
}
}
