using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Assertions;

using System;
using System.Collections.Generic;
using System.Linq;

namespace LRPEGCC
{
  [Record]
  public variant Transition
  {
    public From : int;
    public To : int;
    | Symbol { Chars : RangeSet }
    | End
    | Epsilon
    public override ToString() : string
    {
      match (this)
      {
        | Symbol(chars) => $"Symbol($From $To [$chars])"
        | End           => $"End($From $To)"
        | Epsilon       => $"Epsilon($From $To)"
      }
    }
  }
  public class FSM
  {
    [Accessor]
    private _stateCount : int;
    [Accessor]
    private _transitions : list[Transition];
    [Accessor]
    private _startState : int;
    [Accessor]
    private _okState : int;
    [Accessor]
    private _failState : int;

    public this()
    {
      _stateCount  = 3;
      _transitions = [];
      _startState  = 0;
      _okState     = 1;
      _failState   = 2;
    }

    private this(stateCount : int, transitions : list[Transition], startState : int, okState : int, failState : int)
    {
      _stateCount  = stateCount;
      _transitions = transitions;
      _startState  = startState;
      _okState     = okState;
      _failState   = failState;
    }

    public NewState() : FSM * int
    {
      def fsm = FSM(StateCount + 1, Transitions, StartState, OkState, FailState);
      (fsm, StateCount);
    }

    public NewState(state : int) : FSM
    {
      if (state < StateCount)
        this;
      else
        FSM(state + 1, Transitions, StartState, OkState, FailState);
    }

    public NewStartState() : FSM * int
    {
      def fsm = FSM(StateCount + 1, Transitions, StateCount, OkState, FailState);
      (fsm, StateCount);
    }

    public NewOkState() : FSM * int
    {
      def fsm = FSM(StateCount + 1, Transitions, StartState, StateCount, FailState);
      (fsm, StateCount);
    }

    public NewFailState() : FSM * int
    {
      def fsm = FSM(StateCount + 1, Transitions, StartState, OkState, StateCount);
      (fsm, StateCount);
    }

    public SetStartState(state : int) : FSM
    {
      FSM(StateCount, Transitions, state, OkState, FailState);
    }

    public SetOkState(state : int) : FSM
    {
      FSM(StateCount, Transitions, StartState, state, FailState);
    }

    public SetFailState(state : int) : FSM
    {
      FSM(StateCount, Transitions, StartState, OkState, state);
    }

    public SetTransitions(transitions : list[Transition]) : FSM
    {
      FSM(StateCount, transitions, StartState, OkState, FailState);
    }

    public AddTransition(t : Transition) : FSM
      requires  0 <= t.To && t.To < _stateCount && 0 <= t.From && t.From < _stateCount
      otherwise throw ArgumentException()
    {
      FSM(StateCount, t :: Transitions, StartState, OkState, FailState);
    }

    public IncludeFSM(fsm : FSM) : FSM * int * int * int
    {
      def transitions = fsm.Transitions.Fold(_transitions, (transition, transitions) =>
      {
        def from = transition.From + StateCount;
        def to = transition.To + StateCount;
        def transition = match (transition : Transition)
        {
        | Symbol(chars) => Transition.Symbol(from, to, chars);
        | End           => Transition.End(from, to);
        | Epsilon       => Transition.Epsilon(from, to);
        };
        transition :: transitions;
      });
      def newFsm = FSM(StateCount + fsm.StateCount, transitions, StartState, OkState, FailState);
      (newFsm, fsm.StartState + StateCount, fsm.OkState + StateCount, fsm.FailState + StateCount)
    }
  }
}
