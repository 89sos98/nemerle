using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

using Nemerle.ComputationExpressions;
using Nemerle.ComputationExpressions.Async;

namespace Test
{
  public class AsyncTest
  {
    public static DoAll[A](async : Async[A]) : A
    {
      AsyncJob(SystemThreadPoolExecutionContext.Instance, JobState.Ready).ExecAll(async);
    }

    [TestCaseAttribute(<#
20
20
20
121
121 20
20 123
20 20
#>)]
    public static Test1(stream : IO.TextWriter) : void
    {
      def fn(n)
      {
        comp async
        {
          if (n < 20)
            returncomp fn(n + 1);
          else
            return n;
        }
      }
      def f(n1, n2)
      {
        comp async
        {
          defcomp n1 = fn(n1);
          defcomp n2 = fn(n2);
          return $"$n1 $n2";
        }
      }
      stream.WriteLine(DoAll(fn(1)));
      stream.WriteLine(DoAll(fn(10)));
      stream.WriteLine(DoAll(fn(15)));
      stream.WriteLine(DoAll(fn(121)));
      stream.WriteLine(DoAll(f(121, 1)));
      stream.WriteLine(DoAll(f(11, 123)));
      stream.WriteLine(DoAll(f(11, 4)));
    }

    [TestCaseAttribute(<#
243
270
297
135
121
#>)]
    public static Test2(stream : IO.TextWriter) : void
    {
      def fn(mutable n)
      {
        comp async
        {
          while (n < 100)
          {
            when (n % 3 == 0)
            {
              callcomp Async.SwitchTo(SystemThreadPoolExecutionContext.Instance);
              callcomp Async.Yield();
            }
            n *= 3;
          }
          return n;
        }
      }
      stream.WriteLine(DoAll(fn(1)));
      stream.WriteLine(DoAll(fn(10)));
      stream.WriteLine(DoAll(fn(11)));
      stream.WriteLine(DoAll(fn(15)));
      stream.WriteLine(DoAll(fn(121)));
    }
  }
}
