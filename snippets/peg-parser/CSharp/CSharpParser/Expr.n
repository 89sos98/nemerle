using System;
using SCG = System.Collections.Generic;

using Nemerle.Assertions;
using Nemerle.Utility;
using Nemerle.Peg;

namespace CSharpParser
{
  [Record]
  public variant Expr : Located
  {
    | Alias {
      id : Identifier;
    }
    | Ref {
      id : Identifier;
    }
    | Member {
      expr : Expr;
      member : Expr;
    }
    | PointerMember {
      expr : Expr;
      member : Expr;
    }
    | TypeArguments {
      expr : Expr;
      args : list[TypeRef];
    }
    | Literal {
      literal : CSharpParser.Literal;
    }
    | This
    | Base
    | Predefined {
      predefinedType : TypeRef;
    }
    | TypeOf {
      typeRef : TypeRef;
    }
    | Checked {
      expr : Expr;
      is_checked : bool;
    }
    | DefaultValue {
      typeRef : TypeRef;
    }
    | SizeOf {
      typeRef : TypeRef;
    }
    | Function {
      formalParameter : list[FormalParameter];
      automaticParams : bool;
      is_lambda : bool;
      body : Statement;
    }
    | Call {
      expr : Expr;
      arguments : list[FunctionArgument];
    }
    | Indexer {
      expr : Expr;
      arguments : list[FunctionArgument];
    }
    | UnaryOperator  {
      expr : Expr;
      op : Identifier;
      is_prefix : bool;
    }
    | BinaryOperator {
      left : Expr;
      right : Expr;
      op : Identifier;
    }
    | ConditionalOperator {
      cond : Expr;
      trueExpr : Expr;
      falseExpr : Expr;
    }
    | CastOperator {
      expr : Expr;
      toType : TypeRef;
    }
    | Assign {
      to : Expr;
      value : Expr;
      op : Identifier;
    }
    | NewObject {
      objType : TypeRef;
      args : list[FunctionArgument];
      init : VariableInitializer;
    }
    | NewArray {
      elementType : TypeRef;
      args : list[Expr];
      rank : int;
      init : VariableInitializer;
    }
    | NewAnonymous {
      init : VariableInitializer;
    }
    | Query {
        
      query : list[QueryExpr];
    }
    
    public override ToString() : string
    {
      match (this)
      {
        | Ref(id)
        | Alias(id) => id.ToString()
        | Member(expr, member) => $"$expr.$member"
        | PointerMember(expr, member) => $"$expr->$member"
        | TypeArguments(expr, args) => $"$expr<..$args>"
        | Literal(literal) => literal.ToString()
        | This => "this"
        | Base => "base"
        | Predefined(predefinedType) => predefinedType.ToString()
        | TypeOf(typeRef) => $"typeof($typeRef)"
        | Checked(expr, is_checked) => if (is_checked) $"checked $expr" else $"unchecked $expr"
        | DefaultValue(typeRef) => $"default($typeRef)"
        | SizeOf(typeRef) => $"sizeof($typeRef)"
        | Function(formalParameter, automaticParams, is_lambda, body) =>
          if (is_lambda) $"(..$formalParameter) => body" 
          else "delegate" + (if (automaticParams) "" else $"(..$formalParameter)") + $"{ $body }"
        | Call(expr, arguments) => $"$expr(..$arguments)"
        | Indexer(expr, arguments) => $"$expr[..$arguments]"
        | UnaryOperator(expr, op, is_prefix) => if (is_prefix) $"$op$expr" else $"$expr$op"
        | BinaryOperator(left, right, op) => $"$left $op $right"
        | ConditionalOperator(cond, trueExpr, falseExpr) => $"$cond ? $trueExpr : $falseExpr"
        | CastOperator(expr, toType) => $"($toType)($expr)"
        | Assign(to, value, op) => $"$to $op $value"
        | NewObject(objType, args, init) => $"new $objType(..$args) $init"
        | NewArray(elementType, args, rank, init) => $"new $elementType[..$args][$rank]$init"
        | NewAnonymous(init) => $"new$init"
        | Query(query) => $<#..$(query; " ")#>
      }
    }
  }
}