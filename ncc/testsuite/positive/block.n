using Nemerle.IO;

module M {
  foo (x : int) : int
  {
    ret : {
      when (x < 17)
        ret (42);
      when (x < 33)
        ret (4242);
      x * 33
    }
  }
  
  bar (x : int) : int
  {
    brk : {
      for (mutable tmp = x; tmp > 0; tmp--) {
        when (tmp == 42)
          brk(42);
      }
      x
    }
  }
  
  bar2 (x : int) : int
  {
    brk : { // W: bad style: each branch in this block ends with a break
      for (mutable tmp = x; tmp > 0; tmp--) {
        when (tmp == 42)
          brk(42);
      }
      brk(x)
    }
  }

  foobar (x : int) : string
  {
    mutable res = "";
    brk : {
      for (mutable i = 0; i < x; ++i) {
        when (i == 13)
          brk ();
        res += "x";
      }
    }
    res
  }
  
  quxx () : void
  {
    mutable x = 3;
    x = throw System.Exception (); // W: bad style
    while (true) {} // W: skipped
  }
  
  Main () : void
  {
    assert (foo (3) == 42);
    assert (foo (20) == 4242);
    assert (foo (333) == 33 * 333);
    assert (bar (3) == 3);
    assert (bar (50) == 42);
    assert (bar2 (3) == 3);
    assert (bar2 (50) == 42);
    assert (foobar (3) == "xxx");
    assert (foobar (33) == "xxxxxxxxxxxxx");
  }
}

/*
BEGIN-OUTPUT
END-OUTPUT
*/
