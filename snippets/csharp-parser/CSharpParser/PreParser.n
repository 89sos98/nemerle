using System;
using SCG = System.Collections.Generic;

using Nemerle.Assertions;
using Nemerle.Collections;
using Nemerle.Utility;
using Nemerle.Peg;

namespace CSharpParser
{
  [PegGrammar(
    start,
    grammar
    {
      #region Primitives

      any = ['\u0000'..'\uFFFF'];

      newLine = "\n"
              / "\r\n"
              / "\r"
              / "\u2028"      /*  line separator       */
              / "\u2029";     /*  paragraph separator  */

      whitespace = [Zs]
                / '\t'
                / '\v'        /*  vertial tab          */
                / '\f';       /*  form feed            */

      s : void = whitespace*;
      S : void = !identifierPartCharacters s;

      #endregion

      #region Strings

      delimitedStringLiteral    = "\"" delimitedStringCharacter* "\"";
      delimitedStringCharacter  = "\\\""
                                / !(newLine / "\"") any;

      verbatimStringLiteral     = "@\"" verbatimStringCharacter* "\"";
      verbatimStringCharacter   = "\"\""
                                / !"\"" any;

      stringLiteral = delimitedStringLiteral / verbatimStringLiteral;

      #endregion

      #region Comments

      singleLineComment         = "//" (!newLine any)*;
      delimitedComment          = "/*" (!"*/" any)* "*/";
      comment : PreParseElement = singleLineComment / delimitedComment;

      #endregion

      #region Expressions
      
      letterCharacter       = [Lu, Ll, Lt, Lm, Lo, Nl];
      combiningCharacter    = [Mn, Mc];
      decimalDigitCharacter = [Nd];
      connectingCharacter   = [Pc];
      formattingCharacter   = [Cf];

      identifierStartCharacter  = letterCharacter / "_";
      identifierPartCharacters  = letterCharacter / decimalDigitCharacter / connectingCharacter / combiningCharacter / formattingCharacter;
      identifierBody            = identifierStartCharacter identifierPartCharacters*;

      symbol              : DirectiveExpression = identifierBody s;
      boolLiteral         : DirectiveExpression = ("true" / "false")S;
      braceExpression     : DirectiveExpression = "("s expression ")"s;
      primaryExpression   : DirectiveExpression = (boolLiteral / symbol / braceExpression);
      unaryExpression     : DirectiveExpression = ("!" s)* primaryExpression;
      equalityExpression  : DirectiveExpression = unaryExpression     (("==" / "!=")s unaryExpression   )*;
      andExpression       : DirectiveExpression = equalityExpression  ("&&"s          equalityExpression)*;
      orExpression        : DirectiveExpression = andExpression       ("||"s          andExpression     )*;
      expression          : DirectiveExpression = orExpression s;
      
      message     : string = (!(newLine / comment) any)*;      
      
      fileName    : string = "\"" (!("\"" / newLine) any)* "\"";
      
      #endregion

      #region Directives

      ifDirective           : Directive = "if"        S expression    &(newLine / comment / !any);
      elifDirective         : Directive = "elif"      S expression    &(newLine / comment / !any);
      elseDirective         : Directive = "else"      s               &(newLine / comment / !any);
      endifDirective        : Directive = "endif"     s               &(newLine / comment / !any);
      defineDirective       : Directive = "define"    S expression    &(newLine / comment / !any);
      undefDirective        : Directive = "undef"     S expression    &(newLine / comment / !any);
      lineNumberDirective   : Directive = "line"      S ['0'..'9']+ s fileName? s &(newLine / comment / !any);
      lineDefaultDirective  : Directive = "line"      S "default"   s &(newLine / comment / !any);
      errorDirective        : Directive = "error"     (S message)?    &(newLine / comment / !any);
      warningDirective      : Directive = "warning"   (S message)?    &(newLine / comment / !any);
      regionDirective       : Directive = "region"    (S message)?    &(newLine / comment / !any);
      endregionDirective    : Directive = "endregion" (S message)?    &(newLine / comment / !any);
      invalidDirective      : Directive = message                     &(newLine / comment / !any);

      directive             : PreParseElement = s"#"s (ifDirective
                                                      / elifDirective
                                                      / elseDirective
                                                      / endifDirective
                                                      / defineDirective
                                                      / undefDirective
                                                      / lineNumberDirective
                                                      / lineDefaultDirective
                                                      / errorDirective
                                                      / warningDirective
                                                      / regionDirective
                                                      / endregionDirective
                                                      / invalidDirective);

      //[DisableLocationTracking]
      directiveFromNewLine  : PreParseElement = newLine directive;

      #endregion

      #region Top rules

      rawText = (!(comment / directiveFromNewLine / stringLiteral) any)+ / stringLiteral;

      //[DisableLocationTracking]
      element : PreParseElement         = rawText* (directiveFromNewLine / comment);

      start   : PreparedCompilationUnit = directive? element* rawText* !any;

      #endregion
    }
  )]
  public partial class PreParser
  {
    //comment           : PreParseElement = singleLineComment / delimitedComment;
    comment(c : NToken) : PreParseElement
    {
      PreParseElement.Comment(GetText(c))
    }
    
    //symbol              : DirectiveExpression = identifierBody s;
    symbol(id : NToken) : DirectiveExpression
    {
      DirectiveExpression.Symbol(GetText(id))
    }
    
    //boolLiteral         : DirectiveExpression = ("true" / "false")S;
    boolLiteral(lit : NToken) : DirectiveExpression
    {
      match(GetText(lit))
      {
        | "true" => DirectiveExpression.Boolean(true)
        | "false" => DirectiveExpression.Boolean(false)
        | _ => throw ArgumentOutOfRangeException("lit");
      }
    }
    
    //braceExpression     : DirectiveExpression = "("s expression ")"s;
    braceExpression(_open : NToken, expr : DirectiveExpression, _close : NToken) : DirectiveExpression
    {
      expr
    }
    
    //primaryExpression   : DirectiveExpression = (boolLiteral / symbol / braceExpression);
    primaryExpression(opt : DirectiveExpression) : DirectiveExpression
    {
      opt
    }
    
    //unaryExpression     : DirectiveExpression = ("!" s)* primaryExpression;
    unaryExpression(ops : SCG.List[NToken], expr : DirectiveExpression) : DirectiveExpression
    {
      mutable chain = expr;
      for(mutable i = ops.Count - 1; i >= 0; --i)
      {
        def op = ops[i];
        chain = DirectiveExpression.UnaryOperator(GetText(op), chain);
      }
      chain
    }
    
    foldExpressions(head : DirectiveExpression, tail : SCG.List[NToken * DirectiveExpression]) : DirectiveExpression
    {
      mutable chain = head;
      foreach((op, expr) in tail)
        chain = DirectiveExpression.BinaryOperator(GetText(op), chain, expr);
      chain
    }
    
    //equalityExpression  : DirectiveExpression = unaryExpression     (("==" / "!=")s unaryExpression   )*;
    equalityExpression(head : DirectiveExpression, tail : SCG.List[NToken * DirectiveExpression]) : DirectiveExpression
    {
      foldExpressions(head, tail)
    }
        
    //andExpression       : DirectiveExpression = equalityExpression  ("&&"s          equalityExpression)*;
    andExpression(head : DirectiveExpression, tail : SCG.List[NToken * DirectiveExpression]) : DirectiveExpression
    {
      foldExpressions(head, tail)
    }

    //orExpression        : DirectiveExpression = andExpression       ("||"s          andExpression     )*;
    orExpression(head : DirectiveExpression, tail : SCG.List[NToken * DirectiveExpression]) : DirectiveExpression
    {
      foldExpressions(head, tail)
    }
        
    //expression          : DirectiveExpression = orExpression s;
    expression(expr : DirectiveExpression) : DirectiveExpression
    {
      expr
    }
    
    //message     : string = (!(newLine / comment) any)*;
    message(msg : NToken) : string
    {
      GetText(msg)
    }

    //fileName    : string = "\"" (!("\"" / newLine) any)* "\"";
    fileName(_ : NToken, name : NToken, _ : NToken) : string
    {
      GetText(name)
    }

    //ifDirective           : Directive = "if"        S expression    &(newLine / comment / !any);
    ifDirective(_ : NToken, expression : DirectiveExpression) : Directive
    {
      Directive.If(expression)
    }

    //elifDirective         : Directive = "elif"      S expression    &(newLine / comment / !any);
    elifDirective(_ : NToken, expression : DirectiveExpression) : Directive
    {
      Directive.Elif(expression)
    }

    //elseDirective         : Directive = "else"      s               &(newLine / comment / !any);
    elseDirective(_ : NToken) : Directive
    {
      Directive.Else()
    }

    //endifDirective        : Directive = "endif"     s               &(newLine / comment / !any);
    endifDirective(_ : NToken) : Directive
    {
      Directive.EndIf()
    }

    //defineDirective       : Directive = "define"    S expression    &(newLine / comment / !any);
    defineDirective(_ : NToken, expression : DirectiveExpression) : Directive
    {
      Directive.Define(expression)
    }

    //undefDirective        : Directive = "undef"     S expression    &(newLine / comment / !any);
    undefDirective(_ : NToken, expression : DirectiveExpression) : Directive
    {
      Directive.Undefine(expression)
    }

    //lineNumberDirective   : Directive = "line"      S ['0'..'9']+ s fileName? s &(newLine / comment / !any);
    lineNumberDirective(_ : NToken, number : NToken, fileName : option[string]) : Directive
    {
      mutable lineNumber;
      if(int.TryParse(GetText(number), out lineNumber))
        Directive.LineNumber(lineNumber, fileName.WithDefault(null))
      else
        Directive.Error("invalid line number")
    }

    //lineDefaultDirective  : Directive = "line"      S "default"   s &(newLine / comment / !any);
    lineDefaultDirective(_ : NToken, _ : NToken) : Directive
    {
      Directive.LineDefault()
    }    

    //errorDirective        : Directive = "error"     S message       &(newLine / comment / !any);
    errorDirective(_ : NToken, message : option[string]) : Directive
    {
      Directive.CompilationError(message.WithDefault(""))
    }

    //warningDirective      : Directive = "warning"   S message       &(newLine / comment / !any);
    warningDirective(_ : NToken, message : option[string]) : Directive
    {
      Directive.CompilationWarning(message.WithDefault(""))
    }

    //regionDirective       : Directive = "region"    S message       &(newLine / comment / !any);
    regionDirective(_ : NToken, message : option[string]) : Directive
    {
      Directive.Region(message.WithDefault(""))
    }

    //endregionDirective    : Directive = "endregion" S message       &(newLine / comment / !any);
    endregionDirective(_ : NToken, message : option[string]) : Directive
    {
      Directive.EndRegion(message.WithDefault(""))
    }

    //invalidDirective      : Directive = message                     &(newLine / comment / !any);
    invalidDirective(text : string) : Directive
    {
      Directive.Error($"unknown preprocessor directive: $text")
    }

    directive(_ : NToken, opt : Directive) : PreParseElement
    {
      PreParseElement.Directive(opt)
    }

    //element : PreParseElement = rawText* (directiveFromNewLine / comment);
    element(_ : NToken, opt : PreParseElement) : PreParseElement
    {
      opt
    }

    //directiveFromNewLine  : PreParseElement = newLine directive;
    directiveFromNewLine(_ : NToken, directive : PreParseElement) : PreParseElement
    {
      directive
    }

    //start   : PreparedCompilationUnit = directive? element* rawText* !any;
    start(firstLineDirective  : option[PreParseElement],
          elements            : SCG.List[PreParseElement],
          _                   : NToken) : PreparedCompilationUnit
    {
      PreparedCompilationUnit(match(firstLineDirective)
      {
        | Some(first) => first :: elements.ToList()
        | _ => elements.ToList()
      })
    }
  }
}