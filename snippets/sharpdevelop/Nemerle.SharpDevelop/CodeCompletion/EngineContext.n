using System;
using System.Windows.Forms;
using SCG = System.Collections.Generic;

using ICSharpCode.Core;
using ICSharpCode.SharpDevelop;
using ICSharpCode.SharpDevelop.Project;
using ICSharpCode.TextEditor.Document;

using Nemerle.Assertions;
using Nemerle.Completion2;
using Nemerle.Compiler;
using Nemerle.Compiler.Utils.Async;
using Nemerle.Utility;

namespace Nemerle.SharpDevelop.CodeCompletion {

    public sealed class EngineContext : IIdeProject, IDisposable {

        internal this(project : IProject) {
            this.project = project;
            this.engine = EngineFactory.Create(this, TraceWriter(), false);

            ProjectService.ProjectItemAdded += OnProjectItemAdded;
            ProjectService.ProjectItemRemoved += OnProjectItemRemoved;
            Application.Idle += OnApplicationIdle;
            match(project) {
                | project is AbstractProject =>
                    project.ActiveConfigurationChanged += OnActiveConfigurationChanged;
                | _ => ();
            }

            Reload();
        }

        [Accessor] engine : IEngine;

        [Accessor] sync_root : object = object();

        [Accessor] project : IProject;

        source_map : SCG.Dictionary[int, Source] = SCG.Dictionary();

        private FindSource(item : FileProjectItem) : bool * Source {
            foreach(source in source_map.Values) {
                when(ReferenceEquals(source.Item, item))
                    Nemerle.Imperative.Return( (true, source) )
            }
            (false, null)
        }

        private AddSourceNoCheck(item : FileProjectItem) : ISource {
            def source = Source(this, item);
            source_map.Add(source.FileIndex, source);
            source
        }

        private AddSource(item : FileProjectItem) : ISource {
            match(FindSource(item)) {
                | (true, source) => source
                | (false, _) =>
                    def source = AddSourceNoCheck(item);
                    engine.RequestOnBuildTypesTree();
                    source
            }
        }

        private RemoveSource(item : FileProjectItem) : bool {
            match(FindSource(item)) {
                | (true, source) =>
                    _ = source_map.Remove(source.FileIndex);
                    engine.RequestOnBuildTypesTree();
                    true

                | (false, _) =>
                    false
            }
        }

        private Clear() : void {
            source_map.Clear();
        }

        mutable reload_project_async : AsyncRequest = null;

        public Reload() : void  {
            lock(sync_root) {
                when(null == reload_project_async || reload_project_async.IsCompleted) {
                    Clear();
                    foreach(item is FileProjectItem in project.GetItemsOfType(ItemType.Compile)) {
                        _ = AddSourceNoCheck(item)
                    }
                    reload_project_async = engine.BeginReloadProject();
                }
            }
        }

        #region IIdeProject

        public GetAssemblyReferences() : SCG.IEnumerable[string] {
            foreach(item is ReferenceProjectItem in project.GetItemsOfType(ItemType.Reference)) {
                yield item.AssemblyName.FullName
            }
        }

        public GetMacroAssemblyReferences() : SCG.IEnumerable[string] {
            foreach(item is MacroReferenceProjectItem in project.GetItemsOfType(ItemTypeExt.MacroReference)) {
                yield item.AssemblyName.FullName
            }
        }

        public GetOptions() : CompilationOptions {
            def opt = CompilationOptions();

            opt.GreedyReferences = false;
            opt.ColorMessages    = false;
            opt.IgnoreConfusion  = true;

            match(project) {
                | project is MSBuildBasedProject =>
                    def constants_str = project.GetProperty("", "", "DefineConstants");
                    unless(string.IsNullOrEmpty(constants_str)) {
                        def constants = constants_str.Split(array[";", " "], StringSplitOptions.RemoveEmptyEntries);
                        foreach(c in constants) {
                            opt.DefineConstant(c);
                        }
                    }
                | _ => ();
            }

            opt
        }

        public GetSource(fileIndex : int) : ISource {
            lock(sync_root) {
                mutable source;
                if(source_map.TryGetValue(fileIndex, out source))
                    source
                else
                    null
            }
        }

        public GetSource(fileName : string) : Source {
            lock(sync_root) {
                mutable source;
                if(source_map.TryGetValue(Location.GetFileIndex(fileName), out source))
                    source
                else
                    null
            }
        }

        public GetSources() : SCG.IEnumerable[ISource] {
            lock(sync_root) {
                def sources = array(source_map.Count);
                source_map.Values.CopyTo(sources, 0);
                sources
            }
        }

        public ClearAllCompilerMessages() : void {
        }

        public SetCompilerMessageForCompileUnit(compileUnit : CompileUnit) : void {
        }

        public SetMethodCompilerMessages(member : MemberBuilder, messages : SCG.IEnumerable[CompilerMessage]) : void {
        }

        public SetStatusText(text : string) : void {
            StatusBarService.SetMessage(text);
        }

        public SetTopLevelCompilerMessages(messages : SCG.IEnumerable[CompilerMessage]) : void {
        }

        public ShowMessage(message : string, messageType : MessageType) : void {
            match(messageType) {
                | Error     => MessageService.ShowError(message)
                | Warning   => MessageService.ShowWarning(message)
                | Hint | Info | _  => MessageService.ShowMessage(message)
            }
        }

        public LookupLocationsFromDebugInformation(info : GotoInfo) : array[GotoInfo] {
            array(0)
        }

        public SetHighlights(source : ISource, highlights : SCG.IEnumerable[GotoInfo]) : void {
        }

        public AddUnimplementedMembers(source : ISource, ty : TypeBuilder, unimplementedMembers : SCG.IEnumerable[IMember]) : void {
        }

        public AddOverrideMembers(source : ISource, ty : TypeBuilder, notOverriden : SCG.IEnumerable[IMember]) : void {
        }

        public TypesTreeCreated() : void {
        }

        #endregion


        #region File load/unload handlers

        internal NotifyFileOpened(fileName : string, document : IDocument) : void {
            lock(sync_root) {
                foreach(source in source_map.Values) {
                    when(source.Item.FileName == fileName)
                        source.Document = document;
                }
            }
        }

        internal NotifyFileClosed(document : IDocument) : void {
            lock(sync_root) {
                foreach(source in source_map.Values) {
                    when(ReferenceEquals(source.Document, document))
                        source.Document = null;
                }
            }
        }

        #endregion


        #region Event handlers

        private OnActiveConfigurationChanged(_ : object, e : EventArgs) : void {
            Reload();
        }

        private OnProjectItemAdded(_ : object, e : ProjectItemEventArgs) : void {
            when(ReferenceEquals(e.ProjectItem.Project, project)) {
                if(ItemType.Compile == e.ProjectItem.ItemType)
                    match(e.ProjectItem) {
                        | item is FileProjectItem =>
                            lock(sync_root) {
                                _ = AddSource(item);
                            }
                        | _ => ();
                    }
                else
                    ReloadEngineOnReferenceChange(e.ProjectItem);
            }
        }

        private OnProjectItemRemoved(_ : object, e : ProjectItemEventArgs) : void {
            when(ReferenceEquals(e.ProjectItem.Project, project)) {
                if(ItemType.Compile == e.ProjectItem.ItemType)
                    match(e.ProjectItem) {
                        | item is FileProjectItem =>
                            lock(sync_root) {
                                _ = RemoveSource(item);
                            }
                        | _ => ();
                    }
                else
                    ReloadEngineOnReferenceChange(e.ProjectItem);
            }
        }

        private ReloadEngineOnReferenceChange(item : ProjectItem) : void {
            when(ItemType.Reference == item.ItemType
                || ItemTypeExt.MacroReference == item.ItemType) {
                _ = engine.BeginReloadProject();
            }
        }

        private OnApplicationIdle(_ : object, _ : EventArgs) : void {
            engine.OnIdle();
        }

        #endregion


        #region Disposable

        [Accessor] mutable disposed : bool = false;

        public Dispose() : void {
            lock(sync_root) {
                unless(disposed) {
                    try {
                        engine.Close();
                    } finally {
                        Clear();

                        ProjectService.ProjectItemAdded -= OnProjectItemAdded;
                        ProjectService.ProjectItemRemoved -= OnProjectItemRemoved;                        
                        Application.Idle -= OnApplicationIdle;
                        match(project) {
                            | project is AbstractProject =>
                                project.ActiveConfigurationChanged -= OnActiveConfigurationChanged;
                            | _ => ();
                        }

                        disposed = true;
                    }
                }
            }
        }

        protected override Finalize() : void {
            Dispose();
        }

        #endregion

    }


}