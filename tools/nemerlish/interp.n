using Nemerle.Collections.List;
using Nemerle.Utility.NString;
using System;
using SC=System.Console;
using System.Reflection;
using System.Text.RegularExpressions.Regex;
  
namespace Nemerle.Evaluation.Interpreter
{
  /**
   * <summary>
   *   Holds properties, variables and functions for use within
   *   the interpreter (by the user) and in the interpreters
   *   main loop.
   * </summary>
   */
  public module Internals
  {
    public nccversion : Version = Reflection.Assembly.GetAssembly 
                                  (typeof (Nemerle.Compiler.MainParser)).
                                  GetName ().Version;
    internal interpassembly : string = Reflection.Assembly.GetAssembly
                                       (typeof (Internals)).Location;

    // A list of referenced assemblies, as entered by the user.
    mutable @ref = "";
    // A parsed version of the above, refreshed by the Ref property.
    mutable refparsed : list [string] = [interpassembly];
    // The primary prompt.
    mutable ps1 = "- ";
    // The secondary prompt displayed for multiple line input.
    mutable ps2 = "= ";
    // The current prompt.
    mutable prompt : string = ps1;
  
    public Ref : string {
      get { @ref }
      set { 
        @ref = value;
        refparsed = interpassembly ::
                    Split (Replace (@ref, @"\s", ""), array [',']);
      }
    }
  
    public RefParsed : list [string] {
      get { refparsed }
    }
  
    public PS1 : string {
      get { ps1 }
      set { ps1 = value }
    }
  
    public PS2 : string {
      get { ps2 }
      set { ps2 = value }
    }
  
    public Prompt : string {
      get { prompt }
      set { prompt = value }
    }
  
    public Help : void {
      get { 
        SC.WriteLine ("Help\t-\tPrint this help.");
        SC.WriteLine ("Ref\t-\tLink specified assemblies.\n\t\tex. Ref = "
                      "\"Nemerle.dll, Nemerle.Compiler.dll\"");
        SC.WriteLine ("PS1\t-\tSet primary prompt.\n\t\tex. PS1 = \">>> \"");
        SC.WriteLine ("PS2\t-\tSet secondary prompt.\n\t\tex. PS2 = \"... \"");
        SC.WriteLine ("\nNote: All these properties reside in the\n"
                      "      Nemerle.Evaluation.Interpreter.Internals\n"
                      "      namespace, which is open when using the default\n"
                      "      ~/.nemerlish_profile file.");
        SC.WriteLine ("\nYou can also invoke a command in a subshell with "
                      "\"!command;;\",\nor simply type \"!;;\" to run the "
                      "default shell.\n");
        SC.WriteLine ("Use ^c (ctrl-c) to quit.\n")
      }
    }
  }
}
