namespace Nemerle.ComputationExpressions.Async
{
  [Record]
  internal class AsyncBind[A, B] : Async[B]
  {
    private mutable async : Async[A];
    private cont : A -> Async[B];

    public override Exec(job : AsyncJobBase, _res : ref B) : Async[B]
    {
      mutable r;
      async = job.ExecWhileReady(async, ref r);
      if (async == null)
        cont(r);
      else
        this;
    }

    public override Cancel() : void
    {
      when (async != null)
        async.Cancel();
    }
  }

  [Record]
  internal class AsyncBindVoid[A, B] : Async[B]
  {
    private mutable async : Async[A];
    private cont : void -> Async[B];

    public override Exec(job : AsyncJobBase, _res : ref B) : Async[B]
    {
      mutable r;
      async = job.ExecWhileReady(async, ref r);
      if (async == null)
        cont();
      else
        this;
    }

    public override Cancel() : void
    {
      when (async != null)
        async.Cancel();
    }
  }
}
