using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

namespace LRPEGCC
{
  public module FSMBuilder
  {
    public Symbol(chars : RangeSet) : FSM
    {
      def fsm = FSM();
      def fsm = fsm.AddTransition(Transition.Symbol(fsm.StartState, fsm.OkState, chars));
      //def fsm = fsm.AddTransition(Transition.Symbol(fsm.StartState, fsm.FailState, chars.Invert()));
      def fsm = fsm.AddTransition(Transition.End(fsm.StartState, fsm.FailState));
      fsm
    }
    public Seq(fsms : list[FSM]) : FSM
    {
      def fsm = FSM();
      def fsm = fsm.AddTransition(Transition.Epsilon(fsm.StartState, fsm.OkState));
      def append(fsm2 : FSM, fsm1 : FSM) : FSM
      {
        def (fsm1, start, ok, fail) = fsm1.IncludeFSM(fsm2);
        def fsm1 = fsm1.AddTransition(Transition.Epsilon(fsm1.OkState, start));
        def fsm1 = fsm1.AddTransition(Transition.Epsilon(fail, fsm1.FailState));
        def fsm1 = fsm1.SetOkState(ok);
        fsm1;
      }
      fsms.FoldLeft(fsm, append);
    }
    public Choice(fsms : list[FSM]) : FSM
    {
      def fsm = FSM();
      def append(fsm2 : FSM, fsm1 : FSM) : FSM
      {
        def (fsm1, start, ok, fail) = fsm1.IncludeFSM(fsm2);
        def fsm1 = fsm1.AddTransition(Transition.Epsilon(fsm1.StartState, start));
        def fsm1 = fsm1.AddTransition(Transition.Epsilon(ok, fsm1.OkState));
        def fsm1 = fsm1.AddTransition(Transition.Epsilon(fail, fsm1.FailState));
        fsm1;
      }
      fsms.FoldLeft(fsm, append);
    }
  }
}
