using System;
using System.Text;

using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

namespace LRPEGCC
{
  internal class Grammar
  {
    private _grammar : Map[RuleRef, Rule] = Map();
    [Accessor]
    private _startRuleName : RuleRef;

    public this(startRuleName : RuleRef)
    {
      _startRuleName = startRuleName;
    }

    private this(startRuleName : RuleRef, grammar : Map[RuleRef, Rule])
    {
      _startRuleName = startRuleName;
      _grammar = grammar;
    }

    public Add(name : RuleRef, rule : Rule) : Grammar
    {
      Grammar(StartRuleName, _grammar.Add(name, rule));
    }

    public GetRule(name : RuleRef) : Rule
    {
      if (_grammar.Contains(name))
        _grammar.Get(name);
      else
        throw GrammarException($"There is no rule \"$name\" in grammar.", name.Location);
    }

    public Count : int
    {
      get { _grammar.Count }
    }

    public Names : list[RuleRef]
    {
      get { _grammar.Map((name, _) => name) }
    }

    public TransformRules(fn : Rule -> Rule) : Grammar
    {
      _grammar.Fold(Grammar(StartRuleName), (name, rule, newGrammar) =>
      {
        newGrammar.Add(name, fn(rule))
      })
    }

    public override ToString() : string
    {
      def sb = StringBuilder();
      _ = sb.AppendLine($"Count:$Count");
      _ = sb.AppendLine($"_startRuleName:$_startRuleName");
      _ = sb.AppendLine("rules:");
      foreach((name, rule) in _grammar)
      {
        _ = sb.AppendLine($"  name:$(name)");
        _ = sb.AppendLine($"  rule type:$(rule.GetType().ToString())");
        _ = sb.AppendLine($"  rule:$(rule.ToString())");
        _ = sb.AppendLine(string.Empty);
      }

      sb.ToString()
    }
  }
}
