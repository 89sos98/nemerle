/*
 * Copyright (c) 2003, 2004 The University of Wroclaw.
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions
 * are met:
 *    1. Redistributions of source code must retain the above copyright
 *       notice, this list of conditions and the following disclaimer.
 *    2. Redistributions in binary form must reproduce the above copyright
 *       notice, this list of conditions and the following disclaimer in the
 *       documentation and/or other materials provided with the distribution.
 *    3. The name of the University may not be used to endorse or promote
 *       products derived from this software without specific prior
 *       written permission.
 * 
 * THIS SOFTWARE IS PROVIDED BY THE UNIVERSITY ``AS IS'' AND ANY EXPRESS OR
 * IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES
 * OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN
 * NO EVENT SHALL THE UNIVERSITY BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
 * SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED
 * TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR
 * PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF
 * LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING
 * NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS
 * SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 */

using System.Text;
using System.Globalization;
using System;

using Nemerle.Collections;

namespace Nemerle.Compiler {

public variant Token
{
  | Tok_identifier { name : string; }
  | Tok_keyword { name : string; }
  | Tok_operator { name : string; priority : int; }

  | Tok_string_literal { value : string; }
  | Tok_char_literal { value : char; }

  | Tok_sbyte_literal { value : SByte; }
  | Tok_byte_literal { value : Byte; }
  | Tok_short_literal { value : Int16; }
  | Tok_ushort_literal { value : UInt16; }
  | Tok_integer_literal { value : int; }
  | Tok_uinteger_literal { value : UInt32; }
  | Tok_long_literal { value : Int64; }
  | Tok_ulong_literal { value : UInt64; }

  | Tok_float_literal { value : float; }
  | Tok_double_literal { value : Double; }
  | Tok_decimal_literal { value : Decimal; }

  | Tok_EOF
}

public interface ILexer
{
  get_token () : Token;
  get_location () : Location;
}

public class Lexer : ILexer
{
  file : System.IO.FileStream;
  reader : System.IO.TextReader;
  file_name : string;
  mutable line : int;
  mutable col : int;
  mutable putback : bool;
  mutable putbackVal : char;
  mutable isPendingChar : bool;  // is there already some first char
  mutable pendingChar : char;
  
  mutable last_line : int;
  mutable last_col : int;
  
  static keywords : Hashtable <string, int>;

  public class Error : System.Exception
  {
    public name : string;
    public this (name : string) { this.name <- name; }
  }


  private check_last_line_for_lf () : void
  {
    def _ = file.Seek ((-1 :> Int64), IO.SeekOrigin.End);
    
    def inp = file.ReadByte ();
    
    if (inp >= 0) {
      def ch = Convert.ToChar (inp);
      when (ch != '\n') Message.warning (Location (file_name, line, col),
                                         "no new line at the end of the file");
    } else 
      throw Error ("unexpected end of file");
      
    def _ = file.Seek ((0 :> Int64), IO.SeekOrigin.Begin);
    
    ()
  }


  public this (fn : string)
  {
    file_name <- fn;
    file <- IO.FileStream (fn, IO.FileMode.Open, IO.FileAccess.Read);
    reader <- IO.StreamReader (file, Text.Encoding.UTF8);
    when (Flags.pedantic_lexer)
      check_last_line_for_lf ();
    line <- 1;
    col <- 1;
    putback <- false;
    isPendingChar <- false;
  }

  do_read () : char
  {
    def inp = reader.Read ();
    if (inp >= 0) {
      def ch = Convert.ToChar (inp);
      match (ch) {
        | '\n' =>
          line <- line + 1;
          col <- 1
        | '\r' =>
          when (Flags.pedantic_lexer)
            Message.warning (Location (file_name, line, col),
                             "CR character found in input stream")
        | '\t' =>
          when (Flags.pedantic_lexer)
            Message.warning (Location (file_name, line, col),
                             "tab character found in input stream")
        | _ =>
          col <- col + 1
      };
      ch
    } else 
      throw Error ("unexpected end of file")
  }
        
  read () : char
  {
    if (!putback)
      do_read ()
    else {
      putback <- false;
      putbackVal
    }
  }

  peek () : char
  {
    unless (putback)
      putbackVal <- do_read ();
    putback <- true;
    putbackVal
  }

  is_id_beg (ch : char) : bool
  {
    Char.IsLetter (ch) || ch == '_'
  }

  static is_opchar (ch : char) : bool
  {
    match (ch) {
      | '=' | '<' | '>' | '@' | '^' | '|' | '&' | '+'
      | '-' | '*' | '/' | '$' | '%' | '!' | '?' | '~'
      | '.' | ':' | '#' | '[' | ']' => true
      | _ => false
    }
  }

  skip_comment () : void
  {
    def loop (seen_star) {
      match (read ()) {
        | '*' => loop (true)
        | '/' when seen_star => ()
        | _ => loop (false)
      }
    };

    loop (false)
  }

  get_op (priority : int, first_ch : char) : Token
  {
    def buf = Text.StringBuilder ();
    
    ignore (buf.Append (first_ch));

    while (is_opchar (peek ())) { 
      ignore (buf.Append (read ()))
    };

    def s = buf.ToString ();
    match (s) {
      | "<-" => 
        // force low priority
        Tok_operator ("<-", 9)

      | "|" | ":" | ":>" | "=>" | "=" =>
        // cannot use this as regular operator
        Tok_operator (s, 0)
        
      | _ => 
        Tok_operator (s, priority)
    }
  }

  get_number (first_ch : char) : Token
  {
    def buf = Text.StringBuilder ();
    mutable float_mode <- 
      if (first_ch == '.') true else false;
    mutable last_was_digit <- true;

    ignore (buf.Append (first_ch));

    // read digits and . between them if it is present
    def loop () {
      match (peek ()) {
        | '.' => 
          unless (float_mode) {
            float_mode <- true;
            ignore (read ());
            if (Char.IsDigit (peek ())) {
              ignore (buf.Append ('.'));
              loop ()
            }
            else {
              isPendingChar <- true;
              pendingChar <- '.';
              last_was_digit <- false;
              float_mode <- false;
            }
          }
        | x when Char.IsDigit (x) =>
          ignore (buf.Append (read ()));
          loop ()
        | _ => ()
      }
    };
    loop ();

    def exponent_part (only_realsuf) {
      when (!only_realsuf) {
        match (peek ()) {
          | 'E' | 'e' =>
            ignore (buf.Append (read ()));
            match (peek ()) {
              | '+' | '-' =>
                ignore (buf.Append (read ()));
              | _ => ()
            };
            if (Char.IsDigit (peek ()))
              do {
                ignore (buf.Append (read ()));
              } while (Char.IsDigit (peek ()))
            else
              throw Error ("no digits after exponent sign in float literal")
          | _ => ()
        }
      };
      match (peek ()) {
        | 'F' | 'f' =>
          ignore (read ());
          Tok_float_literal (Single.Parse (buf.ToString (),
                                           NumberFormatInfo.InvariantInfo))
        | 'D' | 'd' =>
          ignore (read ());          
          Tok_double_literal (Double.Parse (buf.ToString (),
                                           NumberFormatInfo.InvariantInfo))
        | 'M' | 'm' =>
          ignore (read ());          
          Tok_decimal_literal (Decimal.Parse (buf.ToString (),
                                              NumberFormatInfo.InvariantInfo))
        | _ =>
          Tok_double_literal (Double.Parse (buf.ToString (),
                                            NumberFormatInfo.InvariantInfo))
      }
    };

    if (float_mode)
      match (peek ()) {
        | 'E' | 'e' => 
          exponent_part (false)
        | _ =>
          exponent_part (true)
      }
    else
      if (last_was_digit)
        match (peek ()) {
          | 'E' | 'e' =>
            exponent_part (false)
          | 'f' | 'F' | 'd' | 'D' | 'm' | 'M' =>
            exponent_part (true)
          | _ =>
            Tok_integer_literal (Int32.Parse (buf.ToString ()))
        }
      else {
        /// we have integer number here
        
        def special (c) {
          | 'l' | 'L' | 's' | 'S' | 'b' | 'B' | 'u' | 'U' =>
            true
          | _ =>
            false
        };

        // take read value as string
        def val = buf.ToString ();

        // check suffixes to make special types conversions
        mutable ch <- peek ();
        if (special (ch)) {
          ignore (read ());
          mutable unsigned <- if (ch == 'u' || ch == 'U') true else false;

          // we can have two letter suffixes
          def ch' = peek ();
          when (special (ch')) {
            ignore (read ());
            if (ch' == 'u' || ch' == 'U') {
              when (unsigned) throw Error ("bad integer suffix (unsigned twice)");
              unsigned <- true;
            }
            else {
              unless (unsigned) throw Error ("bad integer suffix (type contraint twice)");
              ch <- ch';
            }
          };
          // [ch] now contains type meaning or 'u' (then it is uint for sure)
          match (ch) {
            | 'b' | 'B' =>
              if (unsigned)
                Tok_byte_literal (Convert.ToByte (val))
              else
                Tok_sbyte_literal (Convert.ToSByte (val))
            | 's' | 'S' =>
              if (unsigned)
                Tok_ushort_literal (Convert.ToUInt16 (val))
              else
                Tok_short_literal (Convert.ToInt16 (val))
            | 'l' | 'L' =>
              if (unsigned)
                Tok_ulong_literal (Convert.ToUInt64 (val))
              else
                Tok_long_literal (Convert.ToInt64 (val))
            | _ =>
              Tok_uinteger_literal (Convert.ToUInt32 (val))
          }
        }
        else
          Tok_integer_literal (Convert.ToInt32 (val))
      }
  }

  get_id (first_ch : char) : Token
  {
    if (first_ch == '\'' && !is_id_beg (peek ())) {
      get_char ()
    } else {
      def buf = Text.StringBuilder ();
      ignore (buf.Append (first_ch));

      while (is_id_beg (peek ()) || 
             Char.IsDigit (peek ()) || 
             peek () == '\'') {
        ignore (buf.Append (read ()))
      };
          
      def str = buf.ToString ();
      
      if (first_ch == '\'' && str.Length == 3 && str[2] == '\'')
        Tok_char_literal (str[1])
      else if (keywords.Contains (str))
        Tok_keyword (str)
      else
        Tok_identifier (str)
    }
  }

  escape_value (ch : char) : char
  {
    match (ch) {
      | 'n' => '\n'
      | 't' => '\t'
      | 'r' => '\r'
      | 'b' => '\b'
      | 'e' => '\e'
      | '"' => '"'
      | '\'' => '\''
      | '\\' => '\\'
      | '0' => '\0'
      | _ => throw Error ("bad escape character")
    }
  }
    
  get_char () : Token
  {
    match (get_string ('\'')) {
      | Tok_string_literal (v) =>
        if (v.Length == 0)
          throw Error ("empty character literal")
        else {
          if (v.Length != 1)
            throw Error ("character literal too long")
          else
            Tok_char_literal (v[0])
        }
      | _ => Util.ice ("Value is not string in get_char")
    }
  }

  get_string (end_ch : char) : Token
  {
    def buf = StringBuilder ();
    mutable ch <- ' ';
              
    while (ch != end_ch) {
      ch <- read ();
      match (ch) {
        | '\\' =>
          ch <- read ();
          def esc = escape_value (ch);
          ignore (buf.Append (esc));
          ch <- ' ';
        | '\n' =>
          throw Error ("newline before end of string")
        | _ when ch != end_ch => ignore (buf.Append (ch))
        | _ => ()
      }
    };
     
    Tok_string_literal (buf.ToString ())
  }

  get_monkey_string () : Token
  {
    def buf = StringBuilder ();

    def loop () {
      match (read ()) {
        | '"' => Tok_string_literal (buf.ToString ())
        | ch => ignore (buf.Append (ch)); loop ()
      }
    };

    loop ()
  }

  do_get_token (ch : char) : Token
  {
    match (ch) {
      | ' ' | '\t' | '\r' | '\n' => get_token ()
      
      | '"' => get_string ('"')

      | '\'' => get_id ('\'')

      | '.' => 
        if (Char.IsDigit (peek ()))
          get_number (ch)
        else
          Tok_operator (ch.ToString (), 0)

      | '{' | '}' 
      | '[' | ']' 
      | ',' | ';' 
      | '(' | ')' => Tok_operator (String (ch, 1), 0)

      | '@' =>
        def next = read ();
        if (is_opchar (next))
          match (get_op (0, next)) {
            | Tok_operator (s, _) => Tok_identifier (s)
            | _ => Util.ice ()
          }
        else if (is_id_beg (next))
          match (get_id (next)) {
            | Tok_identifier (x)
            | Tok_keyword (x) => Tok_identifier (x)
            | _ => Util.ice ()
          }
        else if (next == '"')
          get_monkey_string ()
        else
          throw Error ("expecting operator, identifier or string literal after '@'")
      
      | '*' =>
        if (peek () == '*')
          get_op (1, ch)
        else
          get_op (2, ch)

      | '/' =>
        if (peek () == '/') {
          while (read () != '\n') {};
          get_token ()
        } else if (peek () == '*') {
          ignore (read ());
          skip_comment ();
          get_token ()
        } else
          get_op (2, ch)
                              
      | '%' | '\\'
        => get_op (2, ch)
      
      | '+' | '-' 
        => if (peek () != '.') get_op (3, ch) 
           else Tok_operator (ch.ToString (), 3)
      
      | '^' | '$' | '~' | '?' | '#'
        => get_op (4, ch)
        
      | '=' | '<' | '>' | '!' 
        => get_op (5, ch)
        
      | '&' => get_op (6, ch)
      | '|' => get_op (7, ch)
      | ':' => get_op (8, ch)

      | _ =>
        if (Char.IsDigit (ch))
          get_number (ch)
        else
          if (is_id_beg (ch))
            get_id (ch)
          else
            throw Error ("invalid character")
    }      
  }

  get_token () : Token implements ILexer.get_token
  {
    last_line <- line;
    last_col <- col;
    mutable eof <- false;

    def ch = 
      if (isPendingChar) {
        isPendingChar <- false;
        pendingChar
      }
      else
        try read () catch { _ : Error => eof <- true; '_'  };

    if (eof)
      Tok_EOF () 
    else
      do_get_token (ch)
  }

  get_location () : Location implements ILexer.get_location
  {
    Location (file_name, last_line, last_col)
  }

  get_current_location () : Location
  {
    Location (file_name, line, col)
  }

  public static this () 
  {
    def tab = array [
        "_", "abstract", "and", "array", "as", "base", "catch",
        "class", "def", "extends", "extern", "false", "finally", "fun",
        "implements", "interface", "internal", "macro", "match", "module",
        "mutable", "namespace", "new", "null", "out", "override",
        "params", "private", "protected", "public", "ref", "sealed",
        "static", "struct", "syntax", "this", "throw", "true", "try",
        "type", "typeof", "using", "variant", "virtual", "void", "when",
        "where"
    ];
    keywords <- Hashtable ();
    for (mutable i <- 0; i < tab.Length; ++i)
      keywords.Add (tab[i], 1)
  }

  public static AddKeyword (str : string) : void 
  {
    match (keywords.Get (str)) {
      | Some (level) =>
        keywords.Set (str, level + 1)
      | _ =>
        keywords.Set (str, 1)
    }
  }

  public static RemoveKeyword (str : string) : void 
  {
    match (keywords.Get (str)) {
      | Some (1) =>
        keywords.Remove (str)
      | Some (level) =>
        keywords.Set (str, level - 1)
      | _ =>
        Util.ice ("trying to remove non-existant keyword from lexer")
    }
  }
  
  public static IsOperator (str : string) : bool
  {
    def len = str.Length;
    mutable is <- true;
    for (mutable i <- 0; i < len && is; ++i) {
      def c = str[i];
      is <- is_opchar (c) || c == '(' || c == ')' || c == ';';
    };
    is
  }

  public static IsKeyword (str : string) : bool
  {
    def len = str.Length;
    mutable is <- true;
    for (mutable i <- 0; i < len && is; ++i)
      is <- Char.IsLetter (str[i]);
    is
  }
} // end class Lexer
} // end ns
