(*
 * Copyright (c) 2003 The University of Wroclaw.
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions
 * are met:
 *    1. Redistributions of source code must retain the above copyright
 *       notice, this list of conditions and the following disclaimer.
 *    2. Redistributions in binary form must reproduce the above copyright
 *       notice, this list of conditions and the following disclaimer in the
 *       documentation and/or other materials provided with the distribution.
 *    3. The name of the University may not be used to endorse or promote
 *       products derived from this software without specific prior
 *       written permission.
 * 
 * THIS SOFTWARE IS PROVIDED BY THE UNIVERSITY ``AS IS'' AND ANY EXPRESS OR
 * IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES
 * OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN
 * NO EVENT SHALL THE UNIVERSITY BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
 * SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED
 * TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR
 * PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF
 * LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING
 * NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS
 * SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 *)

open Nemerle.Compiler;

namespace Nemerle.Compiler {

type long_id = string;
type id = string;

record Location 
  {
    file : string;
    line : int;
    column : int;
  }
  
class Located
  {
    ref loc : Location;
    this () {}
  }

class Named extends Located
  {
    ref name : long_id;
  }

class Decl_base extends Named
  {
    ref modifiers : list (Modifier);
    ref env : Env;
    ref id : int;

    this () { this#id <- Util.next_id(); }
  }

variant Modifier = 
  [
    | Mod_public
    | Mod_private
    | Mod_new
    | Mod_protected
    | Mod_internal
    | Mod_abstract
    | Mod_sealed
  ]
  
variant Top_decl extends Located =
  [
    | TD_namespace { 
        name : long_id; 
        decls : list (Top_decl);
      }
    | TD_open {
        what : long_id;
      }
    | TD_namespace_alias {
        alias : id;
        expanded : long_id;
      }
    | TD_type {
        decl : Type_decl;
      }
  ]

class Type_decl_base extends Decl_base
  {
    typarms : Typarms;
    t_extends : option (Type);
    t_implements : list (Type);
  }

record Constraint
  {
    tyvar : id;
    ty : Type;
  }

record Typarms
  {
    tyvars : list (id);
    constraints : list (Constraint);
  }
  
variant Type_decl extends Type_decl_base =
  [
    | TD_class { ref decls : list (Class_member); }
    | TD_alias { ty : Type; }
    | TD_external { extern_name : string; }
    | TD_interface { methods : list (Class_member); }   // only iface_member
    | TD_variant { decls : list (Type_decl); }       // only variant_option
    | TD_variant_option { ref decls : list (Class_member); }
  ]

variant Fun_kind =
  [
    | FK_method { f_implements : list (long_id); }
    | FK_bound_method { f_implements : list (Decl); }
    | FK_ctor
    | FK_static_ctor
    | FK_function
    | FK_iface_method { new_flag : bool; }
  ]

variant Fun_body =
  [
    | FB_expr { expr : Parsetree.Expr; }
    | FB_typed_expr { expr : Typedtree.Expr; }
    | FB_extern { name : string; }
    | FB_abstract  // for interafce method
  ]

variant Val_kind =
  [
    | Val_normal
    | Val_extern { name : string; }
  ]

variant Class_member extends Decl_base =
  [
    | M_type { td : Type_decl; }
    | M_value { ty : Type; is_ref : bool; kind : Val_kind; }
    | M_field { ty : Type; is_ref : bool; }
    | M_function { 
        fun_header : Fun_header;
        ref fun_kind : Fun_kind;
        ref body : Fun_body;
      }
  ]

class Type_base
  {
    as_string () : string = 
      Tyutil.string_of_type (Tyvars.global_expand ((this :> Type)));
  }
  
variant Type extends Type_base =
  [
    | T_app {
        tycon : long_id;
        args : list (Type);
        ref ref_to : int;
      }
    | T_var { 
        name : id; 
        ref ref_to : int; 
      }
    | T_ref { t : Type; }
    | T_out { t : Type; }
    | T_fun { ref from : list (Type); to : Type; }
    | T_void
    | T_prod { args : list (Type); }
  ]

class Fun_parm extends Named
  {
    ty : Type;
    ref decl : Decl;
    this (loc : Location, name : string, ty : Type) 
      {
        this#loc <- loc;
        this#name <- name;
        this#ty <- ty;
      }
    this () {}
  }
  
class Fun_header extends Named
  {
    id : int;
    ret_type : Type;
    parms : list (Fun_parm);
    typarms : Typarms;
    this () {}
    this (loc : Location, name : string, ret_type : Type, parms : list (Fun_parm)) 
      {
        this#id <- Util.next_id ();
        this#typarms <- Typarms (Nil (), Nil ());
        this#loc <- loc;
        this#name <- name;
        this#ret_type <- ret_type;
        this#parms <- parms;
      }
  }

variant Literal =
  [
    | L_void
    | L_null
    | L_int { val : int; }
    | L_string { val : string; }
    | L_float { val : float; }
  ]

 
} // Nemerle.Compiler

