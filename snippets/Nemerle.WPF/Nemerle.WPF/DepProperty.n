using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;

namespace Nemerle.WPF
{
  [Nemerle.MacroUsage (Nemerle.MacroPhase.BeforeInheritance,
                       Nemerle.MacroTargets.Property,
                       Inherited = false, AllowMultiple = false)]
  public macro DepProperty(tb : TypeBuilder, pp : ParsedProperty, metadata)
  {
    DepPropertyImpl.Impl(tb, pp, metadata);
  }

  internal module DepPropertyImpl
  {
    public Impl(tb : TypeBuilder, pp : ClassMember.Property, metadata : PExpr) : void
    {
      def isPublic(attributes)
      {
        def test(a1, a2) { (a1 & a2) == a2 }
        if (attributes == NemerleAttributes.None)
          test(pp.Attributes, NemerleAttributes.Public);
        else
          test(attributes, NemerleAttributes.Public);
      }

      def getter = match (pp.get) { | Some(fn) => fn; | _ => Message.FatalError (pp.Location, "No getter"); }
      def setter = match (pp.set) { | Some(fn) => fn; | _ => Message.FatalError (pp.Location, "No setter"); }

      when (!isPublic(getter.Attributes))
        Message.FatalError (pp.Location, "Getter must be public");

      def name    = pp.Name + "Property";
      def keyName = pp.Name + "PropertyKey";

      def propType     = pp.prop_ty;
      def registerArgs = [<[ $(pp.Name : string) ]>, <[ typeof($propType) ]>, <[ typeof($(tb.ParsedTypeName)) ]>, metadata];

      def isReadOnly = !isPublic(setter.Attributes);

      getter.Body = <[ this.GetValue($(name : dyn)) :> $propType ]>;
      setter.Body = if (isReadOnly)
      {
        tb.Define(<[decl:
          private static $(keyName : dyn) : System.Windows.DependencyPropertyKey = System.Windows.DependencyProperty.RegisterReadOnly(..$registerArgs);
        ]>);
        tb.Define(<[decl:
          public static $(name : dyn) : System.Windows.DependencyProperty = $(keyName : dyn).DependencyProperty;
        ]>);

        <[ this.SetValue($(keyName : dyn), $("value" : dyn)) ]>;
      }
      else
      {
        tb.Define(<[decl:
          public static $(name : dyn) : System.Windows.DependencyProperty = System.Windows.DependencyProperty.Register(..$registerArgs);
        ]>);

        <[ this.SetValue($(name : dyn), $("value" : dyn)) ]>;
      }
    }
  }
}
