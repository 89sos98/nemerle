using Nemerle.Collections;
using Nemerle.Peg;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle;

using System;
using System.Collections.Generic;
using LRPEGCC;

namespace Calculator
{
  type LoopTokens = NToken * int;
  
  [Record] public class ParserFatalError : Exception
  {
    public Pos     : int;
  }
  
  public class FakeGrammar { }
  
  /// <summary>
  /// Description of CalcParser.
  /// </summary>  
  [PegGrammar(start,
  grammar
  {
    using Calculator.FakeGrammar;
    using A = Calculator.FakeGrammar;
    [Extensible(aaa)]
    xxx;
    any                   = ['\u0000'..'\uFFFF'];
    digit                 = ['0'..'9']+;
    spaces : void         = ' '*;
    
    num                   : int = digit spaces;
    unaryMinus            : int = '-' spaces simplExpr;
    parenthesesExpr       : int = '(' spaces sumOrSub ')' spaces;
    parenthesesExprError  : int = '(' spaces sumOrSub (&any / !any);
    [Extensible(aaa)]
    simplExpr             : int = num / parenthesesExpr / unaryMinus / parenthesesExprError / simplExprError;
    simplExprError        : int = any;
    inputError            : int = any;
    mulOrDiv              : int = simplExpr (('*' / '/') spaces simplExpr)*;
    sumOrSub              : int = mulOrDiv  (('+' / '-') spaces mulOrDiv )*;
    mainRule              : int = sumOrSub inputError?;
    start                 : int = spaces mainRule !any;
  })]
  public class CalcParser
  {    
//Requred for parser --------------------------------------------------------------------------------------------    
    
    //TODO: beter readability of tokens
    private num(digit : NToken) : int
    {
      int.Parse(GetText(digit))
    }
    
    private unaryMinus(_ : NToken, se : int) : int
    {
      -se
    }
    
    private parenthesesExpr(_ : NToken, se : int, _ : NToken) : int
    {
      se
    }
    
    private parenthesesExprError(_ : NToken, _ : int) : int
    {
      throw ParserFatalError("Ожидается закрывающая скобка или '+', '-', '*', '/' за которым следует число или выражение", this.MaxRollbackPos);
    }
    
    private inputError(tok : NToken) : int
    {
      throw ParserFatalError("Ожидается '+', '-', '*', '/' за которым следует число или выражение", tok.StartPos);
    }
    
    private simplExprError(tok : NToken) : int
    {
      throw ParserFatalError("Ожидается число или выражение в скобках", tok.StartPos);
    }
    
    private mainRule(se : int, _ : option[int]) : int
    {
      se
    }

    // TODO: autogenerate this block
    private simplExpr(se : int) : int
    {
      se
    }
    // TODO: end todo
    
    private start(se : int) : int
    {
      se
    }
    
    private mulOrDiv(se : int, lst : List[LoopTokens]) : int
    {
      DoOpHelper(se, lst)
    }
    
    private sumOrSub(se : int, lst : List[LoopTokens]) : int
    { 
      DoOpHelper(se, lst)
    }
     
//End requred for parser --------------------------------------------------------------------------------------------    

    private DoOpHelper(se : int, lst : List[LoopTokens]) : int
    {
      def doOp(x : int, y : int, op : string) : int
      {
        match (op)
        {
          | "*" => x * y
          | "/" => x / y
          | "+" => x + y
          | "-" => x - y
          | _     => assert(false);
        }
      }
           
      mutable r = se;
      
      foreach ((opTok, secondTok) in lst)
        r = doOp(r, secondTok, GetText(opTok));
    
      r  
    }
  }
}
