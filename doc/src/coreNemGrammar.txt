#
# Copyright (c) 2003 The University of Wroclaw.
# All rights reserved.
#
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions
# are met:
#    1. Redistributions of source code must retain the above copyright
#       notice, this list of conditions and the following disclaimer.
#    2. Redistributions in binary form must reproduce the above copyright
#       notice, this list of conditions and the following disclaimer in the
#       documentation and/or other materials provided with the distribution.
#    3. The name of the University may not be used to endorse or promote
#       products derived from this software without specific prior
#       written permission.
# 
# THIS SOFTWARE IS PROVIDED BY THE UNIVERSITY ``AS IS'' AND ANY EXPRESS OR
# IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES
# OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN
# NO EVENT SHALL THE UNIVERSITY BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
# SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED
# TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR
# PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF
# LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING
# NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS
# SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
#

compilation_unit:
	{ toplevel_declaration }

toplevel_declaration:
	"open" qualified_identifier ";"
		Add qualified_identifier to namespace search path.
	"namespace" IDENTIFIER "=" qualified_identifier ";"
		Define short name (an alias) for namespace.
	"namespace" qualified_identifier "{" { top_declaration } "}"
		Declare symbols in specified namespace.
	type_declaration
		Declare toplevel type.

type_header:
	IDENTIFIER [ type_parameters ] [ "extends" type ] [ "implements" type { "," type } ]
	
type_parameters:
	"(" TYPE_VARIABLE { "," TYPE_VARIABLE } ")" [ where_constraints ]

method_type_parameters:
	TYPE_VARIABLE { "," TYPE_VARIABLE } [ where_constraints ]
		Type parameters for methods (just different syntax then in types).
	
where_constraints:
	"where" TYPE_VARIABLE ":>" type { "," TYPE_VARIABLE ":>" type }
	
type_declaration:
	attributes "type" type_header "=" type ";"
		An alias for existing type.
	attributes "type" type_header "=" "extern" STRING_LITERAL ";"
		External type import. To be removed soon.
	attributes "interface" type_header "{" { interface_member } "}"
	attributes "class" type_header "{" { type_member } "}"
	attributes "module" type_header "{" { type_member } "}"
		Class with all members implicitly static.
	attributes "variant" type_header "{" { "|" variant_option } "}"
	
identifier_or_dummy:
	IDENTIFIER
	"_"

qualified_identifier:
	IDENTIFIER { "." IDENTIFIER }

interface_member:
	[ "new" ] method_header ";"
        
variant_option:
	IDENTIFIER [ "{" { field_def } "}" ]
	
attributes:
	{ attribute }

attribute:
	"new"
	"public"
	"protected"
	"internal"
	"private"
	"abstract"
	"sealed"
	"static"

field_definition:
	attributes [ "mutable" ] IDENTIFIER ":" type ";"

type_member:
	attributes IDENTIFIER ":" type [ "=" "extern" STRING_LITERAL ] ";"
	attributes method_header method_body
	type_declaration
	
method_header:
	[ method_type_parameters ] IDENTIFIER "(" method_parameters ")" ":" type [ "implements" qualified_identifier { "," qualified_identifier } ]
	[ method_type_parameters ] "this" "(" method_parameters ")"

method_body:
	"=" "extern" STRING_LITERAL ";"
	block

ref_or_out:
	"ref"
	"out"
        
method_parameter:
	identifier_or_dummy ":" type

method_parameters:
	[ method_parameters { "," method_parameter } ]

primary_type:
	qualified_identifier [ "(" type { "," type } ")" ]
	TYPE_VARIABLE
	"(" type ")"
		Grouping.
	"void"
		Type with exactly one inhibiting value.
	"ref" primary_type
	"out" primary_type

type:
	primary_type { "*" primary_type }
		Tuple type.
	primary_type { "->" primary_type }
		Type of functions.

primary_expr:
	qualified_identifier
		Value reference.
	"this"
		Reference parent type instance in method.
	"value"
		Used in properties and ensure block.
	"true"
	"false"
	"(" expr ")"
		Allow grouping.
	"null"
		Polymorphic constant.
	"(" ")"
		The only value of type void.
	STRING_LITERAL
	NUMBER_LITERAL
	CHARACTER_LITERAL
	"(" expr ":>" type ")"
		Type cast.
	"(" expr ":" type ")"
		Type enforcement.
	primary_expr "." IDENTIFIER
		Member reference.
	"(" expr {+ "," expr }+ ")"
		Tuple constructor.
	expr "[" expr { "," expr } "]"

expr_in_sequence:
	expr ";"
	declarative_expr
	
sequence:
	expr_in_sequence { ";" expr_in_sequence } [ ";" ]
		All expression but the last one ought to have void type.

block:
	"{" sequence "}"

declarative_expr:
	"def" identifier_or_dummy "=" expr ";"
		Define new value. It only make sense to use "def" (or "mutable" for that
		matter) in sequence.
	"def" tuple_pattern "=" expr ";"
		Special form of pattern matching.
	"def" method_header block { "and" method_header block }
		Define several, possibly mutually recursive, local functions.
	"require" block
	"ensure" block
	"mutable" IDENTIFIER "<-" expr ";"
		Define new mutable value.

expr:
	primary_expr
	primary_expr "(" [ parameter { "," parameter } ] ")"
		Function call.
	primary_expr "<-" expr
		Assignment to mutable value (local mutable value, mutable
		type member, or array member).
	"if" "(" expr ")" expr "else" expr
	"match" "(" expr ")" "{" [ "|" ] match_case { "|" match_case } "}"
	"raise" expr
		Raise exception.
	"try" expr "with" IDENTIFIER ":" type "=>" expr
		Try compute first expression. If specified exception is raised -- catch it.
		Exception value is then bound to specified IDENTIFIER.
	"try" expr "finally" expr
		Try compute first expression. No matter how control leaves it, execute second
		expression.
	expr OPERATOR expr
		Operators are defined in standard library.
	OPERATOR expr
	expr "::" expr
		Short syntax for Cons.
	"[" [ { expr ";" } expr [ ";" ] ] "]"
		Short syntax for few Cons.
	block
	[ method_type_parameters ] "fun" "(" method_parameters ")" ":" type block
		Syntactic sugar for function definition.
	"array" "[" [ { expr ";" } expr [ ";" ] ] "]"
	expr "&&" expr
	expr "||" expr

parameter:
	[ "ref" ] expr
	[ "ref" ] IDENTIFIER "=" expr

pattern:
	IDENTIFIER
		Lowercase simple IDENTIFIER is pattern variable.
	qualified_identifier [ pattern ]
		Uppercase, possibly qualified, IDENTIFIERs are expected to be
		variant constructors.
	"_"
		Match-all pattern.
	"{" IDENTIFIER "=" pattern { ";" IDENTIFIER "=" pattern } [ ";" ] "}"
		Decompose record.
	tuple_pattern
		Decompose tuple.
	"(" pattern ")"
		A 1-tuple pattern (singleton). It is not grouping.
	pattern "::" pattern
		List pattern (Cons).
	"[" [ { pattern ";" } pattern [ ";" ] ] "]"
		List pattern (for constant-length lists).
	STRING_LITERAL
	NUMBER_LITERAL
	CHARACTER_LITERAL
	"true"
	"false"

tuple_pattern:
	"(" pattern {+ "," pattern }+ ")"

match_case:
	pattern "=>" sequence

# vim: ai ft=conf ts=4
