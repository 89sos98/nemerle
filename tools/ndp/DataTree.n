using System;
using Nemerle.Collections;


module HtmlGenerator
{
  public Page (f : void -> string, fname : string) : void
  {
    def buf = System.Text.StringBuilder();
    _ = buf.Append("<?xml version=\"1.0\"?>\n");
    _ = buf.Append("<!DOCTYPE html PUBLIC \"-//W3C//DTD XHTML 1.0 Strict//EN\"\n");
    _ = buf.Append("  \"http://www.w3.org/TR/xhtml1/DTD/xhtml1-strict.dtd\">\n");
    _ = buf.Append("<html xmlns=\"http://www.w3.org/1999/xhtml\" lang=\"en\" xml:lang=\"en\">\n");
    _ = buf.Append("<head>\n");
    _ = buf.Append("<title>Nemerle Documentation Project</title>\n");
    _ = buf.Append("<meta http-equiv=\"Content-Type\" content=\"text/html; charset=utf-8\" />\n");
    _ = buf.Append("<link rel=\"stylesheet\" type=\"text/css\" href=\"nemerle-doc.css\" />\n");
    _ = buf.Append("</head>\n");
    _ = buf.Append("<body>\n");
    _ = buf.Append(f());
    _ = buf.Append("<div class=\"footer\">Nemerle Documentation Project</div>");
    _ = buf.Append("</body>\n");
    _ = buf.Append("</html>\n");

    def outf = IO.StreamWriter(fname);
    outf.Write(buf);
    outf.Close();
    // Console.WriteLine("---> zapis do {0}", fname);
  }

  public Title (title : string, subtitle : string) : string
  {
    mutable content = "";
    content += "<div class=\"title\">";
    content += "<span class=\"title\">" + title + "</span><br />";
    content += "<span class=\"subtitle\">";
    content += subtitle;
    content += "</span>";
    content += "</div>\n\n";
    content;  
  }

}

variant ElementType
{
  | Unknown
  | Property
  | Method
  | Field
  | Type
}

class TopNode
{
  
  internal mutable elements : list [ Node ] = [];
    
  /// <summary>A reference on a html page</summary>
  public href : string = this.ToString() + this.GetHashCode().ToString();
  
  internal mutable current : Node;
  
  internal mutable comment : string = "";
  internal mutable remark : string = "";
  internal mutable return : string = "";
  internal mutable param : string = "";
  internal mutable attr : Reflection.TypeAttributes;
  internal mutable info : string;
  internal parent : TopNode;
  
  
  internal page_content (elements : list [ Node ], ns : string) : string
  {
    mutable res = "";
    res += "<div class=\"ns-index\">\n";
    res += "<span class=\"ns-index\" id=\"" + this.href + "\">Namespace " + (if (ns.Length ==0) "?Empty?" else ns) +"</span>\n";
    foreach (e in elements)      
      when (e.elemType.Equals(ElementType.Type()) || e.elemType.Equals(ElementType.Unknown()))
      {
        def ahref = match (e.elemType) { | Unknown => "#" + e.href | _ => e.html_file + "#" + e.href };
        res += "<div title=\"" + e.full_name + "\" class=\"index-element\">";
        res += match (e.elemType) { | Type => "class" | Unknown => "namespace" | _ => e.elemType.ToString() };
        res += " ";
        res += "<a href=\"" + ahref + "\">" + e.name + "</a>";
        res += if (comment.Length > 0) "<div class=\"komentarz\">Comment: " + comment + "</div>" else "";
        res += if (remark.Length > 0) "<div class=\"uwaga\">Remark: " + remark + "</div>" else "";
        res += "</div>\n";
      }
    res += "</div>\n";
    res
  }
  
  public virtual ToIndex (content : ref string) : void
  {
    // first group types in 'anonymous' namespace
    mutable is_anonymous_ns = false;
    foreach (e in elements)      
      when (e.elemType.Equals(ElementType.Type())) is_anonymous_ns = true;
    
    // add elements in anonymous
    when (is_anonymous_ns)
    {
      content += page_content(this.elements, "");
    }
        
    foreach (e in elements)
       when (e.elemType.Equals(ElementType.Unknown())) e.ToIndex(ref content)
  }
  
  public virtual infoPages () : void
  {       
    foreach (e in elements)
        when (e.elemType.Equals(ElementType.Unknown())) e.infoPages();    
  }
  
  public Comment(str : string) : void { 
    current.comment += str;
  }
  
  public Remark(str : string) : void { 
    current.remark += str;
  }
  
  public Return(str : string) : void { 
    current.return += str;
  }
  
  public Param(str : string) : void { 
    current.param += str;
  }   
  
}


class Node : TopNode
{
  public mutable elemType : ElementType = ElementType.Unknown();
  /// <summary>Name of the element</summary>
  public mutable name : string;
  /// <summary>Full name (with namespaces path)</summary>
  public mutable full_name : string = "";
  
  public html_file : string {
    get { if (parent is Node) (parent :> Node).full_name + ".html" else "index.html"}
  
  }
  
  public override ToIndex (content : ref string) : void
  {
    when (this.elemType.Equals(ElementType.Unknown()))
    {
      // Console.WriteLine("Podstrona typu {0}, nazwa {1}", this.elemType, this.full_name);
      // self-explanation
      content += page_content(this.elements, this.full_name);
    
      foreach (e in elements)
        when (e.elemType.Equals(ElementType.Unknown())) e.ToIndex(ref content);
    }
  }

  public override infoPages () : void
  {    
    // create page for current namespace
    // Console.WriteLine("Zapis pliku {0}", this);
    when (this.elemType.Equals(ElementType.Unknown()))
    {
      def fname = this.full_name ;
      mutable content = "";
      content += HtmlGenerator.Title("Nemerle Library", "Namespace " + fname);
      foreach (e in elements)
        content += e.ToHtml();
      HtmlGenerator.Page ( fun () { content }, fname + ".html");
    }
   
    foreach (e in elements)
      when (e.elemType.Equals(ElementType.Unknown())) e.infoPages();    
  }
  

  public this(name : string, prefiks : string, parent : TopNode) {
      this.name = name;
      this.full_name = prefiks;
      this.parent = parent;
      // Console.WriteLine("Nowy element Name " + prefiks);
  }
  
  
  
  filteredIter (l : list [ Node ], cond : Node -> bool, iter : Node -> string) : string
  {
    mutable res = "";
    foreach (e in l) when (cond(e)) res += iter(e);
    res;
  }
  
  public ToHtml () : string
  {
    mutable res = "";
    mutable com = if (comment.Length > 0) "<div class=\"komentarz\">" + comment + "</div>" else "";
    com += if (remark.Length > 0) "<div class=\"uwaga\">Remark: " + remark + "</div>" else "";
    com += if (return.Length > 0) "<div class=\"return\">Return: " + return + "</div>" else "";
    com += if (param.Length > 0) "<div class=\"param\">Parameter: " + param + "</div>" else "";
    
    /*
    mutable att = this.info;
    when (!this.attr.Equals(null))
        att += " " + this.attr.ToString() + " ";
    att += " ";
    */
    def att = "";
    
    match (elemType) {
      | Unknown => 
          /**
          res += att + "<span class=\"namespace\" title=\"" + this.full_name + "\">" + name + "</span>\n";
          res += com;
          res += filteredIter(elements, fun (e) { e.elemType.Equals(ElementType.Method()) }, fun (e) { e.ToHtml() });
          res += filteredIter(elements, fun (e) { e.elemType.Equals(ElementType.Property()) }, fun (e) { e.ToHtml() });
          res += filteredIter(elements, fun (e) { e.elemType.Equals(ElementType.Field()) }, fun (e) { e.ToHtml() });
          res += filteredIter(elements, fun (e) { e.elemType.Equals(ElementType.Type()) }, fun (e) { e.ToHtml() });
          // foreach (e in elements) when (!e.elemType.Equals(ElementType.Unknown())) res += e.ToHtml();
          **/
          res = "";
      | Property => res += "Property: " + att + name + com;
      | Method => res += "Method: " + att + name + com;
      | Field => res += "Field: " + att + name + com;
      | Type =>
          res += "<div class=\"ns-index\"" + ("id=\"" + this.href + "\"") + ">Class " + att;
          res += "<span class=\"ns-index\" title=\"" + this.full_name + "\">" + name + "</span>\n";
          res += com;
          mutable components = "";
          components += filteredIter(elements, fun (e) { e.elemType.Equals(ElementType.Method()) },   fun (e) { "<li>" + e.ToHtml() + "</li>\n"});
          components += filteredIter(elements, fun (e) { e.elemType.Equals(ElementType.Property()) }, fun (e) { "<li>" + e.ToHtml() + "</li>\n"});
          components += filteredIter(elements, fun (e) { e.elemType.Equals(ElementType.Field()) },    fun (e) { "<li>" + e.ToHtml() + "</li>\n"});
          components += filteredIter(elements, fun (e) { e.elemType.Equals(ElementType.Type()) },     fun (e) { "<li>" + e.ToHtml() + "</li>\n"});
          when (components.Length > 0)
            res += "<ul>\n" + components + "</ul>\n";
          res += "</div>\n";
    }
    res  
  }
}

// -----------------------------------------------
class DataTree
{
  split (str : string) : char*string { (str[0], str.Substring(2)) }
  
  mutable tree : TopNode = TopNode();
  mutable sources : list [ string ] = [];
  
  public sourcesToString () : string
  {
    mutable res = "";
    res += 
        match (sources.Length) {
              | 0 => "No sources?"
              | 1 => "Source: "
              | _ => "Sources:<br /> "
        }
    res += List.Head(sources);
    def r = List.Tail(sources);
    foreach (e in r) res += ", " + e;
    res
  }
  
  public AddSource(src : string) : void
  {
    sources += [ src ]
  }     
  
  smart_strip(path : string) : list [ string ]
  {
    // first split into name and parameters
    def par_pos = path.IndexOf('(');
    def path_name = if (par_pos != -1) path.Substring(0, par_pos) else path;
    def param = if (par_pos != -1) path.Substring(par_pos) else "";
    
    // Console.WriteLine("{0} = ({1},{2})", path, path_name, param);
    
    mutable token_list = Nemerle.Collections.List.FromArray(path_name.Split(array ['.']));
    mutable res = [];
    // analyze name
    while (token_list.Length > 1)
    {
      mutable r = List.Hd(token_list);
      
      /// when not .., i.e. ctor
      when (r.Length != 0)
      {
        res += [ r ];
        // Console.WriteLine("<-- " + r);
      }
      when (token_list.Length > 0) token_list = List.Tl(token_list)
    } // while ()
    def last = List.Hd(token_list);
    def last_token = if (par_pos > -1) (last + param) else last;
    res += [ last_token ];
    res
  }
  
  memb (set : TopNode, name : string) : Node
  {
    mutable res = null;
    foreach (e in set.elements) when (name.Equals(e.name)) res = e;
    res;
  }
  
  public AddItem(str : string, attr : System.Reflection.TypeAttributes, info : string) : void
  {
    def path = str.Split(array ['.']);
    mutable pointer = tree;
    mutable next;
    foreach (edge in path)
    {    
      // Console.WriteLine("ścieżka " + edge);
      next = memb(pointer, edge);
      when (next == null)
      {
        def pref = if (pointer is Node) ((pointer :> Node).full_name + "." + edge) else edge;
        next = Node(edge, pref, pointer);
        pointer.elements = pointer.elements + [ next ];
      }
      pointer = next;
    }
    pointer.attr = attr; 
    pointer.info = info; 
  }
  
  public AddItem (str : string) : void
  {
    // System.Console.WriteLine("---> " + str);
    def (tySy, path) = split(str);
    // System.Console.WriteLine("---> split({0}) = ({1}, {2})", str, tySy, path);
    def ty = 
      match (tySy) {
        | 'T' => ElementType.Type();
        | 'F' => ElementType.Field();
        | 'M' => ElementType.Method();
        | 'P' => ElementType.Property()
        | _   => ElementType.Unknown()
      }
      
    def pathList = smart_strip(path);
    mutable pointer = tree;
    mutable next;
    foreach (edge in pathList)
    {    
      next = memb(pointer, edge);
      when (next == null)
      {
        def pref = if (pointer is Node) ((pointer :> Node).full_name + "." + edge) else edge;
        next = Node(edge, pref, pointer);
        pointer.elements = pointer.elements + [ next ];
      }
      pointer = next;
    }
    tree.current = next;
    tree.current.elemType = ty;
    // Console.WriteLine("Added {0} {1}", tree.current.full_name, tree.current.elemType);
  }
    
  public AddComment (str : string) : void 
  {
    tree.Comment(str);
  }
  
  public AddRemark (str : string) : void 
  {
    tree.Remark(str);
  }
  
  public AddReturn (str : string) : void 
  {
    tree.Return(str);
  }
  
  public AddParam (str : string) : void 
  {
    tree.Param(str);
  }
  
  
  index_content () : string
  {
    mutable content = "";
    content += HtmlGenerator.Title("Nemerle Library", this.sourcesToString());
    // teraz zawartość
    
    tree.ToIndex(ref content);
    content;
  }
  
  
  public indexPage() : void
  {
    HtmlGenerator.Page(index_content, "index.html");
  }
  
  public infoPages () : void
  {
    tree.infoPages();
  }
  
}

