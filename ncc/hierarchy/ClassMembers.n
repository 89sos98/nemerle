 /*
 * Copyright (c) 2003-2005 The University of Wroclaw.
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions
 * are met:
 *    1. Redistributions of source code must retain the above copyright
 *       notice, this list of conditions and the following disclaimer.
 *    2. Redistributions in binary form must reproduce the above copyright
 *       notice, this list of conditions and the following disclaimer in the
 *       documentation and/or other materials provided with the distribution.
 *    3. The name of the University may not be used to endorse or promote
 *       products derived from this software without specific prior
 *       written permission.
 * 
 * THIS SOFTWARE IS PROVIDED BY THE UNIVERSITY ``AS IS'' AND ANY EXPRESS OR
 * IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES
 * OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN
 * NO EVENT SHALL THE UNIVERSITY BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
 * SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED
 * TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR
 * PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF
 * LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING
 * NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS
 * SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 */

using Nemerle.Compiler;
using Nemerle.Compiler.Tyops;
using Nemerle.Collections;
using Nemerle.Utility;
using Nemerle.Compiler.Typedtree;

using System.Reflection;

using PT = Nemerle.Compiler.Parsetree;

namespace Nemerle.Compiler {

public class NemerleMember : IMember
{
  protected loc : Location;
  protected mutable name : string;
  protected accessibility : Accessibility;
  protected modifiers : Modifiers;
  protected declaring_type : TypeBuilder;
  id : int;
  protected mutable kind : MemberKind;
  internal mutable ty : TType;
  protected mutable handle : MemberInfo;

  protected mutable attributes : NemerleAttributes;

  protected mutable m_has_been_used : bool;
  
  protected this (par : TypeBuilder, d : PT.ClassMember)
  {
    loc = d.loc;
    modifiers = d.modifiers;
    name = d.Name; 
    declaring_type = par;
    id = Util.next_id ();

    attributes = modifiers.mods;

    unless (attributes %&& NemerleAttributes.AccessModifiers)
      attributes |= NemerleAttributes.Private;

    accessibility = 
      match (attributes %& NemerleAttributes.AccessModifiers) {
        | NemerleAttributes.Private => Accessibility.Private
        | NemerleAttributes.Protected => Accessibility.Protected
        | NemerleAttributes.Internal => Accessibility.Internal
        | _ =>
          if (attributes %&& (NemerleAttributes.Protected %| NemerleAttributes.Internal))
            Accessibility.ProtectedOrInternal
          else
            Accessibility.Public
      }

    match ((par.Accessibility, accessibility)) {
      | (Accessibility.Private, _) =>
        accessibility = Accessibility.Private
      | (Accessibility.Public, _) => ()
      | (Accessibility.Internal, Accessibility.Public) =>
        accessibility = Accessibility.Internal
      | (Accessibility.Protected, Accessibility.Internal)
      | (Accessibility.Internal, Accessibility.Protected) =>
         accessibility = Accessibility.ProtectedAndInternal
      | _ => ()
    }

    // mark public and protected members explicitly used
    m_has_been_used =
      (IsPublic || IsProtected) && declaring_type.IsExternallyAccessible ||
      name.StartsWith ("_") || declaring_type.Name.StartsWith ("_") ||
      name == "value__"
  }

  public CanAccess (source : TypeInfo) : bool
  {
    def walk_declaring (tc : TypeInfo) {
      tc.Equals (declaring_type) || 
        (tc.DeclaringType != null && walk_declaring (tc.DeclaringType))
    };

    IsPublic
    || declaring_type.Equals (source)
    || (IsInternal && 
        (match (source.GetTypeInfoKind ()) { 
          | TypeInfoKind.NemerleType => true 
          | _ => false 
        }))
    || (IsProtected && Option.IsSome (source.SuperType (declaring_type)))
    || walk_declaring (source)
  }

  public DeclaringType : TypeInfo
  {
    get { declaring_type }
  }

  public Name : string
  {
    get { name }
  }

  public OverloadName : string
  {
    get { Util.ice ("OverloadName not implemented for non-external members") }
  }

  public MemberType : System.Reflection.MemberTypes {
    get {
      match (kind) {
        | MemberKind.Field => MemberTypes.Field
        | MemberKind.Method (meth) =>
            match (meth.GetFunKind ()) {
              | FunKind.Constructor
              | FunKind.StaticConstructor => MemberTypes.Constructor
              | _ => MemberTypes.Method
            }
        | MemberKind.Property => MemberTypes.Property
        | MemberKind.Type => MemberTypes.NestedType
        | MemberKind.Event => MemberTypes.Event
      }
    }
  }

  public GetMemType () : TType
  {
    ty
  }

  [Nemerle.OverrideObjectEquals]
  public Equals (o : IMember) : bool
  {
    id == o.GetId ()
  }

  public GetKind () : MemberKind
  {
    kind
  }

  public Location : Location
  {
    get { loc }
  }
  
  public Attributes : NemerleAttributes
  {
    get { attributes }
    set { attributes = value }
  }

  public GetModifiers () : Modifiers
  {
    modifiers
  }
  
  public IsStatic : bool
  {
    get { attributes %&& NemerleAttributes.Static }
  }

  public IsPublic : bool
  {
    get { attributes %&& NemerleAttributes.Public }
  }

  public IsPrivate : bool
  {
    get { attributes %&& NemerleAttributes.Private }
  }

  public IsProtected : bool
  {
    get { attributes %&& NemerleAttributes.Protected }
  }

  public IsInternal : bool
  {
    get { attributes %&& NemerleAttributes.Internal }
  }
  
  public virtual HasBeenUsed : bool
  {
    get { m_has_been_used }
    set {
      m_has_been_used = m_has_been_used || value;
      declaring_type.HasBeenUsed = value
    }
  }
  
  internal SetInstanceUsed () : void {
    m_has_been_used = true;
  }

  public GetId () : int
  {
    id
  }

  public virtual GetHandle () : System.Reflection.MemberInfo
  {
    assert (false)
  }

  public MarkWithSpecialName () : void
  {
    attributes |= NemerleAttributes.SpecialName
  }

  protected UpdateEmbeddedModifiers (parent : NemerleAttributes, child : ref NemerleAttributes) : void
  {
    when (child %&& NemerleAttributes.Static && !(parent %&& NemerleAttributes.Static))
      Message.error ("accessor cannot be static if containing entity isn't");

    // copy access modifiers only if child has none
    child |=
      (if (child %&& NemerleAttributes.AccessModifiers)
        parent %& ~NemerleAttributes.AccessModifiers;
      else
        parent)
  }

  public override ToString () : string
  {
    DescribeMember (this)
  }

  public static DescribeMember (m : IMember) : string
  {
    if (m.GetKind () == null)
      m.GetType ().ToString ()
    else {
      def kind =
        match (m.GetKind ()) {
          | MemberKind.Field => "a field"
          | MemberKind.Method => "a method"
          | MemberKind.Property => "a property"
          | MemberKind.Type => "a type"
          | MemberKind.Event => "an event"
        };
      def full_name =
        if (m.DeclaringType == null)
          m.Name
        else
          m.DeclaringType.FullName + "." + m.Name;
      match (m.GetKind ()) {
        | MemberKind.Method (m) =>
          def parms = List.Map (m.GetHeader ().parms, 
                                fun (p : Fun_parm) { 
                                    p.name + " : " + p.ty.ToString () 
                                });
          def ret_ty = m.GetHeader ().ret_type.ToString ();
          kind + " " + full_name + "(" + NString.Concat (", ", parms) + ") : " + ret_ty
        | _ =>
          kind + " `" + full_name + "'"
      }
    }
  }

  // make sure only a valid subset of NemerleAttributes has been used
  protected check_for_invalid_attr (attr : NemerleAttributes, attr_name : string) : void
  {
    when (attributes %&& attr)
      Message.fatal_error (loc, "invalid attribute `" + attr_name +
                           "' specified for " + ToString ())                           
  }
  
  /**
   * Performs the attributes checks that are common to methods and properties
   */
  protected check_method_like_attributes (title : string,
                                          title_plural : string) : void
  {                                               
    check_for_invalid_attr (NemerleAttributes.Mutable, "mutable");
    check_for_invalid_attr (NemerleAttributes.Struct, "struct");
    check_for_invalid_attr (NemerleAttributes.Macro, "macro");
    check_for_invalid_attr (NemerleAttributes.Volatile, "volatile");

    // check the access attributes for consistency
    match (TypeBuilder.CheckAccessAttributes (attributes)) {
      | Some (msg) => Message.fatal_error (loc, $"$msg for $title: $(this)")
      | _ => ()
    }
    
    def mem_is_sealed = attributes %&& NemerleAttributes.Sealed;
    def mem_is_override = attributes %&& NemerleAttributes.Override;
    def mem_is_virtual = attributes %&& NemerleAttributes.Virtual;    
    def mem_is_new = attributes %&& NemerleAttributes.New;
    def mem_is_abstract = attributes %&& NemerleAttributes.Abstract;                

    // make sure 'virtual', 'new' and 'override' never appear at once 
    when (mem_is_override && mem_is_new)
      Message.fatal_error (loc, $"both `override' and `new' attributes specified"
                           " for $title: $(this)");

    // only allow 'override' methods/properties/events to be 'sealed'
    when (mem_is_sealed && !mem_is_override)
      Message.fatal_error (loc, $"only `override' $title are allowed to be `sealed': $(this)");
                           
    // do not allow new virtual methods/properties/events in sealed clases
    when (declaring_type.IsSealed && mem_is_virtual && !mem_is_override && !declaring_type.IsDelegate)
      Message.fatal_error (loc, Name +
                           " is a new virtual member in a sealed class " +
                           declaring_type.FullName);

    // do not allow private abstract methods
            
    when (mem_is_abstract && IsPrivate)
      Message.fatal_error (loc, "abstract " + title_plural + " are not allowed to be private: " +
                           ToString ());      

    // do not allow private 'virtual' or 'override' methods/properties/events
    when (mem_is_virtual && IsPrivate)
      Message.fatal_error (loc, "virtual " + title_plural + " are not allowed to be private: " +
                           ToString ());      
    
    when (mem_is_override && IsPrivate)
      Message.fatal_error (loc, $"override $title_plural are not allowed to be private: $(this)");
                           
    // do not allow to mix abstract with the 'override' modifier
    when (mem_is_abstract && mem_is_override)
      Message.fatal_error (loc, $"abstract $title_plural are not allowed to have the "
                           "'override' modifier: $(this)");      

    // static methods/properties cannot be abstract, virtual or override
    when ((mem_is_abstract || mem_is_virtual || mem_is_override) && IsStatic)
      Message.fatal_error (loc, $"static $title_plural are not allowed to have the "
                           "`virtual' or `override' modifier: $(this)");      

    // do not allow protected and protected internal methods in structures
    when (declaring_type.IsStruct && IsProtected)
    {
      def msg = if (IsInternal) "protected internal" else "protected";
          
      Message.fatal_error (loc, $"$title_plural defined in a struct are not"
                           " allowed to be $msg: $(this)")
    }
  }
}

public class NemerleField : NemerleMember, IField
{
  public mutable field_builder : Emit.FieldBuilder;
  public mutable enum_value : object;
  
  public this (par : TypeBuilder, f : PT.ClassMember.Field) {
    base (par, f);
    ty =
      if (IsStatic)
        par.BindType (Tyvar.TEnv (), f.ty)
      else
        par.BindType (f.ty);
    kind = MemberKind.Field (this);
    Tyutil.CheckAccessibility (this, accessibility, ty);
  }

  public IsMutable : bool
  {
    get { attributes %&& NemerleAttributes.Mutable }
  }

  public IsVolatile : bool
  {
    get { attributes %&& NemerleAttributes.Volatile }
  }
  
  public IsLiteral : bool
  {
    get
    {
      match (DeclaringType.GetTydecl ()) {
        | TypeDeclaration.Enum when IsStatic => true
        | _ => false
      }
    }
  }

  public GetValue () : object
  {
    assert (IsLiteral, Name);
    enum_value
  }

  public GetFieldInfo () : FieldInfo
  {
    assert (field_builder != null, Name);
    field_builder
  }
  
  override public GetHandle () : System.Reflection.MemberInfo
  {
    assert (field_builder != null, Name);
    field_builder
  }
  
  /**
   * Performs attributes checks for field definition.
   */
  internal CheckAttributes () : void
  {
    check_for_invalid_attr (NemerleAttributes.Abstract, "abstract");
    check_for_invalid_attr (NemerleAttributes.Virtual, "virtual");
    check_for_invalid_attr (NemerleAttributes.Sealed, "sealed");
    check_for_invalid_attr (NemerleAttributes.Override, "override");
    check_for_invalid_attr (NemerleAttributes.Struct, "struct");
    check_for_invalid_attr (NemerleAttributes.Macro, "macro");
    
    // check the access attributes for consistency
    match (TypeBuilder.CheckAccessAttributes (attributes)) {
      | Some (msg) => Message.fatal_error (loc, $"$msg for $(this)")
      | _ => ()
    }

    // check for non-mutable / volatile consistency
    when (!IsMutable && IsVolatile)
      Message.fatal_error (loc, $"only mutable fields are allowed to be volatile in $(this)");

    // do not allow protected and protected internal methods in structures
    when (declaring_type.IsStruct && IsProtected)
    {
      def msg = if (IsInternal) "protected internal" else "protected";
          
      Message.fatal_error (loc, "fields defined in a struct are not allowed to be " +
                           msg + "in " + ToString ())
    }
  }
}

public class NemerleProperty : NemerleMember, IProperty
{
  is_mutable : bool;
  getter : NemerleMethod;
  setter : NemerleMethod;
  internal parms : list [TType];
  mutable parent_property : IProperty = null;
  
  internal mutable property_builder : Emit.PropertyBuilder;
  
  public this (par : TypeBuilder, f : PT.ClassMember.Property) {
    base (par, f);
    is_mutable = Option.IsSome (f.set);

    ty =
      if (IsStatic)
        par.BindType (Tyvar.TEnv (), f.prop_ty)
      else
        par.BindType (f.prop_ty);

    kind = MemberKind.Property (this);

    parms = 
      List.Map (f.dims, fun (parm : PT.Fun_parm) {
        par.BindType (parm.ty)
      });

    match (f.get) {
      | Some (fn : PT.ClassMember.Function) =>
        UpdateEmbeddedModifiers (attributes, ref fn.modifiers.mods);
        getter = NemerleMethod (par, fn, is_property = true);
        getter.MarkWithSpecialName ();        
        declaring_type.AddMember (getter);
        
      | Some => Util.ice ("property getter was set to other class member than ClassMember.Function")
      | None => ()
    };
    match (f.set) {
      | Some (fn : PT.ClassMember.Function) =>
        UpdateEmbeddedModifiers (attributes, ref fn.modifiers.mods);        
        setter = NemerleMethod (declaring_type, fn, is_property = true);
        setter.MarkWithSpecialName ();
        declaring_type.AddMember (setter);
        
      | Some => Util.ice ("property setter was set to other class member than ClassMember.Function")
      | None => ()
    };

    Tyutil.CheckAccessibility (this, accessibility, ty);

    // we'll check for the get/set usage methods instead
    m_has_been_used = true
  }

  public IsMutable : bool
  {
    get { is_mutable }
  }

  public IsIndexer : bool
  {
    get
    {
      !(parms matches [])
    }
  }
  
  public override HasBeenUsed : bool {
    set { 
      base.HasBeenUsed = value;
      // FIXME: only one of them should be used, but typing knows it a 
      //        little bit too late
      when (setter != null) setter.HasBeenUsed = value;
      when (getter != null) getter.HasBeenUsed = value;
    }
  }
  
  public GetPropertyInfo () : PropertyInfo
  {
    assert (property_builder != null);
    property_builder
  }

  public GetGetter () : IMethod
  {
    if (getter == null && parent_property != null)
      parent_property.GetGetter ()
    else
      getter
  }
  
  public GetSetter () : IMethod
  {
    if (setter == null && parent_property != null)
      parent_property.GetSetter ()
    else
      setter
  }

  public UpdateParentProperty (parent_property : IProperty) : void
  {
    this.parent_property = parent_property;
  }
  
  override public GetHandle () : System.Reflection.MemberInfo
  {
    assert (property_builder != null);
    property_builder
  }
  
  /**
   * Performs attributes checks for properties definitions.
   */
  internal CheckAttributes () : void
  {
    // make sure no static indexers get defined
    when (IsStatic && IsIndexer)
      Message.fatal_error (loc, "indexer properties are not allowed to be static in " +
                           ToString ());
    
    // most of the checks are common with the methods and events:
    check_method_like_attributes ("property", "properties")
  }
}

public class NemerleMethod : NemerleMember, IMethod
{
  // FIXME: make these private
  mutable fun_kind : FunKind;
  public mutable fun_body : FunBody;
  public mutable fun_header : Fun_header;
  
  public mutable method_builder : System.Reflection.Emit.MethodBuilder;
  public mutable ctor_builder : System.Reflection.Emit.ConstructorBuilder;

  is_var_args : bool;

  public GetMethodBase () : MethodBase
  {
    def res =
      if (method_builder == null)
        (ctor_builder : MethodBase)
      else
        method_builder;
    assert (res != null);
    res
  }

  public override GetHandle () : MemberInfo
  {
    GetMethodBase ()
  }

  public GetConstructorBuilder () : Emit.ConstructorBuilder 
    implements IMethod.GetConstructorInfo
  {
    assert (ctor_builder != null);
    ctor_builder
  }
  
  public GetMethodBuilder () : Emit.MethodBuilder implements IMethod.GetMethodInfo
  {
    assert (method_builder != null, declaring_type.FullName + "." + Name);
    method_builder
  }

  public GetExternName () : option [string]
  {
    match (fun_body) {
      | FunBody.Extern (name) => Some (name)
      | _ => None ()
    }
  }

  public HasExternBody : bool
  {
    get { (fun_body is FunBody.Extern) }
  }

  public HasAbstractBody : bool
  {
    get { (fun_body is FunBody.Abstract) }
  }
  
  public Body : PT.PExpr {
    get {
      match (fun_body) {
        | FunBody.Parsed (e) => e
        | _ =>
          Message.fatal_error ("No parsetree body accessible")
      }
    }
    
    set { fun_body = FunBody.Parsed (value) }
  }

  public IsFinal : bool
  {
    get { attributes %&& NemerleAttributes.Sealed }
  }
  
  public IsVarArgs : bool
  {
    get { is_var_args }
  }

  public this (par : TypeBuilder, f : PT.ClassMember.Function)
  {
    this (par, f, false)
  }
  
  public this (par : TypeBuilder, f : PT.ClassMember.Function, is_property : bool)
  {
    base (par, f);

    // convert +, -, /= name to op_Addition equivalents
    match (OperatorLongName (this.name, List.Length (f.header.parms))) {
      | Some (long) => this.name = long
      | None => ()
    };
    
    // both override and abstract imply virtual
    when (attributes %&& (NemerleAttributes.Override 
                          %| NemerleAttributes.Abstract))
      attributes |= NemerleAttributes.Virtual;

    def (tenv', typarms) =
      if (IsStatic)
        declaring_type.BindTyparms (Tyvar.TEnv (), f.header.typarms)
      else
        declaring_type.BindTyparms (f.header.typarms);
   
    mutable types_to_check = [];

    foreach (tv in typarms)
      foreach (t in tv.GetConstraints ())
        types_to_check = t :: types_to_check;
        
    def bind (t : PT.PExpr) {
      def ty = par.BindType (tenv', t);
      types_to_check = ty :: types_to_check;
      ty
    };
    def mkparm (p : PT.Fun_parm) : Fun_parm {
      def name = p.name.GetName ();
      Fun_parm (loc = p.loc, name = name.Id, color = name.color, ty = bind (p.ty), 
                decl = null, modifiers = p.modifiers)
    };
    fun_body = f.body;
    
   /* We need to convert things like:
        foo (x : int * int) : int { bar }
            ===>
        foo (_1 : int, _2 : int) : int { def x = (_1, _2); bar }
      To be consistent. */

    def parms =
      match (f.header.parms) {
        | [parm] when ! is_property =>
          match (Tyutil.top_expand (par.BindType (tenv', parm.ty))) {
            | TType.Product (types) =>
              mutable modifiers = parm.modifiers;
              def mkdummyparm (t : TType) {
                // make the first argument intercept custom attributes
                def mods = modifiers;
                modifiers = Modifiers.Empty;
                Fun_parm (loc = parm.loc, 
                          name = Util.tmpname ("tupled"),
                          color = TypingContext.MacroColor,
                          ty = t, 
                          decl = null,
                          modifiers = mods)
              };
              def parms = List.Map (types, mkdummyparm);
              def refs = List.Map (parms, fun (fp : Fun_parm) {
                PT.PExpr.Ref (PT.Name (fp.name))
              });
              match (fun_body) {
                | FunBody.Parsed (body) =>
                  fun_body = FunBody.Parsed (<[ { def $(parm.name) =
                    ( .. $refs ); $body } ]>)
                | _ => Util.ice ()
              };
              parms
            | _ => List.Map (f.header.parms, mkparm)
          }
        | _ => List.Map (f.header.parms, mkparm)
      };

    when (! parms.IsEmpty) {
      def is_params (parm : Fun_parm) {
        List.Exists (parm.modifiers.custom_attrs, 
          fun (e) {
            | <[ System.ParamArrayAttribute ]>
            | <[ System.ParamArrayAttribute () ]>
            | <[ System.ParamArray ]>
            | <[ System.ParamArray () ]> => true
            | _ => false
          })
      }
      def (till_last, last) = List.DivideLast (parms);
      
      when (is_params (last)) {
        match (last.ty) {
          | TType.Array (_, 1) => is_var_args = true
          | _ =>
            Message.error ("only single-dimensional arrays are allowed in `params' parameters")
        }
      }

      when (List.Exists (till_last, is_params))
        Message.error ("`params' is allowed only on last parameter");
    }

    fun_kind =
      match ((f.kind, this.name)) {
        | (FunKind.Method ([]), ".ctor") when IsStatic =>
          this.m_has_been_used = true;
          this.name = ".cctor";
          FunKind.StaticConstructor ()
          
        | (FunKind.Method ([]), ".ctor")         => FunKind.Constructor ()
        | (FunKind.Method ([]), _) when IsStatic => FunKind.Function ()
        | (FunKind.Constructor, _) when IsStatic =>
          this.m_has_been_used = true;
          this.name = ".cctor";
          FunKind.StaticConstructor ()
          
        | (FunKind.Method (_ :: _), ".ctor") =>
          Message.error ("constructor cannot implement anything");
          FunKind.Constructor ()
          
        | (FunKind.Method (_ :: _), _) when IsStatic =>
          Message.error ("static function cannot implement anything");
          FunKind.Function ()
          
        | (x, _) => x
      };

    fun_header = Fun_header (
      ret_type = bind (f.header.ret_type),
      typarms = typarms,
      name = this.name,
      parms = parms,
      tenv = tenv',
      loc = f.header.loc);
      
    ty = Tyutil.fun_type (fun_header);
    
    kind = MemberKind.Method (this);

    // check it at the end, since error reporting routine there
    // needs us to be initialized to name us
    foreach (t in types_to_check)
      Tyutil.CheckAccessibility (this, accessibility, t);

    // don't warn about the unused `Main' methods...
    when (IsStatic && name == "Main")
      HasBeenUsed = true
  }

  public SetHeader (h : Fun_header) : void {
    fun_header = h;
    ty = Tyutil.fun_type (fun_header);
  }

  public GetHeader () : Fun_header
  { fun_header }

  public ReturnType : TType {
    get { fun_header.ret_type }
  }
  
  public GetFreshType () : TType
  { GetMemType () /- Tyvar.fresh_subst (fun_header.typarms) }

  public GetFunKind () : FunKind
  { fun_kind }

  public SetFunKind (kind : FunKind) : void
  { fun_kind = kind }
  
  static OperatorLongName (op : string, params_amount : int) : option [string]
  {
    if (params_amount == 2) {
      match (op) {
        | ":" => Some ("op_Implicit")
        | ":>" => Some ("op_Explicit")
        | "+" => Some ("op_Addition")
        | "-" => Some ("op_Subtraction")
        | "*" => Some ("op_Multiply")
        | "/" => Some ("op_Division")
        | "%" => Some ("op_Modulus")
        | "%^" => Some ("op_ExclusiveOr")
        | "%&" => Some ("op_BitwiseAnd")
        | "%|" => Some ("op_BitwiseOr")
        | "&&" => Some ("op_LogicalAnd")
        | "||" => Some ("op_LogicalOr")
        | "=" => Some ("op_Assign")
        | "<<" => Some ("op_LeftShift")
        | ">>" => Some ("op_RightShift")
        // | ">>*" => Some ("op_SignedRightShift")
        // | ">>&" => Some ("op_UnsignedRightShift")
        | "==" => Some ("op_Equality")
        | ">" => Some ("op_GreaterThan")
        | "<" => Some ("op_LessThan")
        | "!=" => Some ("op_Inequality")
        | ">=" => Some ("op_GreaterThanOrEqual")
        | "<=" => Some ("op_LessThanOrEqual")
        | "*=" => Some ("op_MultiplicationAssignment")
        | "-=" => Some ("op_SubtractionAssignment")
        | "^=" => Some ("op_ExclusiveOrAssignment")
        | "<<=" => Some ("op_LeftShiftAssignment")
        | "%=" => Some ("op_ModulusAssignment")
        | "+=" => Some ("op_AdditionAssignment")
        | "&=" => Some ("op_BitwiseAndAssignment")
        | "|=" => Some ("op_BitwiseOrAssignment")
        | "," => Some ("op_Comma")
        | "/=" => Some ("op_DivisionAssignment")
        | _ => None ()
      }
    }
    else if (params_amount == 1) {
      match (op) {
        | "+" when params_amount == 1 => Some ("op_UnaryPlus")
        | "-" => Some ("op_UnaryNegation")        
        | "--" => Some ("op_Decrement")
        | "++" => Some ("op_Increment")
        | "~" => Some ("op_OnesComplement")
        | "!" => Some ("op_LogicalNot")
        | _ => None ()
      }
    }
    else None ()
  }
  
  /**
   * Performs attributes checks for method definitions.
   */
  internal CheckAttributes () : void
  {
    when (declaring_type.IsInterface) {
      unless (HasAbstractBody)
        Message.error (loc, "interface method cannot have body");
      match (GetFunKind ()) {
        | FunKind.Method (_ :: _) =>
          Message.error (loc, "interface method cannot implement anything");
        | _ => ()
      }
    }
    
    // only allow static methods to be extern
    when (!IsStatic && HasExternBody)
      Message.fatal_error (loc, "only static methods are allowed to be extern in " +
                           ToString ());      

    // only allow abstract methods to have abstract bodies
    def meth_is_abstract = attributes %&& NemerleAttributes.Abstract;

    when (HasAbstractBody && !meth_is_abstract)
      Message.fatal_error (loc, "missing body of a non-abstract member in " +
                           ToString ());
                           
    // most of the checks are common with the properties and events:
    check_method_like_attributes ("method", "methods")
  }
}

public class NemerleEvent : NemerleMember, IEvent
{
  adder : NemerleMethod;
  remover : NemerleMethod;
  
  internal mutable event_builder : Emit.EventBuilder;
  internal storage_field : NemerleField;
  
  public this (par : TypeBuilder, f : PT.ClassMember.Event) {
    base (par, f);
    
    ty =
      if (IsStatic)
        par.BindType (Tyvar.TEnv (), f.ty)
      else
        par.BindType (f.ty);

    kind = MemberKind.Event (this);

    // prevent closurising `this' 
    def self = this;

    mutable meth_attrs = [];
    mutable field_attrs = [];
    def hacking_remove_meth_attrs (x) {
      | <[ ("method", $x) ]> => meth_attrs = x :: meth_attrs; false
      | _ => true;
    }
    def hacking_remove_field_attrs (x) {
      | <[ ("field", $x) ]> => field_attrs = x :: field_attrs; false
      | _ => true;
    }
    modifiers.custom_attrs = List.Filter (modifiers.custom_attrs, hacking_remove_meth_attrs);
    modifiers.custom_attrs = List.Filter (modifiers.custom_attrs, hacking_remove_field_attrs);
    meth_attrs = List.Rev (meth_attrs);
    field_attrs = List.Rev (field_attrs);

    def is_iface = declaring_type.IsInterface;

    def make_mfunction (pref, meth) {
      def attrs = Modifiers (self.attributes, meth_attrs);
      def nm = PT.Name (pref + self.Name);
      def f =
        if (meth == null)
          <[ decl: ..$attrs $(nm : name) (value : $(f.ty)) : void; ]>;
        else
          <[ decl: ..$attrs $(nm : name) (value : $(f.ty)) : void $meth ]>;
      f.loc = self.loc;
      f :> PT.ClassMember.Function
    };

    def (add, remove) =
      match ((f.add, f.remove)) {
        | (Some (add), Some (remove)) =>
          (add, remove)
        | (None, None) =>
          if (is_iface)
            (make_mfunction ("add_", null),
             make_mfunction ("remove_", null))
          else {
            def mods = 
              NemerleAttributes.Private %| NemerleAttributes.Mutable %|
              (attributes %& NemerleAttributes.Static);
              
            def field_name = PT.Name (Util.tmpname (f.name.GetName ().Id));

            def field = PT.ClassMember.Field (f.loc, PT.Splicable.Name (field_name),
                                              Modifiers (mods, field_attrs), f.ty);
            storage_field = NemerleField (par, (field :> PT.ClassMember.Field));
            declaring_type.AddMember (storage_field);
            def lock_expr =
              if (IsStatic) <[
                typeof ($(PT.Name (NString.Split (declaring_type.FullName, array ['.'])) : name))
              ]>
              else <[ this ]>;

            // according to C# spec, we have to add locks
            // http://www.jaggersoft.com/csharp_standard/17.7.1.htm
            (make_mfunction ("add_", <[
               lock ($lock_expr) { $(field_name : name) += $("value" : dyn) }
             ]>),
             make_mfunction ("remove_", <[
               lock ($lock_expr) { $(field_name : name) -= $("value" : dyn) }
             ]>))
          }
        | _ =>
          Message.fatal_error ("events must have both `add' and `remove' methods")
      };
    
    def make_method (mfunc : PT.ClassMember.Function) {
      self.UpdateEmbeddedModifiers (self.attributes, ref mfunc.modifiers.mods);
      def meth' = NemerleMethod (self.declaring_type, mfunc);
      self.declaring_type.AddMember (meth');
      meth'
    };

    adder = make_method (add);
    remover = make_method (remove);

    Tyutil.CheckAccessibility (this, accessibility, ty);
  }

  public GetEventInfo () : EventInfo
  {
     assert (false);
  }

  public override GetHandle () : MemberInfo
  {
    // for some reason !(event_builder)...
    /// lame spec: SRE.EventBuilder is not MemberInfo or event EventInfo
    /// but we return null to do not spoil API
    null
  }

  public GetAdder () : IMethod
  {
    adder
  }
  
  public GetRemover () : IMethod
  {
    remover
  }
  
  /**
   * Performs attributes checks for event definition.
   */
  internal CheckAttributes () : void
  {
    // most of the checks are common with the properties and methods:
    check_method_like_attributes ("event", "events")
  }
}


}
