/*
 * Copyright (c) 2003, 2004 The University of Wroclaw.
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions
 * are met:
 *    1. Redistributions of source code must retain the above copyright
 *       notice, this list of conditions and the following disclaimer.
 *    2. Redistributions in binary form must reproduce the above copyright
 *       notice, this list of conditions and the following disclaimer in the
 *       documentation and/or other materials provided with the distribution.
 *    3. The name of the University may not be used to endorse or promote
 *       products derived from this software without specific prior
 *       written permission.
 * 
 * THIS SOFTWARE IS PROVIDED BY THE UNIVERSITY ``AS IS'' AND ANY EXPRESS OR
 * IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES
 * OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN
 * NO EVENT SHALL THE UNIVERSITY BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
 * SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED
 * TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR
 * PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF
 * LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING
 * NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS
 * SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 */

namespace Nemerle
{
  /**
   * Helper functions for handling the standard input stream
   */
  public module IO
  {
    public class InvalidInput : System.Exception
    {
      public this () {}
    }

    public ConsumeWhiteSpace () : void
    {
      while ({ def ch = System.Console.In.Peek (); ch >= 0 && 
        System.Char.IsWhiteSpace (System.Convert.ToChar (ch)) }) {
        def _ = System.Console.Read ();
        ()
      }
    }

    /**
     * Read digital characters returning integer value of read string
     */
    public ReadInt () : int 
    {
      def first = System.Console.Read ();
      if (first >= 0) {
        def firstc = System.Convert.ToChar (first);
          
        if (System.Char.IsDigit (firstc) || firstc == '-' || firstc == '+') {
          mutable buf = System.Text.StringBuilder();
          buf = buf.Append (firstc);
            
          while ({ def ch = System.Console.In.Peek (); ch >= 0 && 
            System.Char.IsDigit (System.Convert.ToChar (ch))}) {
                 buf = buf.Append (System.Convert.ToChar (System.Console.Read ()))
          };
              
          System.Int32.Parse (buf.ToString ());
        }
        else throw InvalidInput ()
      }
      else
        throw InvalidInput ()
    }

    
    /**
     * Reads characters up to first occurence of whitespace and returns 
     * read string
     */
    public ReadString () : string
    {
      mutable buf = System.Text.StringBuilder();
            
      while ( { def ch = System.Console.In.Peek (); ch >= 0 && 
        !System.Char.IsWhiteSpace (System.Convert.ToChar (ch)) })
      {
        buf = buf.Append (System.Convert.ToChar (System.Console.Read ()))
      }

      buf.ToString ()
    }

    
    /**
     * Checks if input consist exactly of given string (all consecutive 
     * sequences of whitespaces in string and input are treated as single 
     * whitespace character, thus they match each other)
     */
    public CheckInput (str : string) : void
    {
      def n = str.Length;
      mutable i = 0;

      while (i < n)
      {
        if (System.Char.IsWhiteSpace (str [i]))
        {
          ConsumeWhiteSpace ();
          
          while (i < n && System.Char.IsWhiteSpace (str [i]))
            ++i
        } 
        else
        {
          def inp = System.Console.Read ();

          when (inp >= 0 && System.Convert.ToChar (inp) == str [i])
            throw InvalidInput ()
        }

        ++i
      }
    }
  }
}
