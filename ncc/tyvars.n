/*
 * Copyright (c) 2003, 2004 The University of Wroclaw.
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions
 * are met:
 *    1. Redistributions of source code must retain the above copyright
 *       notice, this list of conditions and the following disclaimer.
 *    2. Redistributions in binary form must reproduce the above copyright
 *       notice, this list of conditions and the following disclaimer in the
 *       documentation and/or other materials provided with the distribution.
 *    3. The name of the University may not be used to endorse or promote
 *       products derived from this software without specific prior
 *       written permission.
 * 
 * THIS SOFTWARE IS PROVIDED BY THE UNIVERSITY ``AS IS'' AND ANY EXPRESS OR
 * IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES
 * OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN
 * NO EVENT SHALL THE UNIVERSITY BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
 * SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED
 * TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR
 * PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF
 * LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING
 * NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS
 * SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 */

using Nemerle.Collections;
using Nemerle.Utility;

using Nemerle.Compiler;
using Nemerle.Compiler.Tyops;
using Nemerle.Compiler.Typedtree;
using Nemerle.Compiler.Tyvar;

using PT = Nemerle.Compiler.Parsetree;

namespace Nemerle.Compiler {

public module Tyvar {

  mutable tyvars : SystemMap <int, T>;

  store_tv (t : T) : void
  {
    tyvars = tyvars.Add (t.id, t)
  }
  
  public class T
  {
    public name : string;
    public mutable constraints : list <Type>;
    public id : int;
    public mutable value : TvValue;
    public mutable is_fixed : bool;

    public this (n : string)
    {
      this.name = n;
      this.constraints = [];
      this.is_fixed = false;
      this.id = Util.next_id ();
      this.value = TV_unspecified ();
      store_tv (this);
    }

    public this (t : T)
    {
      this.name = t.name;
      this.constraints = t.constraints;        // to be is_fixed
      this.value = TV_free ();
      this.is_fixed = true;
      this.id = Util.next_id ();
      store_tv (this);
    }

    public this ()
    {
      this.id = Util.next_id ();
      this.name = "_N_fv" + this.id.ToString ();
      this.constraints = [];
      this.value = TV_free ();
      this.is_fixed = true;
      store_tv (this);
    }

    public add_constraint (ty : Type) : void
    {
      if (this.is_fixed)
        Message.error ("this type variable cannot get any new constraints here")
      else ();
      this.constraints = ty :: this.constraints
    }
  }

  public class TEnv
  {
    tyvars : NemerleMap <PT.Name, T>;
    
    private this (tv : NemerleMap <PT.Name, T>)
    {
      this.tyvars = tv;
    }

    public this ()
    {
      this.tyvars = NemerleMap ();
    }

    /** Perform typing of Parsetree type to Typedtree type, looking up type constructors 
        in given global environment (with accessibility information implied by given current
        TypeInfo) and type variables in current instance.
        Is [check_parms] is true, then we check is type substituted for a given type
        variable conforms to all of its constraints. It should be true in general, but
        is false when we don't know the subtyping relations yet (during scanning of
        global types).
     */            
    public bind (env : GlobalEnv, curtc : Tycon, t : PT.Type, check_parms : bool) : Type
    {
      def f (t : PT.Type) : Type {
        match (t) {
          | PT.T_ref (t) => T_ref (f (t))
          | PT.T_out (t) => T_out (f (t))
          | PT.T_array (t, PT.SI_int (rank)) => T_array (f (t), rank)
          | PT.T_fun (x, y) => T_fun (f (x), f (y))
          | PT.T_void => T_void ()
          | PT.T_prod (a) => T_prod (List.Map (a, f))
          | PT.T_app (PT.SS_string (nm), args) =>
            match (this.tyvars.Find (nm)) {
              | Some (tv) =>
                match (args) {
                  | [] => ()
                  | _ => Message.error ("type variable `" + Util.qid_of_list (nm.idl) + 
                                        "' supplied with arguments")
                };
                T_var (tv)
              | None =>
                def env = nm.GetEnv (env);
                def ti = env.GetType (nm.idl, curtc);
                def args = List.Map (args, f);

                when (check_parms) {
                  ignore (ti.MakeSubst (args))
                };
                
                T_app (ti, args)
            }

          | PT.T_typed (body) =>
            body
            
          | PT.T_infer =>
            // this is our entire type inference... 
            free_variable ()

          | PT.T_app (_, _) =>
            Util.ice ("Spliced string survived to typing")
          | PT.T_spliced =>
            Util.ice ("Spliced type survived to typying.")
          | PT.T_array => Message.fatal_error ("$ splice outside of <[ .. quotation ]>")
          | PT.T_type_list =>
            Util.ice ("Type arguments list survived to typying.")
        }
      };
      f (t)
    }
    
    public add_typarms (env : GlobalEnv, tp : PT.Typarms,
                        curtc : Tycon, check_parms : bool) : TEnv * list <T>
    {
      def name_of_tv (tv) {
        def (name, color) = tv;
        PT.Name ([name], color, null)
      }
      def loop (tv : string * int, acc) {
        def (map, the_list) = acc;
        def tv_obj = T (Pair.First (tv));
        ((map : NemerleMap <PT.Name, T>).Replace (name_of_tv (tv), tv_obj),
         tv_obj :: the_list)
      }
      def (m, l) = List.FoldLeft (tp.tyvars, (this.tyvars, []), loop);
      def tenv = TEnv (m);
      def add_constraint (c : PT.Constraint) {
        match (m.Find (name_of_tv (c.tyvar))) {
          | Some (tv) =>
            tv.add_constraint (tenv.bind (env, curtc, c.ty, check_parms))
          | None =>
            Message.error ("unbound type variable `" + Pair.First (c.tyvar) + "' in constraint")
        }
      };
      List.Iter (tp.constraints, add_constraint);
      def fixate (tv : T) : void { tv.is_fixed = true };
      List.Iter (l, fixate);
      (tenv, List.Rev (l))
    }
  }

  private lookup (id : int) : T 
  {
    match (tyvars.Find (id)) {
      | Some (x) => x
      | None => Util.ice ()
    }
  }

  public global_store (s : Subst) : void 
  {
    s.Iter (fun (id, t) { lookup (id).value = t })
  }

  public fresh_subst (parms : list <T>) : Subst
  {
    def loop (proto : T, ctx : Subst * list <T>) {
      def (sub, vars) = ctx;
      def tv = Tyvar.T (proto);
      (sub.Add (proto.id, TV_exactly (T_var (tv))), tv :: vars)
    };
    def (sub, vars) = List.FoldLeft (parms, (SystemMap (), []), loop);
    def sub_constraints (tv : T) {
      tv.constraints = List.Map (tv.constraints, fun (t : Type) : Type { t /- sub })
    };
    List.Iter (vars, sub_constraints);
    sub
  }

  public free_variable () : Type { T_var (T ()) }

  public constraints (tv : T) : list <Type> { tv.constraints }

  this ()
  {
    tyvars = SystemMap ();
  }


} // module Tyvar
} // end ns
