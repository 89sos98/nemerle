using System;
using SCG = System.Collections.Generic;

using Nemerle.Assertions;
using Nemerle.Utility;
using Nemerle.Peg;

namespace CSharpParser
{
  [Record]
  public class PropertyAccessor : Located
  {
    [Accessor] attributes : list[CustomAttribute];
    [Accessor] modifiers : list[Modifier];
    [Accessor] body : Statement;
  }

  [Record]
  public variant ConstructorInitializer : Located
  {
    | Default
    | This { args : list[FunctionArgument]; }
    | Base { args : list[FunctionArgument]; }
  }

  [Record]
  public variant TypeMember : Located
  {
    | Constant {
      name        : Expr;
      memberType  : TypeRef;
      value       : Expr;
    }
    | Field {
      name          : Expr;
      memberType    : TypeRef;
      initialValue  : VariableInitializer; // can be null
    }
    | FixedSizeBuffer {
      name        : Expr;
      elementType : TypeRef;
      size        : Expr;
    }
    | Method {
      name              : Expr;
      returnType        : TypeRef;
      formalParameters  : list[FormalParameter];
      typeParameters    : list[TypeParameter];
      constraints       : list[TypeParameterConstraintClause];
      body              : Statement;
    }
    | Property {
      name : Expr;
      memberType : TypeRef;
      getter : PropertyAccessor; //can be null
      setter : PropertyAccessor; //can be null
    }
    | Event {
      name : Expr;
      memberType : TypeRef;
      adder   : PropertyAccessor; //can be null
      remover : PropertyAccessor; //can be null
      initialValue  : VariableInitializer; // must be null
    }
    | Indexer {
      name : Expr;
      memberType        : TypeRef;
      formalParameters  : list[FormalParameter];
      getter : PropertyAccessor; //can be null
      setter : PropertyAccessor; //can be null
    }
    | Operator {
      op : Identifier;
      returnType : TypeRef;
      formalParameters  : list[FormalParameter];
      body              : Statement;
    }
    | Constructor {
      name : Expr;
      formalParameters : list[FormalParameter];
      init : ConstructorInitializer;
      body : Statement;
    }
    | Destructor {
      name : Expr;
      formalParameters : list[FormalParameter]; // must be empty list
      body : Statement;
    }
    | EnumOption {
      name : Identifier;
      value : Expr;  // can be null
    }
    | TypeDeclaration {
      decl : CSharpParser.TypeDeclaration;
    }

    [Accessor] attributes : list[CustomAttribute];
    [Accessor] modifiers : list[Modifier];
  }
}