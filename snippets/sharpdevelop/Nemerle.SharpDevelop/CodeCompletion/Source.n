using System;
using SCG = System.Collections.Generic;

using ICSharpCode.Core;
using ICSharpCode.SharpDevelop.Dom;
using ICSharpCode.SharpDevelop.Project;
using ICSharpCode.TextEditor.Document;

using Nemerle.Assertions;
using Nemerle.Completion2;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Compiler.Utils.Async;
using Nemerle.Utility;

namespace Nemerle.SharpDevelop.CodeCompletion {

    public class Source : ISource, IDisposable {

        public this(context : EngineContext, item : FileProjectItem) {
            this.context = context;
            this.item = item;
            this.file_index = Location.GetFileIndex(item.FileName);
            this.completion_func = System.Func(Completion);

            RefreshContent();
        }

        mutable content : ISource;

        [Accessor] context : EngineContext;

        [Accessor] item : FileProjectItem;

        [Accessor] file_index : int;

        mutable document : IDocument = null;

        internal Document : IDocument {
            get { document } 
            set {
                unless(ReferenceEquals(document, value)) {
                    document = value;
                    RefreshContent();
                }
            }
        }

        internal Declarations : array[TopDeclaration] {
            get;
            set;
        }

        private RefreshContent() : void {
            DisposeContent();
            content =
                if(null == document)
                    FileNemerleSource(file_index)
                else
                    DocumentNemerleSource(this, document);
        }

        mutable update_compile_unit_async : AsyncRequest = null;

        internal UpdateCompileUnit(
            newEndLine    : int, newEndChar    : int,
            oldEndLine    : int, oldEndChar    : int,
            startLine     : int, startChar     : int) : void {

            def queue = RelocationRequestsQueue;
            unless(queue == null) {
                RelocationQueue.AddRelocationRequest(
                    queue, FileIndex, CurrentVersion,
                    newEndLine, newEndChar,
                    oldEndLine, oldEndChar,
                    startLine,  startChar);
            }

            when(update_compile_unit_async == null || update_compile_unit_async.IsCompleted)
                update_compile_unit_async = context.Engine.BeginUpdateCompileUnit(this);
        }

        internal ParseCompileUnit(projectContent : IProjectContent) : NemerleCompilationUnit {
            CompileUnit = context.Engine.ParseCompileUnit(this);
            compilation_unit = NemerleCompilationUnit(projectContent, this);
            compilation_unit
        }

        public CompileUnit : CompileUnit {
            get;
            set;
        }

        [Accessor] mutable compilation_unit : NemerleCompilationUnit = null;

        public CurrentVersion : int {
            get { content.CurrentVersion }
        }
        
        public RelocationRequestsQueue : SCG.List[RelocationRequest] {
            get { content.RelocationRequestsQueue }
        }

        public LineCount : int { 
            get { content.LineCount }
        }

        public LockWrite() : void {
            content.LockWrite()
        }

        public UnlockWrite() : void {
            content.UnlockWrite()
        }

        public LockReadWrite() : void {
            content.LockReadWrite()
        }

        public UnlocReadkWrite() : void {
            content.UnlocReadkWrite()
        }

        public GetTextCurrentVersionAndFileIndex() : string * int * int {
            content.GetTextCurrentVersionAndFileIndex()
        }

        public SetRegions(regions : SCG.IList[RegionInfo], sourceVersion : int) : void {
            content.SetRegions(regions, sourceVersion)
        }

        public SetTopDeclarations(topDeclarations : array[TopDeclaration]) : void {
            content.SetTopDeclarations(topDeclarations);
            Declarations = topDeclarations;
        }

        public GetText() : string {
            content.GetText()
        }

        public GetRegion(lineStart : int, colStart : int, lineEnd : int, colEnd : int) : string {
            content.GetRegion(lineStart, colStart, lineEnd, colEnd)
        }

        public GetRegion(location : Location) : string {
            content.GetRegion(location)
        }

        public GetLine(line : int) : string {
            content.GetLine(line)
        }

        public GetPositionOfLineIndex(line : int, col : int) : int {
            content.GetPositionOfLineIndex(line, col)
        }

        public GetLineIndexOfPosition(pos : int) : int * int {
            content.GetLineIndexOfPosition(pos)
        }

        completion_func : System.Func[int, int, array[CompletionElem]];

        public Completion(line : int, column : int) : array[CompletionElem] {
            Context.Engine.Completion(this, line, column)
        }

        public BeginCompletionAsync(line : int, column : int, callback : AsyncCallback = null, obj : object = null) : IAsyncResult {
            completion_func.BeginInvoke(line, column, callback, obj)
        }

        public EndCompletionAsync(asyncResult : IAsyncResult) : array[CompletionElem] {
            completion_func.EndInvoke(asyncResult)
        }

        #region Disposable

        [Accessor] mutable disposed : bool = false;

        private DisposeContent() : void {
            match(content) {
                | content is IDisposable => content.Dispose();
                | _ => ();
            }
        }


        public Dispose() : void {
            unless(disposed) {
                DisposeContent();
                disposed = true;
            }
        }

        protected override Finalize() : void {
            Dispose();
        }

        #endregion

    }

}